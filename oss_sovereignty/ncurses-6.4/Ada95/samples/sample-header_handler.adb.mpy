{
  "module_name": "sample-header_handler.adb",
  "hash_id": "691575df9d8973651e8cf7ef4b0760769890c6960f7eb7d8951b4ab125c1b585",
  "original_prompt": "Ingested from ncurses-6.4/Ada95/samples/sample-header_handler.adb",
  "human_readable_source": "------------------------------------------------------------------------------\n--                                                                          --\n--                       GNAT ncurses Binding Samples                       --\n--                                                                          --\n--                            Sample.Header_Handler                         --\n--                                                                          --\n--                                 B O D Y                                  --\n--                                                                          --\n------------------------------------------------------------------------------\n-- Copyright 2020 Thomas E. Dickey                                          --\n-- Copyright 1998-2011,2014 Free Software Foundation, Inc.                  --\n--                                                                          --\n-- Permission is hereby granted, free of charge, to any person obtaining a  --\n-- copy of this software and associated documentation files (the            --\n-- \"Software\"), to deal in the Software without restriction, including      --\n-- without limitation the rights to use, copy, modify, merge, publish,      --\n-- distribute, distribute with modifications, sublicense, and/or sell       --\n-- copies of the Software, and to permit persons to whom the Software is    --\n-- furnished to do so, subject to the following conditions:                 --\n--                                                                          --\n-- The above copyright notice and this permission notice shall be included  --\n-- in all copies or substantial portions of the Software.                   --\n--                                                                          --\n-- THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS  --\n-- OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF               --\n-- MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.   --\n-- IN NO EVENT SHALL THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,   --\n-- DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR    --\n-- OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR    --\n-- THE USE OR OTHER DEALINGS IN THE SOFTWARE.                               --\n--                                                                          --\n-- Except as contained in this notice, the name(s) of the above copyright   --\n-- holders shall not be used in advertising or otherwise to promote the     --\n-- sale, use or other dealings in this Software without prior written       --\n-- authorization.                                                           --\n------------------------------------------------------------------------------\n--  Author:  Juergen Pfeifer, 1996\n--  Version Control\n--  $Revision: 1.21 $\n--  $Date: 2020/02/02 23:34:34 $\n--  Binding Version 01.00\n------------------------------------------------------------------------------\nwith Ada.Calendar; use Ada.Calendar;\nwith Terminal_Interface.Curses.Text_IO.Integer_IO;\nwith Sample.Manifest; use Sample.Manifest;\n\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Integer_IO);\n\n--  This package handles the painting of the header line of the screen.\n--\npackage body Sample.Header_Handler is\n\n   package Int_IO is new\n     Terminal_Interface.Curses.Text_IO.Integer_IO (Integer);\n   use Int_IO;\n\n   Header_Window : Window := Null_Window;\n\n   Display_Hour  : Integer := -1; -- hour last displayed\n   Display_Min   : Integer := -1; -- minute last displayed\n   Display_Day   : Integer := -1; -- day last displayed\n   Display_Month : Integer := -1; -- month last displayed\n\n   --  This is the routine handed over to the curses library to be called\n   --  as initialization routine when ripping of the header lines from\n   --  the screen. This routine must follow C conventions.\n   function Init_Header_Window (Win     : Window;\n                                Columns : Column_Count) return Integer;\n   pragma Convention (C, Init_Header_Window);\n\n   procedure Internal_Update_Header_Window (Do_Update : Boolean);\n\n   --  The initialization must be called before Init_Screen. It steals two\n   --  lines from the top of the screen.\n   procedure Init_Header_Handler\n   is\n   begin\n      Rip_Off_Lines (2, Init_Header_Window'Access);\n   end Init_Header_Handler;\n\n   procedure N_Out (N : Integer);\n\n   --  Emit a two digit number and ensure that a leading zero is generated if\n   --  necessary.\n   procedure N_Out (N : Integer)\n   is\n   begin\n      if N < 10 then\n         Add (Header_Window, '0');\n         Put (Header_Window, N, 1);\n      else\n         Put (Header_Window, N, 2);\n      end if;\n   end N_Out;\n\n   --  Paint the header window. The input parameter is a flag indicating\n   --  whether or not the screen should be updated physically after painting.\n   procedure Internal_Update_Header_Window (Do_Update : Boolean)\n   is\n      type Month_Name_Array is\n         array (Month_Number'First .. Month_Number'Last) of String (1 .. 9);\n\n      Month_Names : constant Month_Name_Array :=\n        (\"January  \",\n         \"February \",\n         \"March    \",\n         \"April    \",\n         \"May      \",\n         \"June     \",\n         \"July     \",\n         \"August   \",\n         \"September\",\n         \"October  \",\n         \"November \",\n         \"December \");\n\n      Now    : constant Time         := Clock;\n      Sec    : constant Integer      := Integer (Seconds (Now));\n      Hour   : constant Integer      := Sec / 3600;\n      Minute : constant Integer      := (Sec - Hour * 3600) / 60;\n      Mon    : constant Month_Number := Month (Now);\n      D      : constant Day_Number   := Day (Now);\n   begin\n      if Header_Window /= Null_Window then\n         if Minute /= Display_Min\n           or else Hour /= Display_Hour\n           or else Display_Day /= D\n           or else Display_Month /= Mon\n         then\n            Move_Cursor (Header_Window, 0, 0);\n            N_Out (D); Add (Header_Window, '.');\n            Add (Header_Window, Month_Names (Mon));\n            Move_Cursor (Header_Window, 1, 0);\n            N_Out (Hour); Add (Header_Window, ':');\n            N_Out (Minute);\n            Display_Min   := Minute;\n            Display_Hour  := Hour;\n            Display_Month := Mon;\n            Display_Day   := D;\n            Refresh_Without_Update (Header_Window);\n            if Do_Update then\n               Update_Screen;\n            end if;\n         end if;\n      end if;\n   end Internal_Update_Header_Window;\n\n   --  This routine is called in the keyboard input timeout handler. So it will\n   --  periodically update the header line of the screen.\n   procedure Update_Header_Window\n   is\n   begin\n      Internal_Update_Header_Window (True);\n   end Update_Header_Window;\n\n   function Init_Header_Window (Win     : Window;\n                                Columns : Column_Count) return Integer\n   is\n      Title  : constant String := \"Ada 95 ncurses Binding Sample\";\n      Pos    : Column_Position;\n   begin\n      Header_Window := Win;\n      if Win /= Null_Window then\n         if Has_Colors then\n            Set_Background (Win => Win,\n                            Ch  => (Ch    => ' ',\n                                    Color => Header_Color,\n                                    Attr  => Normal_Video));\n            Set_Character_Attributes (Win   => Win,\n                                      Attr  => Normal_Video,\n                                      Color => Header_Color);\n            Erase (Win);\n         end if;\n         Leave_Cursor_After_Update (Win, True);\n         Pos := Columns - Column_Position (Title'Length);\n         Add (Win, 0, Pos / 2, Title);\n         --  In this phase we must not allow a physical update, because\n         --  ncurses is not properly initialized at this point.\n         Internal_Update_Header_Window (False);\n         return 0;\n      else\n         return -1;\n      end if;\n   end Init_Header_Window;\n\nend Sample.Header_Handler;\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}