{
  "module_name": "sample-function_key_setting.adb",
  "hash_id": "5da12365e5368d8691ef235287a978ea49a922cc4d8751ad29835d31afa024e4",
  "original_prompt": "Ingested from ncurses-6.4/Ada95/samples/sample-function_key_setting.adb",
  "human_readable_source": "------------------------------------------------------------------------------\n--                                                                          --\n--                       GNAT ncurses Binding Samples                       --\n--                                                                          --\n--                         Sample.Function_Key_Setting                      --\n--                                                                          --\n--                                 B O D Y                                  --\n--                                                                          --\n------------------------------------------------------------------------------\n-- Copyright 2020 Thomas E. Dickey                                          --\n-- Copyright 1998-2009,2011 Free Software Foundation, Inc.                  --\n--                                                                          --\n-- Permission is hereby granted, free of charge, to any person obtaining a  --\n-- copy of this software and associated documentation files (the            --\n-- \"Software\"), to deal in the Software without restriction, including      --\n-- without limitation the rights to use, copy, modify, merge, publish,      --\n-- distribute, distribute with modifications, sublicense, and/or sell       --\n-- copies of the Software, and to permit persons to whom the Software is    --\n-- furnished to do so, subject to the following conditions:                 --\n--                                                                          --\n-- The above copyright notice and this permission notice shall be included  --\n-- in all copies or substantial portions of the Software.                   --\n--                                                                          --\n-- THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS  --\n-- OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF               --\n-- MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.   --\n-- IN NO EVENT SHALL THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,   --\n-- DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR    --\n-- OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR    --\n-- THE USE OR OTHER DEALINGS IN THE SOFTWARE.                               --\n--                                                                          --\n-- Except as contained in this notice, the name(s) of the above copyright   --\n-- holders shall not be used in advertising or otherwise to promote the     --\n-- sale, use or other dealings in this Software without prior written       --\n-- authorization.                                                           --\n------------------------------------------------------------------------------\n--  Author:  Juergen Pfeifer, 1996\n--  Version Control\n--  $Revision: 1.16 $\n--  $Date: 2020/02/02 23:34:34 $\n--  Binding Version 01.00\n------------------------------------------------------------------------------\nwith Ada.Unchecked_Deallocation;\nwith Sample.Manifest; use  Sample.Manifest;\n\n--  This package implements a simple stack of function key label environments.\n--\npackage body Sample.Function_Key_Setting is\n\n   Max_Label_Length : constant Positive := 8;\n   Number_Of_Keys   : Label_Number := Label_Number'Last;\n   Justification    : Label_Justification := Left;\n\n   subtype Label is String (1 .. Max_Label_Length);\n   type Label_Array is array (Label_Number range <>) of Label;\n\n   type Key_Environment (N : Label_Number := Label_Number'Last);\n   type Env_Ptr is access Key_Environment;\n   pragma Controlled (Env_Ptr);\n\n   type String_Access is access String;\n   pragma Controlled (String_Access);\n\n   Active_Context : String_Access := new String'(\"MAIN\");\n   Active_Notepad : Panel := Null_Panel;\n\n   type Key_Environment  (N : Label_Number := Label_Number'Last) is\n      record\n         Prev    : Env_Ptr;\n         Help    : String_Access;\n         Notepad : Panel;\n         Labels  : Label_Array (1 .. N);\n      end record;\n\n   procedure Release_String is\n     new Ada.Unchecked_Deallocation (String,\n                                     String_Access);\n\n   procedure Release_Environment is\n      new Ada.Unchecked_Deallocation (Key_Environment,\n                                      Env_Ptr);\n\n   Top_Of_Stack : Env_Ptr := null;\n\n   procedure Push_Environment (Key   : String;\n                               Reset : Boolean := True)\n   is\n      P : constant Env_Ptr := new Key_Environment (Number_Of_Keys);\n   begin\n      --  Store the current labels in the environment\n      for I in 1 .. Number_Of_Keys loop\n         Get_Soft_Label_Key (I, P.all.Labels (I));\n         if Reset then\n            Set_Soft_Label_Key (I, \" \");\n         end if;\n      end loop;\n      P.all.Prev := Top_Of_Stack;\n      --  now store active help context and notepad\n      P.all.Help := Active_Context;\n      P.all.Notepad := Active_Notepad;\n      --  The notepad must now vanish and the new notepad is empty.\n      if P.all.Notepad /= Null_Panel then\n         Hide (P.all.Notepad);\n         Update_Panels;\n      end if;\n      Active_Notepad := Null_Panel;\n      Active_Context := new String'(Key);\n\n      Top_Of_Stack := P;\n      if Reset then\n         Refresh_Soft_Label_Keys_Without_Update;\n      end if;\n   end Push_Environment;\n\n   procedure Pop_Environment\n   is\n      P : Env_Ptr := Top_Of_Stack;\n   begin\n      if Top_Of_Stack = null then\n         raise Function_Key_Stack_Error;\n      else\n         for I in 1 .. Number_Of_Keys loop\n            Set_Soft_Label_Key (I, P.all.Labels (I), Justification);\n         end loop;\n         pragma Assert (Active_Context /= null);\n         Release_String (Active_Context);\n         Active_Context := P.all.Help;\n         Refresh_Soft_Label_Keys_Without_Update;\n         Notepad_To_Context (P.all.Notepad);\n         Top_Of_Stack := P.all.Prev;\n         Release_Environment (P);\n      end if;\n   end Pop_Environment;\n\n   function Context return String\n   is\n   begin\n      if Active_Context /= null then\n         return Active_Context.all;\n      else\n         return \"\";\n      end if;\n   end Context;\n\n   function Find_Context (Key : String) return Boolean\n   is\n      P : Env_Ptr := Top_Of_Stack;\n   begin\n      if Active_Context.all = Key then\n         return True;\n      else\n         loop\n            exit when P = null;\n            if P.all.Help.all = Key then\n               return True;\n            else\n               P := P.all.Prev;\n            end if;\n         end loop;\n         return False;\n      end if;\n   end Find_Context;\n\n   procedure Notepad_To_Context (Pan : Panel)\n   is\n      W : Window;\n   begin\n      if Active_Notepad /= Null_Panel then\n         W := Get_Window (Active_Notepad);\n         Clear (W);\n         Delete (Active_Notepad);\n         Delete (W);\n      end if;\n      Active_Notepad := Pan;\n      if Pan /= Null_Panel then\n         Top  (Pan);\n      end if;\n      Update_Panels;\n      Update_Screen;\n   end Notepad_To_Context;\n\n   procedure Initialize (Mode : Soft_Label_Key_Format := PC_Style;\n                         Just : Label_Justification := Left)\n   is\n   begin\n      case Mode is\n         when PC_Style .. PC_Style_With_Index\n           => Number_Of_Keys := 12;\n         when others\n           => Number_Of_Keys := 8;\n      end case;\n      Init_Soft_Label_Keys (Mode);\n      Justification := Just;\n   end Initialize;\n\n   procedure Default_Labels\n   is\n   begin\n      Set_Soft_Label_Key (FKEY_QUIT, \"Quit\");\n      Set_Soft_Label_Key (FKEY_HELP, \"Help\");\n      Set_Soft_Label_Key (FKEY_EXPLAIN, \"Keys\");\n      Refresh_Soft_Label_Keys_Without_Update;\n   end Default_Labels;\n\n   function Notepad_Window return Window\n   is\n   begin\n      if Active_Notepad /= Null_Panel then\n         return Get_Window (Active_Notepad);\n      else\n         return Null_Window;\n      end if;\n   end Notepad_Window;\n\nend Sample.Function_Key_Setting;\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}