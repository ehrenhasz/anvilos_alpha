{
  "module_name": "sample-keyboard_handler.adb",
  "hash_id": "12dd7918c8aa73966a8c8ecfd623b002c41a6fbf85ff0aa52373843cea5878dc",
  "original_prompt": "Ingested from ncurses-6.4/Ada95/samples/sample-keyboard_handler.adb",
  "human_readable_source": "------------------------------------------------------------------------------\n--                                                                          --\n--                       GNAT ncurses Binding Samples                       --\n--                                                                          --\n--                            Sample.Keyboard_Handler                       --\n--                                                                          --\n--                                 B O D Y                                  --\n--                                                                          --\n------------------------------------------------------------------------------\n-- Copyright 2020 Thomas E. Dickey                                          --\n-- Copyright 1998-2006,2011 Free Software Foundation, Inc.                  --\n--                                                                          --\n-- Permission is hereby granted, free of charge, to any person obtaining a  --\n-- copy of this software and associated documentation files (the            --\n-- \"Software\"), to deal in the Software without restriction, including      --\n-- without limitation the rights to use, copy, modify, merge, publish,      --\n-- distribute, distribute with modifications, sublicense, and/or sell       --\n-- copies of the Software, and to permit persons to whom the Software is    --\n-- furnished to do so, subject to the following conditions:                 --\n--                                                                          --\n-- The above copyright notice and this permission notice shall be included  --\n-- in all copies or substantial portions of the Software.                   --\n--                                                                          --\n-- THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS  --\n-- OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF               --\n-- MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.   --\n-- IN NO EVENT SHALL THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,   --\n-- DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR    --\n-- OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR    --\n-- THE USE OR OTHER DEALINGS IN THE SOFTWARE.                               --\n--                                                                          --\n-- Except as contained in this notice, the name(s) of the above copyright   --\n-- holders shall not be used in advertising or otherwise to promote the     --\n-- sale, use or other dealings in this Software without prior written       --\n-- authorization.                                                           --\n------------------------------------------------------------------------------\n--  Author:  Juergen Pfeifer, 1996\n--  Version Control\n--  $Revision: 1.17 $\n--  $Date: 2020/02/02 23:34:34 $\n--  Binding Version 01.00\n------------------------------------------------------------------------------\nwith Ada.Strings; use Ada.Strings;\nwith Ada.Strings.Fixed; use Ada.Strings.Fixed;\nwith Ada.Strings.Maps.Constants; use Ada.Strings.Maps.Constants;\nwith Ada.Characters.Latin_1; use Ada.Characters.Latin_1;\nwith Ada.Characters.Handling; use Ada.Characters.Handling;\n\nwith Terminal_Interface.Curses.Panels; use Terminal_Interface.Curses.Panels;\nwith Terminal_Interface.Curses.Forms; use Terminal_Interface.Curses.Forms;\nwith Terminal_Interface.Curses.Forms.Field_Types.Enumeration;\nuse  Terminal_Interface.Curses.Forms.Field_Types.Enumeration;\n\nwith Sample.Header_Handler; use Sample.Header_Handler;\nwith Sample.Form_Demo.Aux; use Sample.Form_Demo.Aux;\nwith Sample.Manifest; use Sample.Manifest;\nwith Sample.Form_Demo.Handler;\n\n--  This package contains a centralized keyboard handler used throughout\n--  this example. The handler establishes a timeout mechanism that provides\n--  periodical updates of the common header lines used in this example.\n--\n\npackage body Sample.Keyboard_Handler is\n\n   In_Command : Boolean := False;\n\n   function Get_Key (Win : Window := Standard_Window) return Real_Key_Code\n   is\n      K : Real_Key_Code;\n\n      function Command return Real_Key_Code;\n\n      function Command return Real_Key_Code\n      is\n         function My_Driver (F : Form;\n                             C : Key_Code;\n                             P : Panel) return Boolean;\n         package Fh is new Sample.Form_Demo.Handler (My_Driver);\n\n         type Label_Array is array (Label_Number) of String (1 .. 8);\n\n         Labels : Label_Array;\n\n         FA : Field_Array_Access := new Field_Array'\n           (Make (0, 0, \"Command:\"),\n            Make (Top => 0, Left => 9, Width => Columns - 11),\n            Null_Field);\n\n         K  : Real_Key_Code := Key_None;\n         N  : Natural := 0;\n\n         function My_Driver (F : Form;\n                             C : Key_Code;\n                             P : Panel) return Boolean\n         is\n            Ch : Character;\n         begin\n            if P = Null_Panel then\n               raise Panel_Exception;\n            end if;\n            if C in User_Key_Code'Range and then C = QUIT then\n               if Driver (F, F_Validate_Field) = Form_Ok  then\n                  K := Key_None;\n                  return True;\n               end if;\n            elsif C in Normal_Key_Code'Range then\n               Ch := Character'Val (C);\n               if Ch = LF or else Ch = CR then\n                  if Driver (F, F_Validate_Field) = Form_Ok  then\n                     declare\n                        Buffer : String (1 .. Positive (Columns - 11));\n                        Cmdc : String (1 .. 8);\n                     begin\n                        Get_Buffer (Fld => FA.all (2), Str => Buffer);\n                        Trim (Buffer, Left);\n                        if Buffer (1) /= ' ' then\n                           Cmdc := To_Upper (Buffer (Cmdc'Range));\n                           for I in Labels'Range loop\n                              if Cmdc = Labels (I) then\n                                 K := Function_Key_Code\n                                   (Function_Key_Number (I));\n                                 exit;\n                              end if;\n                           end loop;\n                        end if;\n                        return True;\n                     end;\n                  end if;\n               end if;\n            end if;\n            return False;\n         end My_Driver;\n\n      begin\n         In_Command := True;\n         for I in Label_Number'Range loop\n            Get_Soft_Label_Key (I, Labels (I));\n            Trim (Labels (I), Left);\n            Translate (Labels (I), Upper_Case_Map);\n            if Labels (I) (1) /= ' ' then\n               N := N + 1;\n            end if;\n         end loop;\n         if N > 0 then --  some labels were really set\n            declare\n               Enum_Info    : Enumeration_Info (N);\n               Enum_Field   : Enumeration_Field;\n               J : Positive := Enum_Info.Names'First;\n\n               Frm : Form := Create (FA);\n\n            begin\n               for I in Label_Number'Range loop\n                  if Labels (I) (1) /= ' ' then\n                     Enum_Info.Names (J) := new String'(Labels (I));\n                     J := J + 1;\n                  end if;\n               end loop;\n               Enum_Field := Create (Enum_Info, True);\n               Set_Field_Type (FA.all (2), Enum_Field);\n               Set_Background (FA.all (2), Normal_Video);\n\n               Fh.Drive_Me (Frm, Lines - 3, 0);\n               Delete (Frm);\n               Update_Panels; Update_Screen;\n            end;\n         end if;\n         Free (FA, True);\n         In_Command := False;\n         return K;\n      end Command;\n\n   begin\n      Set_Timeout_Mode (Win, Delayed, 30000);\n      loop\n         K := Get_Keystroke (Win);\n         if K = Key_None then  -- a timeout occurred\n            Update_Header_Window;\n         elsif K = 3 and then not In_Command  then  -- CTRL-C\n            K := Command;\n            exit when K /= Key_None;\n         else\n            exit;\n         end if;\n      end loop;\n      return K;\n   end Get_Key;\n\n   procedure Init_Keyboard_Handler is\n   begin\n      null;\n   end Init_Keyboard_Handler;\n\nend Sample.Keyboard_Handler;\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}