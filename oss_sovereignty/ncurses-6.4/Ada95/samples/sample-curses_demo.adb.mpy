{
  "module_name": "sample-curses_demo.adb",
  "hash_id": "7a45d20ecac37251357809ee76b57510b0a7d63caa885116bd2d1be05e4d75a1",
  "original_prompt": "Ingested from ncurses-6.4/Ada95/samples/sample-curses_demo.adb",
  "human_readable_source": "------------------------------------------------------------------------------\n--                                                                          --\n--                       GNAT ncurses Binding Samples                       --\n--                                                                          --\n--                            Sample.Curses_Demo                            --\n--                                                                          --\n--                                 B O D Y                                  --\n--                                                                          --\n------------------------------------------------------------------------------\n-- Copyright 2020 Thomas E. Dickey                                          --\n-- Copyright 1998-2004,2011 Free Software Foundation, Inc.                  --\n--                                                                          --\n-- Permission is hereby granted, free of charge, to any person obtaining a  --\n-- copy of this software and associated documentation files (the            --\n-- \"Software\"), to deal in the Software without restriction, including      --\n-- without limitation the rights to use, copy, modify, merge, publish,      --\n-- distribute, distribute with modifications, sublicense, and/or sell       --\n-- copies of the Software, and to permit persons to whom the Software is    --\n-- furnished to do so, subject to the following conditions:                 --\n--                                                                          --\n-- The above copyright notice and this permission notice shall be included  --\n-- in all copies or substantial portions of the Software.                   --\n--                                                                          --\n-- THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS  --\n-- OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF               --\n-- MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.   --\n-- IN NO EVENT SHALL THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,   --\n-- DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR    --\n-- OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR    --\n-- THE USE OR OTHER DEALINGS IN THE SOFTWARE.                               --\n--                                                                          --\n-- Except as contained in this notice, the name(s) of the above copyright   --\n-- holders shall not be used in advertising or otherwise to promote the     --\n-- sale, use or other dealings in this Software without prior written       --\n-- authorization.                                                           --\n------------------------------------------------------------------------------\n--  Author:  Juergen Pfeifer, 1996\n--  Version Control\n--  $Revision: 1.18 $\n--  $Date: 2020/02/02 23:34:34 $\n--  Binding Version 01.00\n------------------------------------------------------------------------------\nwith Terminal_Interface.Curses; use Terminal_Interface.Curses;\nwith Terminal_Interface.Curses.Menus; use Terminal_Interface.Curses.Menus;\nwith Terminal_Interface.Curses.Mouse;  use Terminal_Interface.Curses.Mouse;\nwith Terminal_Interface.Curses.Panels;  use Terminal_Interface.Curses.Panels;\nwith Terminal_Interface.Curses.Panels.User_Data;\n\nwith Sample.Manifest; use Sample.Manifest;\nwith Sample.Helpers; use Sample.Helpers;\nwith Sample.Function_Key_Setting; use Sample.Function_Key_Setting;\n\nwith Sample.Explanation; use Sample.Explanation;\n\nwith Sample.Menu_Demo.Handler;\nwith Sample.Curses_Demo.Mouse;\nwith Sample.Curses_Demo.Attributes;\n\npackage body Sample.Curses_Demo is\n\n   type User_Data is new Integer;\n   type User_Data_Access is access all User_Data;\n   package PUD is new Panels.User_Data (User_Data, User_Data_Access);\n   --  We use above instantiation of the generic User_Data package to\n   --  demonstrate and test the use of the user data mechanism.\n\n   procedure Demo\n   is\n      function My_Driver (M : Menu;\n                          K : Key_Code;\n                          Pan : Panel) return Boolean;\n      package Mh is new Sample.Menu_Demo.Handler (My_Driver);\n\n      Itm : Item_Array_Access := new Item_Array'\n        (New_Item (\"Attributes Demo\"),\n         New_Item (\"Mouse Demo\"),\n         Null_Item);\n      M  : Menu := New_Menu (Itm);\n      U1 : constant User_Data_Access := new User_Data'(4711);\n      U2 : User_Data_Access;\n\n      function My_Driver (M : Menu;\n                          K : Key_Code;\n                          Pan : Panel) return Boolean\n      is\n         Idx : constant Positive := Get_Index (Current (M));\n         Result : Boolean := False;\n      begin\n         PUD.Set_User_Data (Pan, U1); --  set some user data, just for fun\n         if K in User_Key_Code'Range then\n            if K = QUIT then\n               Result := True;\n            elsif K = SELECT_ITEM then\n               if Idx in Itm'Range then\n                  Hide (Pan);\n                  Update_Panels;\n               end if;\n               case Idx is\n                  when 1 => Sample.Curses_Demo.Attributes.Demo;\n                  when 2 => Sample.Curses_Demo.Mouse.Demo;\n                  when others => Not_Implemented;\n               end case;\n               if Idx in Itm'Range then\n                  Top (Pan);\n                  Show (Pan);\n                  Update_Panels;\n                  Update_Screen;\n               end if;\n            end if;\n         end if;\n         PUD.Get_User_Data (Pan, U2); --  get the user data\n         pragma Assert (U1.all = U2.all and then U1 = U2);\n         return Result;\n      end My_Driver;\n\n   begin\n\n      if (1 + Item_Count (M)) /= Itm'Length then\n         raise Constraint_Error;\n      end if;\n\n      if not Has_Mouse then\n         declare\n            O : Item_Option_Set;\n         begin\n            Get_Options (Itm.all (2), O);\n            O.Selectable := False;\n            Set_Options (Itm.all (2), O);\n         end;\n      end if;\n\n      Push_Environment (\"CURSES00\");\n      Notepad (\"CURSES-PAD00\");\n      Default_Labels;\n      Refresh_Soft_Label_Keys_Without_Update;\n\n      Mh.Drive_Me (M, \" Demo \");\n      Pop_Environment;\n\n      Delete (M);\n      Free (Itm, True);\n   end Demo;\n\nend Sample.Curses_Demo;\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}