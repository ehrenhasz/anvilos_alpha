{
  "module_name": "ncurses2-util.adb",
  "hash_id": "25b9f4748e8816e86d881316a5742387897763a60f161e94d6f3b104aab869a9",
  "original_prompt": "Ingested from ncurses-6.4/Ada95/samples/ncurses2-util.adb",
  "human_readable_source": "------------------------------------------------------------------------------\n--                                                                          --\n--                       GNAT ncurses Binding Samples                       --\n--                                                                          --\n--                               ncurses2.util                              --\n--                                                                          --\n--                                 B O D Y                                  --\n--                                                                          --\n------------------------------------------------------------------------------\n-- Copyright 2020 Thomas E. Dickey                                          --\n-- Copyright 2000-2008,2014 Free Software Foundation, Inc.                  --\n--                                                                          --\n-- Permission is hereby granted, free of charge, to any person obtaining a  --\n-- copy of this software and associated documentation files (the            --\n-- \"Software\"), to deal in the Software without restriction, including      --\n-- without limitation the rights to use, copy, modify, merge, publish,      --\n-- distribute, distribute with modifications, sublicense, and/or sell       --\n-- copies of the Software, and to permit persons to whom the Software is    --\n-- furnished to do so, subject to the following conditions:                 --\n--                                                                          --\n-- The above copyright notice and this permission notice shall be included  --\n-- in all copies or substantial portions of the Software.                   --\n--                                                                          --\n-- THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS  --\n-- OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF               --\n-- MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.   --\n-- IN NO EVENT SHALL THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,   --\n-- DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR    --\n-- OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR    --\n-- THE USE OR OTHER DEALINGS IN THE SOFTWARE.                               --\n--                                                                          --\n-- Except as contained in this notice, the name(s) of the above copyright   --\n-- holders shall not be used in advertising or otherwise to promote the     --\n-- sale, use or other dealings in this Software without prior written       --\n-- authorization.                                                           --\n------------------------------------------------------------------------------\n--  Author: Eugene V. Melaragno <aldomel@ix.netcom.com> 2000\n--  Version Control\n--  $Revision: 1.10 $\n--  $Date: 2020/02/02 23:34:34 $\n--  Binding Version 01.00\n------------------------------------------------------------------------------\nwith Ada.Text_IO; use Ada.Text_IO;\n\nwith Terminal_Interface.Curses.Trace; use Terminal_Interface.Curses.Trace;\n\nwith Interfaces.C;\nwith Interfaces.C.Strings;\n\nwith Ada.Characters.Handling;\n\nwith ncurses2.genericPuts;\n\npackage body ncurses2.util is\n\n   --  #defines from C\n   --  #define CTRL(x)         ((x) & 0x1f)\n   function CTRL (c : Character) return Key_Code is\n   begin\n      return Character'Pos (c) mod 16#20#;\n      --  uses a property of ASCII\n      --  A = 16#41#; a = 16#61#; ^A = 1 or 16#1#\n   end CTRL;\n\n   function CTRL (c : Character) return Character is\n   begin\n      return Character'Val (Character'Pos (c) mod 16#20#);\n      --  uses a property of ASCII\n      --  A = 16#41#; a = 16#61#; ^A = 1 or 16#1#\n   end CTRL;\n\n   save_trace : Trace_Attribute_Set;\n   --  Common function to allow ^T to toggle trace-mode in the middle of a test\n   --  so that trace-files can be made smaller.\n   function Getchar (win : Window := Standard_Window) return Key_Code is\n      c : Key_Code;\n   begin\n      --  #ifdef TRACE\n      c := Get_Keystroke (win);\n      while c = CTRL ('T') loop\n         --  if _nc_tracing  in C\n         if Current_Trace_Setting /= Trace_Disable then\n            save_trace := Current_Trace_Setting;\n            Trace_Put (\"TOGGLE-TRACING OFF\");\n            Current_Trace_Setting := Trace_Disable;\n         else\n            Current_Trace_Setting := save_trace;\n         end if;\n         Trace_On (Current_Trace_Setting);\n         if Current_Trace_Setting /= Trace_Disable then\n            Trace_Put (\"TOGGLE-TRACING ON\");\n         end if;\n      end loop;\n      --  #else c := Get_Keystroke;\n      return c;\n   end Getchar;\n\n   procedure Getchar (win : Window := Standard_Window) is\n   begin\n      if Getchar (win) < 0 then\n         Beep;\n      end if;\n   end Getchar;\n\n   procedure Pause is\n   begin\n      Move_Cursor (Line => Lines - 1, Column => 0);\n      Add (Str => \"Press any key to continue... \");\n      Getchar;\n   end Pause;\n\n   procedure Cannot (s : String) is\n      use Interfaces.C;\n      use Interfaces.C.Strings;\n      function getenv (x : char_array)  return chars_ptr;\n      pragma Import (C, getenv, \"getenv\");\n      tmp1 : char_array (0 .. 10);\n      package p is new ncurses2.genericPuts (1024);\n      use p;\n      use p.BS;\n\n      tmpb : BS.Bounded_String;\n\n      Length : size_t;\n   begin\n      To_C (\"TERM\", tmp1, Length);\n      Fill_String (getenv (tmp1), tmpb);\n      Add (Ch => newl);\n      myAdd (Str => \"This \" & tmpb & \" terminal \" & s);\n      Pause;\n   end Cannot;\n\n   procedure ShellOut (message : Boolean) is\n      use Interfaces.C;\n      Txt : char_array (0 .. 10);\n      Length : size_t;\n      procedure system (x : char_array);\n      pragma Import (C, system, \"system\");\n   begin\n      To_C (\"sh\", Txt,  Length);\n      if message then\n         Add (Str => \"Shelling out...\");\n      end if;\n      Save_Curses_Mode (Mode => Curses);\n      End_Windows;\n      system (Txt);\n      if message then\n         Add (Str => \"returned from shellout.\");\n         Add (Ch => newl);\n      end if;\n      Refresh;\n   end ShellOut;\n\n   function Is_Digit (c : Key_Code) return Boolean is\n   begin\n      if c >= 16#100# then\n         return False;\n      else\n         return Ada.Characters.Handling.Is_Digit (Character'Val (c));\n      end if;\n   end Is_Digit;\n\n   procedure P (s : String) is\n   begin\n      Add (Str => s);\n      Add (Ch => newl);\n   end P;\n\n   function Code_To_Char (c : Key_Code) return Character is\n   begin\n      if c > Character'Pos (Character'Last) then\n         return Character'Val (0);\n         --  maybe raise exception?\n      else\n         return Character'Val (c);\n      end if;\n   end Code_To_Char;\n\n   --  This was untestable due to a bug in GNAT (3.12p)\n   --  Hmm, what bug? I don't remember.\n   function ctoi (c : Character) return Integer is\n   begin\n      return Character'Pos (c) - Character'Pos ('0');\n   end ctoi;\n\nend ncurses2.util;\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}