{
  "module_name": "sample-text_io_demo.adb",
  "hash_id": "058e1b32eb75aaae00b63410b8ef01092d62265cc3910213c2d1a6eaab409027",
  "original_prompt": "Ingested from ncurses-6.4/Ada95/samples/sample-text_io_demo.adb",
  "human_readable_source": "------------------------------------------------------------------------------\n--                                                                          --\n--                       GNAT ncurses Binding Samples                       --\n--                                                                          --\n--                            Sample.Text_IO_Demo                           --\n--                                                                          --\n--                                 B O D Y                                  --\n--                                                                          --\n------------------------------------------------------------------------------\n-- Copyright 2018,2020 Thomas E. Dickey                                     --\n-- Copyright 1998-2006,2011 Free Software Foundation, Inc.                  --\n--                                                                          --\n-- Permission is hereby granted, free of charge, to any person obtaining a  --\n-- copy of this software and associated documentation files (the            --\n-- \"Software\"), to deal in the Software without restriction, including      --\n-- without limitation the rights to use, copy, modify, merge, publish,      --\n-- distribute, distribute with modifications, sublicense, and/or sell       --\n-- copies of the Software, and to permit persons to whom the Software is    --\n-- furnished to do so, subject to the following conditions:                 --\n--                                                                          --\n-- The above copyright notice and this permission notice shall be included  --\n-- in all copies or substantial portions of the Software.                   --\n--                                                                          --\n-- THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS  --\n-- OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF               --\n-- MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.   --\n-- IN NO EVENT SHALL THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,   --\n-- DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR    --\n-- OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR    --\n-- THE USE OR OTHER DEALINGS IN THE SOFTWARE.                               --\n--                                                                          --\n-- Except as contained in this notice, the name(s) of the above copyright   --\n-- holders shall not be used in advertising or otherwise to promote the     --\n-- sale, use or other dealings in this Software without prior written       --\n-- authorization.                                                           --\n------------------------------------------------------------------------------\n--  Author:  Juergen Pfeifer, 1996\n--  Version Control\n--  $Revision: 1.19 $\n--  $Date: 2020/02/02 23:34:34 $\n--  Binding Version 01.00\n------------------------------------------------------------------------------\nwith Ada.Numerics.Generic_Elementary_Functions;\n\nwith Ada.Numerics.Complex_Types;\nuse  Ada.Numerics.Complex_Types;\n\nwith Terminal_Interface.Curses;\nuse  Terminal_Interface.Curses;\n\nwith Terminal_Interface.Curses.Panels;\nuse  Terminal_Interface.Curses.Panels;\n\nwith Terminal_Interface.Curses.Text_IO;\nuse  Terminal_Interface.Curses.Text_IO;\n\nwith Terminal_Interface.Curses.Text_IO.Integer_IO;\nwith Terminal_Interface.Curses.Text_IO.Float_IO;\nwith Terminal_Interface.Curses.Text_IO.Enumeration_IO;\nwith Terminal_Interface.Curses.Text_IO.Complex_IO;\nwith Terminal_Interface.Curses.Text_IO.Decimal_IO;\nwith Terminal_Interface.Curses.Text_IO.Modular_IO;\n\nwith Sample.Manifest; use Sample.Manifest;\nwith Sample.Function_Key_Setting; use Sample.Function_Key_Setting;\nwith Sample.Keyboard_Handler; use Sample.Keyboard_Handler;\nwith Sample.Explanation; use Sample.Explanation;\n\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Complex_IO);\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Decimal_IO);\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Enumeration_IO);\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Float_IO);\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Integer_IO);\npragma Elaborate_All (Terminal_Interface.Curses.Text_Io.Modular_IO);\n\npackage body Sample.Text_IO_Demo is\n\n   type Weekday is (Sunday,\n                    Monday,\n                    Tuesday,\n                    Wednesday,\n                    Thursday,\n                    Friday,\n                    Saturday);\n\n   type Dec is delta 0.01 digits 5 range 0.0 .. 4.0;\n   type Md is mod 5;\n\n   package Math is new\n     Ada.Numerics.Generic_Elementary_Functions (Float);\n\n   package Int_IO is new\n     Terminal_Interface.Curses.Text_IO.Integer_IO (Integer);\n   use Int_IO;\n\n   package Real_IO is new\n     Terminal_Interface.Curses.Text_IO.Float_IO (Float);\n   use Real_IO;\n\n   package Enum_IO is new\n     Terminal_Interface.Curses.Text_IO.Enumeration_IO (Weekday);\n   use Enum_IO;\n\n   package C_IO is new\n     Terminal_Interface.Curses.Text_IO.Complex_IO (Ada.Numerics.Complex_Types);\n   use C_IO;\n\n   package D_IO is new\n     Terminal_Interface.Curses.Text_IO.Decimal_IO (Dec);\n   use D_IO;\n\n   package M_IO is new\n     Terminal_Interface.Curses.Text_IO.Modular_IO (Md);\n   use M_IO;\n\n   procedure Demo\n   is\n      W : Window;\n      P : Panel := Create (Standard_Window);\n      K : Real_Key_Code;\n      Im : constant Complex := (0.0, 1.0);\n      Fx : constant Dec := 3.14;\n      Dc : constant Dec := 2.72;\n      L : Md;\n\n   begin\n      Push_Environment (\"TEXTIO\");\n      Default_Labels;\n      Notepad (\"TEXTIO-PAD00\");\n\n      Set_Echo_Mode (False);\n      Set_Meta_Mode;\n      Set_KeyPad_Mode;\n      W := Sub_Window (Standard_Window, Lines - 2, Columns - 2, 1, 1);\n      Box;\n      Refresh_Without_Update;\n      Set_Meta_Mode (W);\n      Set_KeyPad_Mode (W);\n      Immediate_Update_Mode (W, True);\n\n      Set_Window (W);\n\n      for I in 1 .. 10 loop\n         Put (\"Square root of \");\n         Put (Item => I, Width => 5);\n         Put (\" is \");\n         Put (Item => Math.Sqrt (Float (I)), Exp => 0, Aft => 7);\n         New_Line;\n      end loop;\n\n      for W in Weekday loop\n         Put (Item => W); Put (' ');\n      end loop;\n      New_Line;\n\n      L := Md'First;\n      for I in 1 .. 2 loop\n         for J in Md'Range loop\n            Put (L); Put (' ');\n            L := L + 1;\n         end loop;\n      end loop;\n      New_Line;\n\n      Put (Im); New_Line;\n      Put (Fx); New_Line;\n      Put (Dc); New_Line;\n\n      loop\n         K := Get_Key;\n         if K in Special_Key_Code'Range then\n            case K is\n               when QUIT_CODE     => exit;\n               when HELP_CODE     => Explain_Context;\n               when EXPLAIN_CODE  => Explain (\"TEXTIOKEYS\");\n               when others        => null;\n            end case;\n         end if;\n      end loop;\n\n      Set_Window (Null_Window);\n      Erase; Refresh_Without_Update;\n      Delete (P);\n      Delete (W);\n\n      Pop_Environment;\n   end Demo;\n\nend Sample.Text_IO_Demo;\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}