{
  "module_name": "kernel-kmem.m4",
  "hash_id": "5f103f86b3330b21151169096b4c6c1c95567f77a88632db596f42703ca36dc9",
  "original_prompt": "Ingested from zfs-2.2.2/config/kernel-kmem.m4",
  "human_readable_source": "dnl #\ndnl # Enabled by default it provides a minimal level of memory tracking.\ndnl # A total count of bytes allocated is kept for each alloc and free.\ndnl # Then at module unload time a report to the console will be printed\ndnl # if memory was leaked.\ndnl #\nAC_DEFUN([SPL_AC_DEBUG_KMEM], [\n\tAC_ARG_ENABLE([debug-kmem],\n\t\t[AS_HELP_STRING([--enable-debug-kmem],\n\t\t[Enable basic kmem accounting @<:@default=no@:>@])],\n\t\t[],\n\t\t[enable_debug_kmem=no])\n\n\tAS_IF([test \"x$enable_debug_kmem\" = xyes],\n\t[\n\t\tKERNELCPPFLAGS=\"${KERNELCPPFLAGS} -DDEBUG_KMEM\"\n\t\tDEBUG_KMEM=\"_with_debug_kmem\"\n\t\tAC_DEFINE([DEBUG_KMEM], [1],\n\t\t[Define to 1 to enable basic kmem accounting])\n\t], [\n\t\tDEBUG_KMEM=\"_without_debug_kmem\"\n\t])\n\n\tAC_SUBST(DEBUG_KMEM)\n\tAC_MSG_CHECKING([whether basic kmem accounting is enabled])\n\tAC_MSG_RESULT([$enable_debug_kmem])\n])\n\ndnl #\ndnl # Disabled by default it provides detailed memory tracking.  This\ndnl # feature also requires --enable-debug-kmem to be set.  When enabled\ndnl # not only will total bytes be tracked but also the location of every\ndnl # alloc and free.  When the SPL module is unloaded a list of all leaked\ndnl # addresses and where they were allocated will be dumped to the console.\ndnl # Enabling this feature has a significant impact on performance but it\ndnl # makes finding memory leaks pretty straight forward.\ndnl #\nAC_DEFUN([SPL_AC_DEBUG_KMEM_TRACKING], [\n\tAC_ARG_ENABLE([debug-kmem-tracking],\n\t\t[AS_HELP_STRING([--enable-debug-kmem-tracking],\n\t\t[Enable detailed kmem tracking  @<:@default=no@:>@])],\n\t\t[],\n\t\t[enable_debug_kmem_tracking=no])\n\n\tAS_IF([test \"x$enable_debug_kmem_tracking\" = xyes],\n\t[\n\t\tKERNELCPPFLAGS=\"${KERNELCPPFLAGS} -DDEBUG_KMEM_TRACKING\"\n\t\tDEBUG_KMEM_TRACKING=\"_with_debug_kmem_tracking\"\n\t\tAC_DEFINE([DEBUG_KMEM_TRACKING], [1],\n\t\t[Define to 1 to enable detailed kmem tracking])\n\t], [\n\t\tDEBUG_KMEM_TRACKING=\"_without_debug_kmem_tracking\"\n\t])\n\n\tAC_SUBST(DEBUG_KMEM_TRACKING)\n\tAC_MSG_CHECKING([whether detailed kmem tracking is enabled])\n\tAC_MSG_RESULT([$enable_debug_kmem_tracking])\n])\n\ndnl #\ndnl # 4.12 API,\ndnl # Added kvmalloc allocation strategy\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_KVMALLOC], [\n\tZFS_LINUX_TEST_SRC([kvmalloc], [\n\t\t#include <linux/mm.h>\n\t\t#include <linux/slab.h>\n\t],[\n\t\tvoid *p __attribute__ ((unused));\n\n\t\tp = kvmalloc(0, GFP_KERNEL);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_KVMALLOC], [\n\tAC_MSG_CHECKING([whether kvmalloc(ptr, flags) is available])\n\tZFS_LINUX_TEST_RESULT([kvmalloc], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_KVMALLOC, 1, [kvmalloc exists])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\ndnl #\ndnl # 5.8 API,\ndnl # __vmalloc PAGE_KERNEL removal\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_VMALLOC_PAGE_KERNEL], [\n\tZFS_LINUX_TEST_SRC([__vmalloc], [\n\t\t#include <linux/mm.h>\n\t\t#include <linux/vmalloc.h>\n\t],[\n\t\tvoid *p __attribute__ ((unused));\n\n\t\tp = __vmalloc(0, GFP_KERNEL, PAGE_KERNEL);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_VMALLOC_PAGE_KERNEL], [\n\tAC_MSG_CHECKING([whether __vmalloc(ptr, flags, pageflags) is available])\n\tZFS_LINUX_TEST_RESULT([__vmalloc], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_VMALLOC_PAGE_KERNEL, 1, [__vmalloc page flags exists])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n-",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}