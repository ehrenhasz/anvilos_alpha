{
  "module_name": "kernel-config-defined.m4",
  "hash_id": "78974ffe5aeffd8f87323fd930f122f72ed0ec9dde87f10872bb86db94c0f0a7",
  "original_prompt": "Ingested from zfs-2.2.2/config/kernel-config-defined.m4",
  "human_readable_source": "dnl #\ndnl # Certain kernel build options are not supported.  These must be\ndnl # detected at configure time and cause a build failure.  Otherwise\ndnl # modules may be successfully built that behave incorrectly.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_DEFINED], [\n\tAS_IF([test \"x$cross_compiling\" != xyes], [\n\t\tAC_RUN_IFELSE([\n\t\t\tAC_LANG_PROGRAM([\n\t\t\t\t#include \"$LINUX/include/linux/license.h\"\n\t\t\t], [\n\t\t\t\treturn !license_is_gpl_compatible(\n\t\t\t\t    \"$ZFS_META_LICENSE\");\n\t\t\t])\n\t\t], [\n\t\t\tAC_DEFINE([ZFS_IS_GPL_COMPATIBLE], [1],\n\t\t\t    [Define to 1 if GPL-only symbols can be used])\n\t\t], [\n\t\t])\n\t])\n\n\tZFS_AC_KERNEL_SRC_CONFIG_MODULES\n\tZFS_AC_KERNEL_SRC_CONFIG_BLOCK\n\tZFS_AC_KERNEL_SRC_CONFIG_DEBUG_LOCK_ALLOC\n\tZFS_AC_KERNEL_SRC_CONFIG_TRIM_UNUSED_KSYMS\n\tZFS_AC_KERNEL_SRC_CONFIG_ZLIB_DEFLATE\n\tZFS_AC_KERNEL_SRC_CONFIG_ZLIB_INFLATE\n\n\tAC_MSG_CHECKING([for kernel config option compatibility])\n\tZFS_LINUX_TEST_COMPILE_ALL([config])\n\tAC_MSG_RESULT([done])\n\n\tZFS_AC_KERNEL_CONFIG_MODULES\n\tZFS_AC_KERNEL_CONFIG_BLOCK\n\tZFS_AC_KERNEL_CONFIG_DEBUG_LOCK_ALLOC\n\tZFS_AC_KERNEL_CONFIG_TRIM_UNUSED_KSYMS\n\tZFS_AC_KERNEL_CONFIG_ZLIB_DEFLATE\n\tZFS_AC_KERNEL_CONFIG_ZLIB_INFLATE\n])\n\ndnl #\ndnl # Check CONFIG_BLOCK\ndnl #\ndnl # Verify the kernel has CONFIG_BLOCK support enabled.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONFIG_BLOCK], [\n\tZFS_LINUX_TEST_SRC([config_block], [\n\t\t#if !defined(CONFIG_BLOCK)\n\t\t#error CONFIG_BLOCK not defined\n\t\t#endif\n\t],[])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_BLOCK], [\n\tAC_MSG_CHECKING([whether CONFIG_BLOCK is defined])\n\tZFS_LINUX_TEST_RESULT([config_block], [\n\t\tAC_MSG_RESULT([yes])\n\t],[\n\t\tAC_MSG_RESULT([no])\n\t\tAC_MSG_ERROR([\n\t*** This kernel does not include the required block device support.\n\t*** Rebuild the kernel with CONFIG_BLOCK=y set.])\n\t])\n])\n\ndnl #\ndnl # Check CONFIG_DEBUG_LOCK_ALLOC\ndnl #\ndnl # This is typically only set for debug kernels because it comes with\ndnl # a performance penalty.  However, when it is set it maps the non-GPL\ndnl # symbol mutex_lock() to the GPL-only mutex_lock_nested() symbol.\ndnl # This will cause a failure at link time which we'd rather know about\ndnl # at compile time.\ndnl #\ndnl # Since we plan to pursue making mutex_lock_nested() a non-GPL symbol\ndnl # with the upstream community we add a check to detect this case.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONFIG_DEBUG_LOCK_ALLOC], [\n\tZFS_LINUX_TEST_SRC([config_debug_lock_alloc], [\n\t\t#include <linux/mutex.h>\n\t],[\n\t\tstruct mutex lock;\n\n\t\tmutex_init(&lock);\n\t\tmutex_lock(&lock);\n\t\tmutex_unlock(&lock);\n\t], [], [ZFS_META_LICENSE])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_DEBUG_LOCK_ALLOC], [\n\tAC_MSG_CHECKING([whether mutex_lock() is GPL-only])\n\tZFS_LINUX_TEST_RESULT([config_debug_lock_alloc_license], [\n\t\tAC_MSG_RESULT(no)\n\t],[\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_MSG_ERROR([\n\t*** Kernel built with CONFIG_DEBUG_LOCK_ALLOC which is incompatible\n\t*** with the CDDL license and will prevent the module linking stage\n\t*** from succeeding.  You must rebuild your kernel without this\n\t*** option enabled.])\n\t])\n])\n\ndnl #\ndnl # Check CONFIG_MODULES\ndnl #\ndnl # Verify the kernel has CONFIG_MODULES support enabled.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONFIG_MODULES], [\n\tZFS_LINUX_TEST_SRC([config_modules], [\n\t\t#if !defined(CONFIG_MODULES)\n\t\t#error CONFIG_MODULES not defined\n\t\t#endif\n\t],[])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_MODULES], [\n\tAC_MSG_CHECKING([whether CONFIG_MODULES is defined])\n\tAS_IF([test \"x$enable_linux_builtin\" != xyes], [\n\t\tZFS_LINUX_TEST_RESULT([config_modules], [\n\t\t\tAC_MSG_RESULT([yes])\n\t\t],[\n\t\t\tAC_MSG_RESULT([no])\n\t\t\tAC_MSG_ERROR([\n\t\t*** This kernel does not include the required loadable module\n\t\t*** support!\n\t\t***\n\t\t*** To build OpenZFS as a loadable Linux kernel module\n\t\t*** enable loadable module support by setting\n\t\t*** `CONFIG_MODULES=y` in the kernel configuration and run\n\t\t*** `make modules_prepare` in the Linux source tree.\n\t\t***\n\t\t*** If you don't intend to enable loadable kernel module\n\t\t*** support, please compile OpenZFS as a Linux kernel built-in.\n\t\t***\n\t\t*** Prepare the Linux source tree by running `make prepare`,\n\t\t*** use the OpenZFS `--enable-linux-builtin` configure option,\n\t\t*** copy the OpenZFS sources into the Linux source tree using\n\t\t*** `./copy-builtin <linux source directory>`,\n\t\t*** set `CONFIG_ZFS=y` in the kernel configuration and compile\n\t\t*** kernel as usual.\n\t\t\t])\n\t\t])\n\t], [\n\t\tZFS_LINUX_TRY_COMPILE([], [], [\n\t\t\tAC_MSG_RESULT([not needed])\n\t\t],[\n\t\t\tAC_MSG_RESULT([error])\n\t\t\tAC_MSG_ERROR([\n\t\t*** This kernel is unable to compile object files.\n\t\t***\n\t\t*** Please make sure you prepared the Linux source tree\n\t\t*** by running `make prepare` there.\n\t\t\t])\n\t\t])\n\t])\n])\n\ndnl #\ndnl # Check CONFIG_TRIM_UNUSED_KSYMS\ndnl #\ndnl # Verify the kernel has CONFIG_TRIM_UNUSED_KSYMS disabled.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONFIG_TRIM_UNUSED_KSYMS], [\n\tZFS_LINUX_TEST_SRC([config_trim_unusued_ksyms], [\n\t\t#if defined(CONFIG_TRIM_UNUSED_KSYMS)\n\t\t#error CONFIG_TRIM_UNUSED_KSYMS not defined\n\t\t#endif\n\t],[])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_TRIM_UNUSED_KSYMS], [\n\tAC_MSG_CHECKING([whether CONFIG_TRIM_UNUSED_KSYM is disabled])\n\tZFS_LINUX_TEST_RESULT([config_trim_unusued_ksyms], [\n\t\tAC_MSG_RESULT([yes])\n\t],[\n\t\tAC_MSG_RESULT([no])\n\t\tAS_IF([test \"x$enable_linux_builtin\" != xyes], [\n\t\t\tAC_MSG_ERROR([\n\t*** This kernel has unused symbols trimming enabled, please disable.\n\t*** Rebuild the kernel with CONFIG_TRIM_UNUSED_KSYMS=n set.])\n\t\t])\n\t])\n])\n\ndnl #\ndnl # Check CONFIG_ZLIB_INFLATE\ndnl #\ndnl # Verify the kernel has CONFIG_ZLIB_INFLATE support enabled.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONFIG_ZLIB_INFLATE], [\n\tZFS_LINUX_TEST_SRC([config_zlib_inflate], [\n\t\t#if !defined(CONFIG_ZLIB_INFLATE) && \\\n\t\t    !defined(CONFIG_ZLIB_INFLATE_MODULE)\n\t\t#error CONFIG_ZLIB_INFLATE not defined\n\t\t#endif\n\t],[])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_ZLIB_INFLATE], [\n\tAC_MSG_CHECKING([whether CONFIG_ZLIB_INFLATE is defined])\n\tZFS_LINUX_TEST_RESULT([config_zlib_inflate], [\n\t\tAC_MSG_RESULT([yes])\n\t],[\n\t\tAC_MSG_RESULT([no])\n\t\tAC_MSG_ERROR([\n\t*** This kernel does not include the required zlib inflate support.\n\t*** Rebuild the kernel with CONFIG_ZLIB_INFLATE=y|m set.])\n\t])\n])\n\ndnl #\ndnl # Check CONFIG_ZLIB_DEFLATE\ndnl #\ndnl # Verify the kernel has CONFIG_ZLIB_DEFLATE support enabled.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONFIG_ZLIB_DEFLATE], [\n\tZFS_LINUX_TEST_SRC([config_zlib_deflate], [\n\t\t#if !defined(CONFIG_ZLIB_DEFLATE) && \\\n\t\t    !defined(CONFIG_ZLIB_DEFLATE_MODULE)\n\t\t#error CONFIG_ZLIB_DEFLATE not defined\n\t\t#endif\n\t],[])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONFIG_ZLIB_DEFLATE], [\n\tAC_MSG_CHECKING([whether CONFIG_ZLIB_DEFLATE is defined])\n\tZFS_LINUX_TEST_RESULT([config_zlib_deflate], [\n\t\tAC_MSG_RESULT([yes])\n\t],[\n\t\tAC_MSG_RESULT([no])\n\t\tAC_MSG_ERROR([\n\t*** This kernel does not include the required zlib deflate support.\n\t*** Rebuild the kernel with CONFIG_ZLIB_DEFLATE=y|m set.])\n\t])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}