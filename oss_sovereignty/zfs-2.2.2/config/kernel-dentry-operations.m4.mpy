{
  "module_name": "kernel-dentry-operations.m4",
  "hash_id": "29edf436525405746092fc8fe9d2a60db38ac4aae63a94d904340804c9cb720e",
  "original_prompt": "Ingested from zfs-2.2.2/config/kernel-dentry-operations.m4",
  "human_readable_source": "dnl #\ndnl # 3.4.0 API change\ndnl # Added d_make_root() to replace previous d_alloc_root() function.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_D_MAKE_ROOT], [\n\tZFS_LINUX_TEST_SRC([d_make_root], [\n\t\t#include <linux/dcache.h>\n\t], [\n\t\td_make_root(NULL);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_D_MAKE_ROOT], [\n\tAC_MSG_CHECKING([whether d_make_root() is available])\n\tZFS_LINUX_TEST_RESULT_SYMBOL([d_make_root],\n\t    [d_make_root], [fs/dcache.c], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_D_MAKE_ROOT, 1, [d_make_root() is available])\n\t], [\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\ndnl #\ndnl # 2.6.28 API change\ndnl # Added d_obtain_alias() helper function.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_D_OBTAIN_ALIAS], [\n\tZFS_LINUX_TEST_SRC([d_obtain_alias], [\n\t\t#include <linux/dcache.h>\n\t], [\n\t\td_obtain_alias(NULL);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_D_OBTAIN_ALIAS], [\n\tAC_MSG_CHECKING([whether d_obtain_alias() is available])\n\tZFS_LINUX_TEST_RESULT_SYMBOL([d_obtain_alias],\n\t    [d_obtain_alias], [fs/dcache.c], [\n\t\tAC_MSG_RESULT(yes)\n\t], [\n\t\tZFS_LINUX_TEST_ERROR([d_obtain_alias()])\n\t])\n])\n\ndnl #\ndnl # 2.6.12 API change\ndnl # d_prune_aliases() helper function available.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_D_PRUNE_ALIASES], [\n\tZFS_LINUX_TEST_SRC([d_prune_aliases], [\n\t\t#include <linux/dcache.h>\n\t], [\n\t\tstruct inode *ip = NULL;\n\t\td_prune_aliases(ip);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_D_PRUNE_ALIASES], [\n\tAC_MSG_CHECKING([whether d_prune_aliases() is available])\n\tZFS_LINUX_TEST_RESULT_SYMBOL([d_prune_aliases],\n\t    [d_prune_aliases], [fs/dcache.c], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_D_PRUNE_ALIASES, 1,\n\t\t    [d_prune_aliases() is available])\n\t], [\n\t\tZFS_LINUX_TEST_ERROR([d_prune_aliases()])\n\t])\n])\n\ndnl #\ndnl # 2.6.38 API change\ndnl # Added d_set_d_op() helper function.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_D_SET_D_OP], [\n\tZFS_LINUX_TEST_SRC([d_set_d_op], [\n\t\t#include <linux/dcache.h>\n\t], [\n\t\td_set_d_op(NULL, NULL);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_D_SET_D_OP], [\n\tAC_MSG_CHECKING([whether d_set_d_op() is available])\n\tZFS_LINUX_TEST_RESULT_SYMBOL([d_set_d_op],\n\t    [d_set_d_op], [fs/dcache.c], [\n\t\tAC_MSG_RESULT(yes)\n\t], [\n\t\tZFS_LINUX_TEST_ERROR([d_set_d_op])\n\t])\n])\n\ndnl #\ndnl # 3.6 API change\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_D_REVALIDATE_NAMEIDATA], [\n\tZFS_LINUX_TEST_SRC([dentry_operations_revalidate], [\n\t\t#include <linux/dcache.h>\n\t\t#include <linux/sched.h>\n\n\t\tint revalidate (struct dentry *dentry,\n\t\t    struct nameidata *nidata) { return 0; }\n\n\t\tstatic const struct dentry_operations\n\t\t    dops __attribute__ ((unused)) = {\n\t\t\t.d_revalidate\t= revalidate,\n\t\t};\n\t],[])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_D_REVALIDATE_NAMEIDATA], [\n\tAC_MSG_CHECKING([whether dops->d_revalidate() takes struct nameidata])\n\tZFS_LINUX_TEST_RESULT([dentry_operations_revalidate], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_D_REVALIDATE_NAMEIDATA, 1,\n\t\t    [dops->d_revalidate() operation takes nameidata])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\ndnl #\ndnl # 2.6.30 API change\ndnl # The 'struct dentry_operations' was constified in the dentry structure.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_CONST_DENTRY_OPERATIONS], [\n\tZFS_LINUX_TEST_SRC([dentry_operations_const], [\n\t\t#include <linux/dcache.h>\n\n\t\tconst struct dentry_operations test_d_op = {\n\t\t\t.d_revalidate = NULL,\n\t\t};\n\t],[\n\t\tstruct dentry d __attribute__ ((unused));\n\t\td.d_op = &test_d_op;\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_CONST_DENTRY_OPERATIONS], [\n\tAC_MSG_CHECKING([whether dentry uses const struct dentry_operations])\n\tZFS_LINUX_TEST_RESULT([dentry_operations_const], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_CONST_DENTRY_OPERATIONS, 1,\n\t\t    [dentry uses const struct dentry_operations])\n\t],[\n\t\tZFS_LINUX_TEST_ERROR([const dentry_operations])\n\t])\n])\n\ndnl #\ndnl # 2.6.38 API change\ndnl # Added sb->s_d_op default dentry_operations member\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_S_D_OP], [\n\tZFS_LINUX_TEST_SRC([super_block_s_d_op], [\n\t\t#include <linux/fs.h>\n\t],[\n\t\tstruct super_block sb __attribute__ ((unused));\n\t\tsb.s_d_op = NULL;\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_S_D_OP], [\n\tAC_MSG_CHECKING([whether super_block has s_d_op])\n\tZFS_LINUX_TEST_RESULT([super_block_s_d_op], [\n\t\tAC_MSG_RESULT(yes)\n\t], [\n\t\tZFS_LINUX_TEST_ERROR([super_block s_d_op])\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_SRC_DENTRY], [\n        ZFS_AC_KERNEL_SRC_D_MAKE_ROOT\n        ZFS_AC_KERNEL_SRC_D_OBTAIN_ALIAS\n        ZFS_AC_KERNEL_SRC_D_PRUNE_ALIASES\n        ZFS_AC_KERNEL_SRC_D_SET_D_OP\n        ZFS_AC_KERNEL_SRC_D_REVALIDATE_NAMEIDATA\n        ZFS_AC_KERNEL_SRC_CONST_DENTRY_OPERATIONS\n        ZFS_AC_KERNEL_SRC_S_D_OP\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_DENTRY], [\n        ZFS_AC_KERNEL_D_MAKE_ROOT\n        ZFS_AC_KERNEL_D_OBTAIN_ALIAS\n        ZFS_AC_KERNEL_D_PRUNE_ALIASES\n        ZFS_AC_KERNEL_D_SET_D_OP\n        ZFS_AC_KERNEL_D_REVALIDATE_NAMEIDATA\n        ZFS_AC_KERNEL_CONST_DENTRY_OPERATIONS\n        ZFS_AC_KERNEL_S_D_OP\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}