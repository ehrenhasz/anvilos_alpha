{
  "module_name": "kernel-global_page_state.m4",
  "hash_id": "aab95e63296661a4b60185cd88fe78b69e74ec538148ac1ad857e92f4d8b6e39",
  "original_prompt": "Ingested from zfs-2.2.2/config/kernel-global_page_state.m4",
  "human_readable_source": "dnl #\ndnl # 4.8 API change\ndnl #\ndnl # 75ef71840539 mm, vmstat: add infrastructure for per-node vmstats\ndnl # 599d0c954f91 mm, vmscan: move LRU lists to node\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_GLOBAL_NODE_PAGE_STATE], [\n\tZFS_LINUX_TEST_SRC([global_node_page_state], [\n\t\t#include <linux/mm.h>\n\t\t#include <linux/vmstat.h>\n\t],[\n\t\t(void) global_node_page_state(0);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_GLOBAL_NODE_PAGE_STATE], [\n\tAC_MSG_CHECKING([whether global_node_page_state() exists])\n\tZFS_LINUX_TEST_RESULT([global_node_page_state], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(ZFS_GLOBAL_NODE_PAGE_STATE, 1,\n\t\t    [global_node_page_state() exists])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\ndnl #\ndnl # 4.14 API change\ndnl #\ndnl # c41f012ade0b mm: rename global_page_state to global_zone_page_state\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_GLOBAL_ZONE_PAGE_STATE], [\n\tZFS_LINUX_TEST_SRC([global_zone_page_state], [\n\t\t#include <linux/mm.h>\n\t\t#include <linux/vmstat.h>\n\t],[\n\t\t(void) global_zone_page_state(0);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_GLOBAL_ZONE_PAGE_STATE], [\n\tAC_MSG_CHECKING([whether global_zone_page_state() exists])\n\tZFS_LINUX_TEST_RESULT([global_zone_page_state], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(ZFS_GLOBAL_ZONE_PAGE_STATE, 1,\n\t\t    [global_zone_page_state() exists])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\ndnl #\ndnl # Create a define and autoconf variable for an enum member\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_ENUM_MEMBER], [\n\tAC_MSG_CHECKING([whether enum $2 contains $1])\n\tAS_IF([AC_TRY_COMMAND(\n\t    \"${srcdir}/scripts/enum-extract.pl\" \"$2\" \"$3\" | grep -Eqx $1)],[\n\t\tAC_MSG_RESULT([yes])\n\t\tAC_DEFINE(m4_join([_], [ZFS_ENUM], m4_toupper($2), $1), 1,\n\t\t    [enum $2 contains $1])\n\t\tm4_join([_], [ZFS_ENUM], m4_toupper($2), $1)=1\n\t],[\n\t\tAC_MSG_RESULT([no])\n\t])\n])\n\ndnl #\ndnl # Sanity check helpers\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_ERROR],[\n\tAC_MSG_RESULT(no)\n\tAC_MSG_RESULT([$1 in either node_stat_item or zone_stat_item: $2])\n\tZFS_LINUX_TEST_ERROR([global page state])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_CHECK], [\n\tenum_check_a=\"m4_join([_], [$ZFS_ENUM_NODE_STAT_ITEM], $1)\"\n\tenum_check_b=\"m4_join([_], [$ZFS_ENUM_ZONE_STAT_ITEM], $1)\"\n\tAS_IF([test -n \"$enum_check_a\" -a -n \"$enum_check_b\"],[\n\t\tZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_ERROR([$1], [DUPLICATE])\n\t])\n\tAS_IF([test -z \"$enum_check_a\" -a -z \"$enum_check_b\"],[\n\t\tZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_ERROR([$1], [NOT FOUND])\n\t])\n])\n\ndnl #\ndnl # Ensure the config tests are finding one and only one of each enum.\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_GLOBAL_ZONE_PAGE_STATE_SANITY], [\n\tAC_MSG_CHECKING([whether global_page_state enums are sane])\n\n\tZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_CHECK([NR_FILE_PAGES])\n\tZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_CHECK([NR_INACTIVE_ANON])\n\tZFS_AC_KERNEL_GLOBAL_PAGE_STATE_ENUM_CHECK([NR_INACTIVE_FILE])\n\n\tAC_MSG_RESULT(yes)\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_SRC_GLOBAL_PAGE_STATE], [\n\tZFS_AC_KERNEL_SRC_GLOBAL_NODE_PAGE_STATE\n\tZFS_AC_KERNEL_SRC_GLOBAL_ZONE_PAGE_STATE\n])\n\ndnl #\ndnl # enum members in which we're interested\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_GLOBAL_PAGE_STATE], [\n\tZFS_AC_KERNEL_GLOBAL_NODE_PAGE_STATE\n\tZFS_AC_KERNEL_GLOBAL_ZONE_PAGE_STATE\n\n\tZFS_AC_KERNEL_ENUM_MEMBER([NR_FILE_PAGES],\n\t    [node_stat_item], [$LINUX/include/linux/mmzone.h])\n\tZFS_AC_KERNEL_ENUM_MEMBER([NR_INACTIVE_ANON],\n\t    [node_stat_item], [$LINUX/include/linux/mmzone.h])\n\tZFS_AC_KERNEL_ENUM_MEMBER([NR_INACTIVE_FILE],\n\t    [node_stat_item], [$LINUX/include/linux/mmzone.h])\n\n\tZFS_AC_KERNEL_ENUM_MEMBER([NR_FILE_PAGES],\n\t    [zone_stat_item], [$LINUX/include/linux/mmzone.h])\n\tZFS_AC_KERNEL_ENUM_MEMBER([NR_INACTIVE_ANON],\n\t    [zone_stat_item], [$LINUX/include/linux/mmzone.h])\n\tZFS_AC_KERNEL_ENUM_MEMBER([NR_INACTIVE_FILE],\n\t    [zone_stat_item], [$LINUX/include/linux/mmzone.h])\n\n\tZFS_AC_KERNEL_GLOBAL_ZONE_PAGE_STATE_SANITY\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}