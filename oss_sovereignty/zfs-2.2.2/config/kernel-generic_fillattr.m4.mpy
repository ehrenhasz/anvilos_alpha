{
  "module_name": "kernel-generic_fillattr.m4",
  "hash_id": "58a842fe0a58ee4fe1ebd8d1df0abffb2090823ef4cc5fb5b8550657374c6dc1",
  "original_prompt": "Ingested from zfs-2.2.2/config/kernel-generic_fillattr.m4",
  "human_readable_source": "dnl #\ndnl # 5.12 API\ndnl #\ndnl # generic_fillattr in linux/fs.h now requires a struct user_namespace*\ndnl # as the first arg, to support idmapped mounts.\ndnl #\ndnl # 6.3 API\ndnl # generic_fillattr() now takes struct mnt_idmap* as the first argument\ndnl #\ndnl # 6.6 API\ndnl # generic_fillattr() now takes u32 as second argument, representing a\ndnl # request_mask for statx\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_GENERIC_FILLATTR], [\n\tZFS_LINUX_TEST_SRC([generic_fillattr_userns], [\n\t\t#include <linux/fs.h>\n\t],[\n\t\tstruct user_namespace *userns = NULL;\n\t\tstruct inode *in = NULL;\n\t\tstruct kstat *k = NULL;\n\t\tgeneric_fillattr(userns, in, k);\n\t])\n\n\tZFS_LINUX_TEST_SRC([generic_fillattr_mnt_idmap], [\n\t\t#include <linux/fs.h>\n\t],[\n\t\tstruct mnt_idmap *idmap = NULL;\n\t\tstruct inode *in = NULL;\n\t\tstruct kstat *k = NULL;\n\t\tgeneric_fillattr(idmap, in, k);\n\t])\n\n\tZFS_LINUX_TEST_SRC([generic_fillattr_mnt_idmap_reqmask], [\n\t\t#include <linux/fs.h>\n\t],[\n\t\tstruct mnt_idmap *idmap = NULL;\n\t\tstruct inode *in = NULL;\n\t\tstruct kstat *k = NULL;\n\t\tgeneric_fillattr(idmap, 0, in, k);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_GENERIC_FILLATTR], [\n\tAC_MSG_CHECKING(\n\t    [whether generic_fillattr requires struct mnt_idmap* and request_mask])\n\tZFS_LINUX_TEST_RESULT([generic_fillattr_mnt_idmap_reqmask], [\n\t\tAC_MSG_RESULT([yes])\n\t\tAC_DEFINE(HAVE_GENERIC_FILLATTR_IDMAP_REQMASK, 1,\n\t\t    [generic_fillattr requires struct mnt_idmap* and u32 request_mask])\n\t],[\n\t\tAC_MSG_CHECKING([whether generic_fillattr requires struct mnt_idmap*])\n\t\tZFS_LINUX_TEST_RESULT([generic_fillattr_mnt_idmap], [\n\t\t\tAC_MSG_RESULT([yes])\n\t\t\tAC_DEFINE(HAVE_GENERIC_FILLATTR_IDMAP, 1,\n\t\t\t\t[generic_fillattr requires struct mnt_idmap*])\n\t\t],[\n\t\t\tAC_MSG_CHECKING([whether generic_fillattr requires struct user_namespace*])\n\t\t\tZFS_LINUX_TEST_RESULT([generic_fillattr_userns], [\n\t\t\t\tAC_MSG_RESULT([yes])\n\t\t\t\tAC_DEFINE(HAVE_GENERIC_FILLATTR_USERNS, 1,\n\t\t\t\t\t[generic_fillattr requires struct user_namespace*])\n\t\t\t],[\n\t\t\t\tAC_MSG_RESULT([no])\n\t\t\t])\n\t\t])\n\t])\n])\n\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}