{
  "module_name": "kernel-rw.m4",
  "hash_id": "3d4ca2237d452feb03ac724894b1e28c4073c51474b75974efc5b8e640d69b2c",
  "original_prompt": "Ingested from zfs-2.2.2/config/kernel-rw.m4",
  "human_readable_source": "dnl #\ndnl # 4.14 API change\ndnl # kernel_write() which was introduced in 3.9 was updated to take\ndnl # the offset as a pointer which is needed by vn_rdwr().\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_WRITE], [\n\tZFS_LINUX_TEST_SRC([kernel_write], [\n\t\t#include <linux/fs.h>\n\t],[\n\t\tstruct file *file = NULL;\n\t\tconst void *buf = NULL;\n\t\tsize_t count = 0;\n\t\tloff_t *pos = NULL;\n\t\tssize_t ret;\n\n\t\tret = kernel_write(file, buf, count, pos);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_WRITE], [\n\tAC_MSG_CHECKING([whether kernel_write() takes loff_t pointer])\n\tZFS_LINUX_TEST_RESULT([kernel_write], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_KERNEL_WRITE_PPOS, 1,\n\t\t    [kernel_write() take loff_t pointer])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\ndnl #\ndnl # 4.14 API change\ndnl # kernel_read() which has existed for forever was updated to take\ndnl # the offset as a pointer which is needed by vn_rdwr().\ndnl #\nAC_DEFUN([ZFS_AC_KERNEL_SRC_READ], [\n\tZFS_LINUX_TEST_SRC([kernel_read], [\n\t\t#include <linux/fs.h>\n\t],[\n\t\tstruct file *file = NULL;\n\t\tvoid *buf = NULL;\n\t\tsize_t count = 0;\n\t\tloff_t *pos = NULL;\n\t\tssize_t ret;\n\n\t\tret = kernel_read(file, buf, count, pos);\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_READ], [\n\tAC_MSG_CHECKING([whether kernel_read() takes loff_t pointer])\n\tZFS_LINUX_TEST_RESULT([kernel_read], [\n\t\tAC_MSG_RESULT(yes)\n\t\tAC_DEFINE(HAVE_KERNEL_READ_PPOS, 1,\n\t\t    [kernel_read() take loff_t pointer])\n\t],[\n\t\tAC_MSG_RESULT(no)\n\t])\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_SRC_RW], [\n\tZFS_AC_KERNEL_SRC_WRITE\n\tZFS_AC_KERNEL_SRC_READ\n])\n\nAC_DEFUN([ZFS_AC_KERNEL_RW], [\n\tZFS_AC_KERNEL_WRITE\n\tZFS_AC_KERNEL_READ\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}