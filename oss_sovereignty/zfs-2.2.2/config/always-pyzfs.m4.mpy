{
  "module_name": "always-pyzfs.m4",
  "hash_id": "9bc54854df243adab72a1004d3fd70febb72dc5feaf3d6c1124a8dac42aa03de",
  "original_prompt": "Ingested from zfs-2.2.2/config/always-pyzfs.m4",
  "human_readable_source": "dnl #\ndnl # ZFS_AC_PYTHON_MODULE(module_name, [action-if-true], [action-if-false])\ndnl #\ndnl # Checks for Python module. Freely inspired by AX_PYTHON_MODULE\ndnl # https://www.gnu.org/software/autoconf-archive/ax_python_module.html\ndnl # Required by ZFS_AC_CONFIG_ALWAYS_PYZFS.\ndnl #\nAC_DEFUN([ZFS_AC_PYTHON_MODULE], [\n\tPYTHON_NAME=${PYTHON##*/}\n\tAC_MSG_CHECKING([for $PYTHON_NAME module: $1])\n\tAS_IF([$PYTHON -c \"import $1\" 2>/dev/null], [\n\t\tAC_MSG_RESULT(yes)\n\t\tm4_ifvaln([$2], [$2])\n\t], [\n\t\tAC_MSG_RESULT(no)\n\t\tm4_ifvaln([$3], [$3])\n\t])\n])\n\ndnl #\ndnl # Determines if pyzfs can be built, requires Python 3.6 or later.\ndnl #\nAC_DEFUN([ZFS_AC_CONFIG_ALWAYS_PYZFS], [\n\tAC_ARG_ENABLE([pyzfs],\n\t\tAS_HELP_STRING([--enable-pyzfs],\n\t\t[install libzfs_core python bindings @<:@default=check@:>@]),\n\t\t[enable_pyzfs=$enableval],\n\t\t[enable_pyzfs=check])\n\n\tdnl #\n\tdnl # Packages for pyzfs specifically enabled/disabled.\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" != xcheck], [\n\t\tAS_IF([test \"x$enable_pyzfs\" = xyes], [\n\t\t\tDEFINE_PYZFS='--with pyzfs'\n\t\t], [\n\t\t\tDEFINE_PYZFS='--without pyzfs'\n\t\t])\n\t], [\n\t\tAS_IF([test \"$PYTHON\" != :], [\n\t\t\tDEFINE_PYZFS=''\n\t\t], [\n\t\t\tenable_pyzfs=no\n\t\t\tDEFINE_PYZFS='--without pyzfs'\n\t\t])\n\t])\n\tAC_SUBST(DEFINE_PYZFS)\n\n\tdnl #\n\tdnl # Autodetection disables pyzfs if kernel or srpm config\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" = xcheck], [\n\t\tAS_IF([test \"x$ZFS_CONFIG\" = xkernel -o \"x$ZFS_CONFIG\" = xsrpm ], [\n\t\t\t\tenable_pyzfs=no\n\t\t\t\tAC_MSG_NOTICE([Disabling pyzfs for kernel/srpm config])\n\t\t])\n\t])\n\n\tdnl #\n\tdnl # Python \"packaging\" (or, failing that, \"distlib\") module is required to build and install pyzfs\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" = xcheck -o \"x$enable_pyzfs\" = xyes], [\n\t\tZFS_AC_PYTHON_MODULE([packaging], [], [\n\t\t\tZFS_AC_PYTHON_MODULE([distlib], [], [\n\t\t\t\tAS_IF([test \"x$enable_pyzfs\" = xyes], [\n\t\t\t\t\tAC_MSG_ERROR(\"Python $PYTHON_VERSION packaging and distlib modules are not installed\")\n\t\t\t\t], [test \"x$enable_pyzfs\" != xno], [\n\t\t\t\t\tenable_pyzfs=no\n\t\t\t\t])\n\t\t\t])\n\t\t])\n\t])\n\n\tdnl #\n\tdnl # Require python3-devel libraries\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" = xcheck  -o \"x$enable_pyzfs\" = xyes], [\n\t\tAS_CASE([$PYTHON_VERSION],\n\t\t\t[3.*], [PYTHON_REQUIRED_VERSION=\">= '3.6.0'\"],\n\t\t\t[AC_MSG_ERROR(\"Python $PYTHON_VERSION unknown\")]\n\t\t)\n\n\t\tAX_PYTHON_DEVEL([$PYTHON_REQUIRED_VERSION], [\n\t\t\tAS_IF([test \"x$enable_pyzfs\" = xyes], [\n\t\t\t\tAC_MSG_ERROR(\"Python $PYTHON_REQUIRED_VERSION development library is not installed\")\n\t\t\t], [test \"x$enable_pyzfs\" != xno], [\n\t\t\t\tenable_pyzfs=no\n\t\t\t])\n\t\t])\n\t])\n\n\tdnl #\n\tdnl # Python \"setuptools\" module is required to build and install pyzfs\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" = xcheck -o \"x$enable_pyzfs\" = xyes], [\n\t\tZFS_AC_PYTHON_MODULE([setuptools], [], [\n\t\t\tAS_IF([test \"x$enable_pyzfs\" = xyes], [\n\t\t\t\tAC_MSG_ERROR(\"Python $PYTHON_VERSION setuptools is not installed\")\n\t\t\t], [test \"x$enable_pyzfs\" != xno], [\n\t\t\t\tenable_pyzfs=no\n\t\t\t])\n\t\t])\n\t])\n\n\tdnl #\n\tdnl # Python \"cffi\" module is required to run pyzfs\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" = xcheck -o \"x$enable_pyzfs\" = xyes], [\n\t\tZFS_AC_PYTHON_MODULE([cffi], [], [\n\t\t\tAS_IF([test \"x$enable_pyzfs\" = xyes], [\n\t\t\t\tAC_MSG_ERROR(\"Python $PYTHON_VERSION cffi is not installed\")\n\t\t\t], [test \"x$enable_pyzfs\" != xno], [\n\t\t\t\tenable_pyzfs=no\n\t\t\t])\n\t\t])\n\t])\n\n\tdnl #\n\tdnl # Set enable_pyzfs to 'yes' if every check passed\n\tdnl #\n\tAS_IF([test \"x$enable_pyzfs\" = xcheck], [enable_pyzfs=yes])\n\n\tAM_CONDITIONAL([PYZFS_ENABLED], [test \"x$enable_pyzfs\" = xyes])\n\tAC_SUBST([PYZFS_ENABLED], [$enable_pyzfs])\n\tAC_SUBST(pythonsitedir, [$PYTHON_SITE_PKG])\n\n\tAC_MSG_CHECKING([whether to enable pyzfs: ])\n\tAC_MSG_RESULT($enable_pyzfs)\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}