{
  "module_name": "timespec.m4",
  "hash_id": "864bc93a8da61259ea2d24310d484f719d2a1937b4049e79dad6824d73a8a2b7",
  "original_prompt": "Ingested from bash-5.2.21/m4/timespec.m4",
  "human_readable_source": "# Configure checks for struct timespec\n\n# Copyright (C) 2000-2001, 2003-2007, 2009-2011, 2012 Free Software Foundation, Inc.\n\n# This file is free software; the Free Software Foundation\n# gives unlimited permission to copy and/or distribute it,\n# with or without modifications, as long as this notice is preserved.\n\n# Original written by Paul Eggert and Jim Meyering.\n# Modified by Chet Ramey for bash\n\ndnl Define HAVE_STRUCT_TIMESPEC if `struct timespec' is declared\ndnl in time.h, sys/time.h, or pthread.h.\n\nAC_DEFUN([BASH_CHECK_TYPE_STRUCT_TIMESPEC],\n[\n  AC_CHECK_HEADERS_ONCE([sys/time.h])\n  AC_CACHE_CHECK([for struct timespec in <time.h>],\n    [bash_cv_sys_struct_timespec_in_time_h],\n    [AC_COMPILE_IFELSE(\n       [AC_LANG_PROGRAM(\n          [[#include <time.h>\n          ]],\n          [[static struct timespec x; x.tv_sec = x.tv_nsec;]])],\n       [bash_cv_sys_struct_timespec_in_time_h=yes],\n       [bash_cv_sys_struct_timespec_in_time_h=no])])\n\n  HAVE_STRUCT_TIMESPEC=0\n  TIME_H_DEFINES_STRUCT_TIMESPEC=0\n  SYS_TIME_H_DEFINES_STRUCT_TIMESPEC=0\n  PTHREAD_H_DEFINES_STRUCT_TIMESPEC=0\n  if test $bash_cv_sys_struct_timespec_in_time_h = yes; then\n    AC_DEFINE([HAVE_STRUCT_TIMESPEC])\n    AC_DEFINE([TIME_H_DEFINES_STRUCT_TIMESPEC])\n    TIME_H_DEFINES_STRUCT_TIMESPEC=1\n  else\n    AC_CACHE_CHECK([for struct timespec in <sys/time.h>],\n      [bash_cv_sys_struct_timespec_in_sys_time_h],\n      [AC_COMPILE_IFELSE(\n         [AC_LANG_PROGRAM(\n            [[#include <sys/time.h>\n            ]],\n            [[static struct timespec x; x.tv_sec = x.tv_nsec;]])],\n         [bash_cv_sys_struct_timespec_in_sys_time_h=yes],\n         [bash_cv_sys_struct_timespec_in_sys_time_h=no])])\n    if test $bash_cv_sys_struct_timespec_in_sys_time_h = yes; then\n      SYS_TIME_H_DEFINES_STRUCT_TIMESPEC=1\n      AC_DEFINE([HAVE_STRUCT_TIMESPEC])\n      AC_DEFINE([SYS_TIME_H_DEFINES_STRUCT_TIMESPEC])\n    else\n      AC_CACHE_CHECK([for struct timespec in <pthread.h>],\n        [bash_cv_sys_struct_timespec_in_pthread_h],\n        [AC_COMPILE_IFELSE(\n           [AC_LANG_PROGRAM(\n              [[#include <pthread.h>\n              ]],\n              [[static struct timespec x; x.tv_sec = x.tv_nsec;]])],\n           [bash_cv_sys_struct_timespec_in_pthread_h=yes],\n           [bash_cv_sys_struct_timespec_in_pthread_h=no])])\n      if test $bash_cv_sys_struct_timespec_in_pthread_h = yes; then\n        PTHREAD_H_DEFINES_STRUCT_TIMESPEC=1\n\tAC_DEFINE([HAVE_STRUCT_TIMESPEC])\n\tAC_DEFINE([PTHREAD_H_DEFINES_STRUCT_TIMESPEC])\n      fi\n    fi\n  fi\n  AC_SUBST([TIME_H_DEFINES_STRUCT_TIMESPEC])\n  AC_SUBST([SYS_TIME_H_DEFINES_STRUCT_TIMESPEC])\n  AC_SUBST([PTHREAD_H_DEFINES_STRUCT_TIMESPEC])\n\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}