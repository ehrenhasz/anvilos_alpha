{
  "module_name": "intl-thread-locale.m4",
  "hash_id": "86d618cb7ce26a9147a9b646e401002d144b26e552e099d324fd58de40a7a8ae",
  "original_prompt": "Ingested from bash-5.2.21/m4/intl-thread-locale.m4",
  "human_readable_source": "# intl-thread-locale.m4 serial 4\ndnl Copyright (C) 2015-2019 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\ndnl\ndnl This file can be used in projects which are not available under\ndnl the GNU General Public License or the GNU Library General Public\ndnl License but which still want to provide support for the GNU gettext\ndnl functionality.\ndnl Please note that the actual code of the GNU gettext library is covered\ndnl by the GNU Library General Public License, and the rest of the GNU\ndnl gettext package is covered by the GNU General Public License.\ndnl They are *not* in the public domain.\n\ndnl Check how to retrieve the name of a per-thread locale (POSIX locale_t).\ndnl Sets gt_nameless_locales.\nAC_DEFUN([gt_INTL_THREAD_LOCALE_NAME],\n[\n  AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n\n  dnl Persuade Solaris <locale.h> to define 'locale_t'.\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  dnl Test whether uselocale() exists and works at all.\n  gt_FUNC_USELOCALE\n\n  dnl On OpenBSD >= 6.2, the locale_t type and the uselocale(), newlocale(),\n  dnl duplocale(), freelocale() functions exist but are effectively useless,\n  dnl because the locale_t value depends only on the LC_CTYPE category of the\n  dnl locale and furthermore contains only one bit of information (it\n  dnl distinguishes the \"C\" locale from the *.UTF-8 locales). See\n  dnl <https://cvsweb.openbsd.org/src/lib/libc/locale/newlocale.c?rev=1.1&content-type=text/x-cvsweb-markup>.\n  dnl In the setlocale() implementation they have thought about the programs\n  dnl that use the API (\"Even though only LC_CTYPE has any effect in the\n  dnl OpenBSD base system, store complete information about the global locale,\n  dnl such that third-party software can access it\"), but for uselocale()\n  dnl they did not think about the programs.\n  dnl In this situation, even the HAVE_NAMELESS_LOCALES support does not work.\n  dnl So, define HAVE_FAKE_LOCALES and disable all locale_t support.\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_CHECK_HEADERS_ONCE([xlocale.h])\n      AC_CACHE_CHECK([for fake locale system (OpenBSD)],\n        [gt_cv_locale_fake],\n        [AC_RUN_IFELSE(\n           [AC_LANG_SOURCE([[\n#include <locale.h>\n#if HAVE_XLOCALE_H\n# include <xlocale.h>\n#endif\nint main ()\n{\n  locale_t loc1, loc2;\n  if (setlocale (LC_ALL, \"de_DE.UTF-8\") == NULL) return 1;\n  if (setlocale (LC_ALL, \"fr_FR.UTF-8\") == NULL) return 1;\n  loc1 = newlocale (LC_ALL_MASK, \"de_DE.UTF-8\", (locale_t)0);\n  loc2 = newlocale (LC_ALL_MASK, \"fr_FR.UTF-8\", (locale_t)0);\n  return !(loc1 == loc2);\n}]])],\n           [gt_cv_locale_fake=yes],\n           [gt_cv_locale_fake=no],\n           [dnl Guess the locale system is fake only on OpenBSD.\n            case \"$host_os\" in\n              openbsd*) gt_cv_locale_fake=\"guessing yes\" ;;\n              *)        gt_cv_locale_fake=\"guessing no\" ;;\n            esac\n           ])\n        ])\n      ;;\n    *) gt_cv_locale_fake=no ;;\n  esac\n  case \"$gt_cv_locale_fake\" in\n    *yes)\n      AC_DEFINE([HAVE_FAKE_LOCALES], [1],\n        [Define if the locale_t type contains insufficient information, as on OpenBSD.])\n      ;;\n  esac\n\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_CACHE_CHECK([for Solaris 11.4 locale system],\n        [gt_cv_locale_solaris114],\n        [case \"$host_os\" in\n           solaris*)\n             dnl Test whether <locale.h> defines locale_t as a typedef of\n             dnl 'struct _LC_locale_t **' (whereas Illumos defines it as a\n             dnl typedef of 'struct _locale *').\n             dnl Another possible test would be to include <sys/localedef.h>\n             dnl and test whether it defines the _LC_core_data_locale_t type.\n             dnl This type was added in Solaris 11.4.\n             AC_COMPILE_IFELSE(\n               [AC_LANG_PROGRAM([[\n                  #include <locale.h>\n                  struct _LC_locale_t *x;\n                  locale_t y;\n                ]],\n                [[*y = x;]])],\n               [gt_cv_locale_solaris114=yes],\n               [gt_cv_locale_solaris114=no])\n             ;;\n           *) gt_cv_locale_solaris114=no ;;\n         esac\n        ])\n      ;;\n    *) gt_cv_locale_solaris114=no ;;\n  esac\n  if test $gt_cv_locale_solaris114 = yes; then\n    AC_DEFINE([HAVE_SOLARIS114_LOCALES], [1],\n      [Define if the locale_t type is as on Solaris 11.4.])\n  fi\n\n  dnl Solaris 12 will maybe provide getlocalename_l.  If it does, it will\n  dnl improve the implementation of gl_locale_name_thread(), by removing\n  dnl the use of undocumented structures.\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_CHECK_FUNCS([getlocalename_l])\n      ;;\n  esac\n\n  dnl This code is for future use, in case we some day have to port to a\n  dnl platform where the locale_t type does not provide access to the name of\n  dnl each locale category.  This code has the drawback that it requires the\n  dnl gnulib overrides of 'newlocale', 'duplocale', 'freelocale', which is a\n  dnl problem for GNU libunistring.  Therefore try hard to avoid enabling this\n  dnl code!\n  gt_nameless_locales=no\n  if false; then\n    gt_nameless_locales=yes\n    AC_DEFINE([HAVE_NAMELESS_LOCALES], [1],\n      [Define if the locale_t type does not contain the name of each locale category.])\n  fi\n])\n\ndnl Tests whether uselocale() exists and is usable.\ndnl Sets gt_cv_func_uselocale_works. Defines HAVE_WORKING_USELOCALE.\nAC_DEFUN([gt_FUNC_USELOCALE],\n[\n  AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n\n  dnl Persuade Solaris <locale.h> to define 'locale_t'.\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  AC_CHECK_FUNCS_ONCE([uselocale])\n\n  dnl On AIX 7.2, the uselocale() function is not documented and leads to\n  dnl crashes in subsequent setlocale() invocations.\n  if test $ac_cv_func_uselocale = yes; then\n    AC_CHECK_HEADERS_ONCE([xlocale.h])\n    AC_CACHE_CHECK([whether uselocale works],\n      [gt_cv_func_uselocale_works],\n      [AC_RUN_IFELSE(\n         [AC_LANG_SOURCE([[\n#include <locale.h>\n#if HAVE_XLOCALE_H\n# include <xlocale.h>\n#endif\nint main ()\n{\n  uselocale (NULL);\n  setlocale (LC_ALL, \"en_US.UTF-8\");\n  return 0;\n}]])],\n         [gt_cv_func_uselocale_works=yes],\n         [gt_cv_func_uselocale_works=no],\n         [# Guess no on AIX, yes otherwise.\n          case \"$host_os\" in\n            aix*) gt_cv_func_uselocale_works=\"guessing no\" ;;\n            *)    gt_cv_func_uselocale_works=\"guessing yes\" ;;\n          esac\n         ])\n      ])\n  else\n    gt_cv_func_uselocale_works=no\n  fi\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_DEFINE([HAVE_WORKING_USELOCALE], [1],\n        [Define if the uselocale function exists any may safely be called.])\n      ;;\n  esac\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}