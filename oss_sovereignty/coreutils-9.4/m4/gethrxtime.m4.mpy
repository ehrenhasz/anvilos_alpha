{
  "module_name": "gethrxtime.m4",
  "hash_id": "d6d46866d6400de5bea36f3acf70bb6e44b09ba9c81b52a40198af39c8efd900",
  "original_prompt": "Ingested from coreutils-9.4/m4/gethrxtime.m4",
  "human_readable_source": "# gethrxtime.m4 serial 15\ndnl Copyright (C) 2005-2006, 2008-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\ndnl Written by Paul Eggert.\n\nAC_DEFUN([gl_GETHRXTIME],\n[\n  AC_REQUIRE([gl_ARITHMETIC_HRTIME_T])\n  AC_REQUIRE([gl_USE_SYSTEM_EXTENSIONS])\n  AC_REQUIRE([gl_XTIME])\n  AC_CHECK_DECLS([gethrtime], [], [], [[#include <time.h>]])\n  GETHRXTIME_LIB=\n  if test $ac_cv_have_decl_gethrtime = no \\\n     || test $gl_cv_arithmetic_hrtime_t = no; then\n    dnl Find libraries needed to link lib/gethrxtime.c.\n    AC_REQUIRE([gl_CLOCK_TIME])\n    AC_REQUIRE([gl_USE_SYSTEM_EXTENSIONS])\n    AC_CHECK_FUNCS_ONCE([microuptime nanouptime])\n    if test $ac_cv_func_nanouptime != yes \\\n       && { test $ac_cv_have_decl_gethrtime = no \\\n            || test $gl_cv_arithmetic_hrtime_t = no; }; then\n      AC_CACHE_CHECK([whether CLOCK_MONOTONIC or CLOCK_REALTIME is defined],\n        [gl_cv_have_clock_gettime_macro],\n        [AC_EGREP_CPP([have_clock_gettime_macro],\n          [\n#          include <time.h>\n#          if defined CLOCK_MONOTONIC || defined CLOCK_REALTIME\n            have_clock_gettime_macro\n#          endif\n          ],\n          [gl_cv_have_clock_gettime_macro=yes],\n          [gl_cv_have_clock_gettime_macro=no])])\n      if test $gl_cv_have_clock_gettime_macro = yes; then\n        GETHRXTIME_LIB=$CLOCK_TIME_LIB\n      fi\n    fi\n  fi\n  AC_SUBST([GETHRXTIME_LIB])\n  dnl For backward compatibility.\n  LIB_GETHRXTIME=\"$GETHRXTIME_LIB\"\n  AC_SUBST([LIB_GETHRXTIME])\n])\n\n# Test whether hrtime_t is an arithmetic type.\n# It is not arithmetic in older Solaris c89 (which insists on\n# not having a long long int type).\nAC_DEFUN([gl_ARITHMETIC_HRTIME_T],\n[\n  AC_REQUIRE([gl_USE_SYSTEM_EXTENSIONS])\n  AC_CACHE_CHECK([for arithmetic hrtime_t], [gl_cv_arithmetic_hrtime_t],\n    [AC_COMPILE_IFELSE(\n       [AC_LANG_PROGRAM([[#include <time.h>]],\n          [[hrtime_t x = 0; return x/x;]])],\n       [gl_cv_arithmetic_hrtime_t=yes],\n       [gl_cv_arithmetic_hrtime_t=no])])\n  if test $gl_cv_arithmetic_hrtime_t = yes; then\n    AC_DEFINE([HAVE_ARITHMETIC_HRTIME_T], [1],\n      [Define if you have an arithmetic hrtime_t type.])\n  fi\n])\n\n# Prerequisites of lib/xtime.h.\nAC_DEFUN([gl_XTIME],\n[\n  :\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}