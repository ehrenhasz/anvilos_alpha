{
  "module_name": "getgroups.m4",
  "hash_id": "9cfa0628283b27e5bd450675cfd3bd912bc0f5db26d45c0e78745b5cc210438b",
  "original_prompt": "Ingested from coreutils-9.4/m4/getgroups.m4",
  "human_readable_source": "# serial 24\n\ndnl From Jim Meyering.\ndnl A wrapper around AC_FUNC_GETGROUPS.\n\n# Copyright (C) 1996-1997, 1999-2004, 2008-2023 Free Software Foundation, Inc.\n#\n# This file is free software; the Free Software Foundation\n# gives unlimited permission to copy and/or distribute it,\n# with or without modifications, as long as this notice is preserved.\n\n# This is taken from the following Autoconf patch:\n# https://git.savannah.gnu.org/gitweb/?p=autoconf.git;a=commitdiff;h=7fbb553727ed7e0e689a17594b58559ecf3ea6e9\nAC_DEFUN([AC_FUNC_GETGROUPS],\n[\n  AC_REQUIRE([AC_TYPE_GETGROUPS])dnl\n  AC_REQUIRE([AC_TYPE_SIZE_T])dnl\n  AC_REQUIRE([AC_CANONICAL_HOST])dnl for cross-compiles\n  AC_CHECK_FUNC([getgroups])\n\n  # If we don't yet have getgroups, see if it's in -lbsd.\n  # This is reported to be necessary on an ITOS 3000WS running SEIUX 3.1.\n  ac_save_LIBS=$LIBS\n  if test $ac_cv_func_getgroups = no; then\n    AC_CHECK_LIB(bsd, getgroups, [GETGROUPS_LIB=-lbsd])\n  fi\n\n  # Run the program to test the functionality of the system-supplied\n  # getgroups function only if there is such a function.\n  if test $ac_cv_func_getgroups = yes; then\n    AC_CACHE_CHECK([for working getgroups], [ac_cv_func_getgroups_works],\n      [AC_RUN_IFELSE(\n         [AC_LANG_PROGRAM(\n            [AC_INCLUDES_DEFAULT],\n            [[/* On NeXTstep 3.2, getgroups (0, 0) always fails.  */\n              return getgroups (0, 0) == -1;]])\n         ],\n         [ac_cv_func_getgroups_works=yes],\n         [ac_cv_func_getgroups_works=no],\n         [case \"$host_os\" in # ((\n                           # Guess yes on glibc systems.\n            *-gnu* | gnu*) ac_cv_func_getgroups_works=\"guessing yes\" ;;\n                           # Guess yes on musl systems.\n            *-musl*)       ac_cv_func_getgroups_works=\"guessing yes\" ;;\n                           # If we don't know, obey --enable-cross-guesses.\n            *)             ac_cv_func_getgroups_works=\"$gl_cross_guess_normal\" ;;\n          esac\n         ])\n      ])\n  else\n    ac_cv_func_getgroups_works=no\n  fi\n  case \"$ac_cv_func_getgroups_works\" in\n    *yes)\n      AC_DEFINE([HAVE_GETGROUPS], [1],\n        [Define to 1 if your system has a working `getgroups' function.])\n      ;;\n  esac\n  LIBS=$ac_save_LIBS\n])# AC_FUNC_GETGROUPS\n\nAC_DEFUN([gl_FUNC_GETGROUPS],\n[\n  AC_REQUIRE([AC_TYPE_GETGROUPS])\n  AC_REQUIRE([gl_UNISTD_H_DEFAULTS])\n  AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n\n  AC_FUNC_GETGROUPS\n  if test $ac_cv_func_getgroups != yes; then\n    HAVE_GETGROUPS=0\n  else\n    if test \"$ac_cv_type_getgroups\" != gid_t \\\n       || { case \"$ac_cv_func_getgroups_works\" in\n              *yes) false;;\n              *) true;;\n            esac\n          }; then\n      REPLACE_GETGROUPS=1\n      AC_DEFINE([GETGROUPS_ZERO_BUG], [1], [Define this to 1 if\n        getgroups(0,NULL) does not return the number of groups.])\n    else\n      dnl Detect Mac OS X and FreeBSD bug; POSIX requires getgroups(-1,ptr)\n      dnl to fail.\n      AC_CACHE_CHECK([whether getgroups handles negative values],\n        [gl_cv_func_getgroups_works],\n        [AC_RUN_IFELSE([AC_LANG_PROGRAM([AC_INCLUDES_DEFAULT],\n          [[int size = getgroups (0, 0);\n            gid_t *list = malloc (size * sizeof *list);\n            int result = getgroups (-1, list) != -1;\n            free (list);\n            return result;]])],\n          [gl_cv_func_getgroups_works=yes],\n          [gl_cv_func_getgroups_works=no],\n          [case \"$host_os\" in\n                            # Guess yes on glibc systems.\n             *-gnu* | gnu*) gl_cv_func_getgroups_works=\"guessing yes\" ;;\n                            # Guess yes on musl systems.\n             *-musl*)       gl_cv_func_getgroups_works=\"guessing yes\" ;;\n                            # If we don't know, obey --enable-cross-guesses.\n             *)             gl_cv_func_getgroups_works=\"$gl_cross_guess_normal\" ;;\n           esac\n          ])])\n      case \"$gl_cv_func_getgroups_works\" in\n        *yes) ;;\n        *) REPLACE_GETGROUPS=1 ;;\n      esac\n    fi\n  fi\n  test -n \"$GETGROUPS_LIB\" && LIBS=\"$GETGROUPS_LIB $LIBS\"\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}