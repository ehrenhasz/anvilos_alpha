{
  "module_name": "acl.m4",
  "hash_id": "e3016af2b373c454d7e9585adda3767c9a1738c65eea151d1ec9052bfdec0fc8",
  "original_prompt": "Ingested from coreutils-9.4/m4/acl.m4",
  "human_readable_source": "# acl.m4 - check for access control list (ACL) primitives\n# serial 29\n\n# Copyright (C) 2002, 2004-2023 Free Software Foundation, Inc.\n# This file is free software; the Free Software Foundation\n# gives unlimited permission to copy and/or distribute it,\n# with or without modifications, as long as this notice is preserved.\n\n# Written by Paul Eggert and Jim Meyering.\n\nAC_DEFUN([gl_FUNC_ACL_ARG],\n[\n  gl_need_lib_has_acl=\n  AC_ARG_ENABLE([acl],\n    AS_HELP_STRING([[--disable-acl]], [do not support ACLs]),\n    , [enable_acl=auto])\n])\n\n\nAC_DEFUN_ONCE([gl_FUNC_ACL],\n[\n  AC_REQUIRE([gl_FUNC_ACL_ARG])\n  AC_CHECK_FUNCS_ONCE([fchmod])\n  LIB_ACL=\n  use_acl=0\n  if test \"$enable_acl\" != no; then\n    dnl On all platforms, the ACL related API is declared in <sys/acl.h>.\n    AC_CHECK_HEADERS([sys/acl.h])\n    if test $ac_cv_header_sys_acl_h = yes; then\n      ac_save_LIBS=$LIBS\n\n      dnl Test for POSIX-draft-like API (GNU/Linux, FreeBSD, Mac OS X,\n      dnl IRIX, Tru64, Cygwin >= 2.5).\n      dnl -lacl is needed on GNU/Linux, -lpacl on OSF/1.\n      if test $use_acl = 0; then\n        AC_SEARCH_LIBS([acl_get_file], [acl pacl],\n          [if test \"$ac_cv_search_acl_get_file\" != \"none required\"; then\n             LIB_ACL=$ac_cv_search_acl_get_file\n           fi\n           AC_CHECK_FUNCS(\n             [acl_get_file acl_get_fd acl_set_file acl_set_fd \\\n              acl_free acl_from_mode acl_from_text \\\n              acl_delete_def_file acl_extended_file \\\n              acl_delete_fd_np acl_delete_file_np \\\n              acl_copy_ext_native acl_create_entry_np \\\n              acl_to_short_text acl_free_text])\n           # If the acl_get_file bug is detected, don't enable the ACL support.\n           gl_ACL_GET_FILE([use_acl=1], [])\n           if test $use_acl = 1; then\n             dnl On GNU/Linux, an additional API is declared in <acl/libacl.h>.\n             AC_CHECK_HEADERS([acl/libacl.h])\n             AC_REPLACE_FUNCS([acl_entries])\n             AC_CACHE_CHECK([for ACL_FIRST_ENTRY],\n               [gl_cv_acl_ACL_FIRST_ENTRY],\n               [AC_COMPILE_IFELSE([AC_LANG_PROGRAM(\n[[#include <sys/types.h>\n#include <sys/acl.h>\nint type = ACL_FIRST_ENTRY;]])],\n                  [gl_cv_acl_ACL_FIRST_ENTRY=yes],\n                  [gl_cv_acl_ACL_FIRST_ENTRY=no])])\n             if test $gl_cv_acl_ACL_FIRST_ENTRY = yes; then\n               AC_DEFINE([HAVE_ACL_FIRST_ENTRY], [1],\n                 [Define to 1 if the constant ACL_FIRST_ENTRY exists.])\n             fi\n             dnl On Mac OS X, other types of ACLs are supported.\n             AC_CACHE_CHECK([for ACL_TYPE_EXTENDED],\n               [gl_cv_acl_ACL_TYPE_EXTENDED],\n               [AC_COMPILE_IFELSE([AC_LANG_PROGRAM(\n[[#include <sys/types.h>\n#include <sys/acl.h>\nint type = ACL_TYPE_EXTENDED;]])],\n                  [gl_cv_acl_ACL_TYPE_EXTENDED=yes],\n                  [gl_cv_acl_ACL_TYPE_EXTENDED=no])])\n             if test $gl_cv_acl_ACL_TYPE_EXTENDED = yes; then\n               AC_DEFINE([HAVE_ACL_TYPE_EXTENDED], [1],\n                 [Define to 1 if the ACL type ACL_TYPE_EXTENDED exists.])\n             fi\n           else\n             LIB_ACL=\n           fi\n          ])\n      fi\n\n      dnl Test for Solaris API (Solaris, Cygwin).\n      if test $use_acl = 0; then\n        AC_CHECK_FUNCS([facl])\n        if test $ac_cv_func_facl = yes; then\n          AC_SEARCH_LIBS([acl_trivial], [sec],\n            [if test \"$ac_cv_search_acl_trivial\" != \"none required\"; then\n               LIB_ACL=$ac_cv_search_acl_trivial\n             fi\n            ])\n          AC_CHECK_FUNCS([acl_trivial])\n          use_acl=1\n        fi\n      fi\n\n      dnl Test for HP-UX API.\n      if test $use_acl = 0; then\n        AC_CHECK_FUNCS([getacl])\n        if test $ac_cv_func_getacl = yes; then\n          use_acl=1\n        fi\n        dnl Test for HP-UX 11.11 API.\n        AC_CHECK_HEADERS([aclv.h], [], [], [#include <sys/types.h>])\n      fi\n\n      dnl Test for AIX API (AIX 5.3 or newer).\n      if test $use_acl = 0; then\n        AC_CHECK_FUNCS([aclx_get])\n        if test $ac_cv_func_aclx_get = yes; then\n          use_acl=1\n        fi\n      fi\n\n      dnl Test for older AIX API.\n      if test $use_acl = 0 || test \"$ac_cv_func_aclx_get\" = yes; then\n        AC_CHECK_FUNCS([statacl])\n        if test $ac_cv_func_statacl = yes; then\n          use_acl=1\n        fi\n      fi\n\n      dnl Test for NonStop Kernel API.\n      if test $use_acl = 0; then\n        AC_CHECK_FUNCS([aclsort])\n        if test $ac_cv_func_aclsort = yes; then\n          use_acl=1\n        fi\n      fi\n\n      LIBS=$ac_save_LIBS\n    fi\n\n    if test \"$enable_acl$use_acl\" = yes0; then\n      AC_MSG_ERROR([ACLs enabled but support not detected])\n    elif test \"$enable_acl$use_acl\" = auto0; then\n      AC_MSG_WARN([libacl development library was not found or not usable.])\n      AC_MSG_WARN([AC_PACKAGE_NAME will be built without ACL support.])\n    fi\n  fi\n  if test -n \"$gl_need_lib_has_acl\"; then\n    FILE_HAS_ACL_LIB=$LIB_ACL\n  fi\n  AC_SUBST([LIB_ACL])\n  AC_DEFINE_UNQUOTED([USE_ACL], [$use_acl],\n    [Define to nonzero if you want access control list support.])\n  USE_ACL=$use_acl\n  AC_SUBST([USE_ACL])\n])\n\n# gl_ACL_GET_FILE(IF-WORKS, IF-NOT)\n# ---------------------------------\n# If 'acl_get_file' works (does not have a particular bug),\n# run IF-WORKS, otherwise, IF-NOT.\n# When building natively, test for a Darwin 8.7.0 bug, whereby acl_get_file\n# returns NULL, but sets errno = ENOENT for an existing file or directory.\n# When cross-compiling, assume that this old bug no longer applies.\nAC_DEFUN([gl_ACL_GET_FILE],\n[\n  AC_CACHE_CHECK([for working acl_get_file], [gl_cv_func_working_acl_get_file],\n    [gl_cv_func_working_acl_get_file=no\n     AC_LINK_IFELSE(\n       [AC_LANG_PROGRAM(\n          [[#include <sys/types.h>\n           #include <sys/acl.h>\n           #include <errno.h>\n          ]],\n          [[acl_t acl = acl_get_file (\".\", ACL_TYPE_ACCESS);\n            return acl ? acl_free (acl) != 0 : errno == ENOENT;\n          ]])],\n       [if test $cross_compiling = yes; then\n          gl_cv_func_working_acl_get_file=\"guessing yes\"\n        elif ./conftest$ac_exeext; then\n          gl_cv_func_working_acl_get_file=yes\n        fi])])\n  AS_IF([test \"$gl_cv_func_working_acl_get_file\" != no], [$1], [$2])\n])\n\n# On GNU/Linux, testing if a file has an acl can be done with the\n# listxattr and getxattr syscalls, which don't require linking\n# against additional libraries.  Assume this works if linux/attr.h\n# and listxattr are present.\nAC_DEFUN([gl_FILE_HAS_ACL],\n[\n  AC_REQUIRE([gl_FUNC_ACL_ARG])\n  AC_CHECK_HEADERS_ONCE([linux/xattr.h])\n  AC_CHECK_FUNCS_ONCE([listxattr])\n  FILE_HAS_ACL_LIB=\n  AS_CASE([$enable_acl,$ac_cv_header_linux_xattr_h,$ac_cv_func_listxattr],\n    [no,*,*], [],\n    [*,yes,yes], [],\n    [*],\n      [dnl Set gl_need_lib_has_acl to a nonempty value, so that any\n       dnl later gl_FUNC_ACL call will set FILE_HAS_ACL_LIB=$LIB_ACL.\n       gl_need_lib_has_acl=1\n       FILE_HAS_ACL_LIB=$LIB_ACL])\n  AC_SUBST([FILE_HAS_ACL_LIB])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}