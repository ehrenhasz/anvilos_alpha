{
  "module_name": "realloc.m4",
  "hash_id": "1d474b69df2bf49a666422e4c8b7a68f7c490783bb5b7731c0a841ec7010794e",
  "original_prompt": "Ingested from coreutils-9.4/m4/realloc.m4",
  "human_readable_source": "# realloc.m4 serial 28\ndnl Copyright (C) 2007, 2009-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\n# This is adapted with modifications from upstream Autoconf here:\n# https://git.savannah.gnu.org/cgit/autoconf.git/tree/lib/autoconf/functions.m4?id=v2.70#n1455\nAC_DEFUN([_AC_FUNC_REALLOC_IF],\n[\n  AC_REQUIRE([AC_CANONICAL_HOST])dnl for cross-compiles\n  AC_CACHE_CHECK([whether realloc (0, 0) returns nonnull],\n    [ac_cv_func_realloc_0_nonnull],\n    [AC_RUN_IFELSE(\n       [AC_LANG_PROGRAM(\n          [[#include <stdlib.h>\n          ]],\n          [[void *p = realloc (0, 0);\n            int result = !p;\n            free (p);\n            return result;]])\n       ],\n       [ac_cv_func_realloc_0_nonnull=yes],\n       [ac_cv_func_realloc_0_nonnull=no],\n       [case \"$host_os\" in\n          # Guess yes on platforms where we know the result.\n          *-gnu* | freebsd* | netbsd* | openbsd* | bitrig* \\\n          | gnu* | *-musl* | midipix* | midnightbsd* \\\n          | hpux* | solaris* | cygwin* | mingw* | windows* | msys* )\n            ac_cv_func_realloc_0_nonnull=\"guessing yes\" ;;\n          # If we don't know, obey --enable-cross-guesses.\n          *) ac_cv_func_realloc_0_nonnull=\"$gl_cross_guess_normal\" ;;\n        esac\n       ])\n    ])\n  AS_CASE([$ac_cv_func_realloc_0_nonnull], [*yes], [$1], [$2])\n])# AC_FUNC_REALLOC\n\n# gl_FUNC_REALLOC_GNU\n# -------------------\n# Replace realloc if it is not compatible with GNU libc.\nAC_DEFUN([gl_FUNC_REALLOC_GNU],\n[\n  AC_REQUIRE([gl_STDLIB_H_DEFAULTS])\n  AC_REQUIRE([gl_FUNC_REALLOC_POSIX])\n  if test $REPLACE_REALLOC_FOR_REALLOC_GNU = 0; then\n    _AC_FUNC_REALLOC_IF([], [REPLACE_REALLOC_FOR_REALLOC_GNU=1])\n  fi\n])# gl_FUNC_REALLOC_GNU\n\n# gl_FUNC_REALLOC_POSIX\n# ---------------------\n# Test whether 'realloc' is POSIX compliant (sets errno to ENOMEM when it\n# fails, and doesn't mess up with ptrdiff_t overflow),\n# and replace realloc if it is not.\nAC_DEFUN([gl_FUNC_REALLOC_POSIX],\n[\n  AC_REQUIRE([gl_STDLIB_H_DEFAULTS])\n  AC_REQUIRE([gl_FUNC_MALLOC_POSIX])\n  if test $REPLACE_MALLOC_FOR_MALLOC_POSIX = 1; then\n    REPLACE_REALLOC_FOR_REALLOC_POSIX=1\n  fi\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}