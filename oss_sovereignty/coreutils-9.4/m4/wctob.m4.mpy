{
  "module_name": "wctob.m4",
  "hash_id": "dd9f8e7f87625e5e4bf27c3fd1487fc860fa89fe1f01e2a71a22626e7e644042",
  "original_prompt": "Ingested from coreutils-9.4/m4/wctob.m4",
  "human_readable_source": "# wctob.m4 serial 14\ndnl Copyright (C) 2008-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN([gl_FUNC_WCTOB],\n[\n  AC_REQUIRE([gl_WCHAR_H_DEFAULTS])\n\n  AC_CHECK_FUNCS_ONCE([wctob])\n  if test $ac_cv_func_wctob = no; then\n    HAVE_WCTOB=0\n    HAVE_DECL_WCTOB=0\n  else\n    HAVE_WCTOB=1\n\n    dnl Solaris 9 has the wctob() function but it does not work.\n    dnl Cygwin 1.7.2 has the wctob() function but it clobbers caller-owned\n    dnl registers, see <https://cygwin.com/ml/cygwin/2010-05/msg00015.html>.\n    AC_REQUIRE([AC_PROG_CC])\n    AC_REQUIRE([gt_LOCALE_FR])\n    AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n    AC_CACHE_CHECK([whether wctob works],\n      [gl_cv_func_wctob_works],\n      [\n        dnl Initial guess, used when cross-compiling or when no suitable locale\n        dnl is present.\nchangequote(,)dnl\n        case \"$host_os\" in\n            # Guess no on Solaris <= 9 and Cygwin.\n          solaris2.[1-9] | solaris2.[1-9].* | cygwin*)\n            gl_cv_func_wctob_works=\"guessing no\" ;;\n            # Guess no on native Windows.\n          mingw* | windows*)\n            gl_cv_func_wctob_works=\"guessing no\" ;;\n            # Guess yes otherwise.\n          *) gl_cv_func_wctob_works=\"guessing yes\" ;;\n        esac\nchangequote([,])dnl\n        case \"$host_os\" in\n          cygwin*)\n            AC_RUN_IFELSE(\n              [AC_LANG_SOURCE([[\n#include <locale.h>\n#include <wchar.h>\n\nregister long global __asm__ (\"%ebx\");\n\nint main ()\n{\n  setlocale (LC_ALL, \"en_US.UTF-8\");\n\n  global = 0x12345678;\n  if (wctob (0x00FC) != -1)\n    return 1;\n  if (global != 0x12345678)\n    return 2;\n  return 0;\n}]])],\n              [:],\n              [gl_cv_func_wctob_works=no],\n              [:])\n            ;;\n        esac\n        if test \"$gl_cv_func_wctob_works\" != no && test $LOCALE_FR != none; then\n          AC_RUN_IFELSE(\n            [AC_LANG_SOURCE([[\n#include <locale.h>\n#include <stdlib.h>\n#include <wchar.h>\nint main ()\n{\n  if (setlocale (LC_ALL, \"$LOCALE_FR\") != NULL)\n    {\n      wchar_t wc;\n\n      if (mbtowc (&wc, \"\\374\", 1) == 1)\n        if (wctob (wc) != (unsigned char) '\\374')\n          return 1;\n    }\n  return 0;\n}]])],\n            [gl_cv_func_wctob_works=yes],\n            [gl_cv_func_wctob_works=no],\n            [:])\n        fi\n      ])\n    case \"$gl_cv_func_wctob_works\" in\n      *yes) ;;\n      *) REPLACE_WCTOB=1 ;;\n    esac\n    if test $REPLACE_WCTOB = 0; then\n\n      dnl IRIX 6.5 has the wctob() function but does not declare it.\n      AC_CHECK_DECLS([wctob], [], [], [[\n        #include <wchar.h>\n      ]])\n      if test $ac_cv_have_decl_wctob != yes; then\n        HAVE_DECL_WCTOB=0\n      fi\n    fi\n  fi\n])\n\n# Prerequisites of lib/wctob.c.\nAC_DEFUN([gl_PREREQ_WCTOB], [\n  :\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}