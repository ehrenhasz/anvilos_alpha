{
  "module_name": "readutmp.m4",
  "hash_id": "1735c3c03fb946485fc3b7cd6b1f9137b34c2cfcea33f4409335666e498f0f20",
  "original_prompt": "Ingested from coreutils-9.4/m4/readutmp.m4",
  "human_readable_source": "# readutmp.m4 serial 30\ndnl Copyright (C) 2002-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN([gl_READUTMP],\n[\n  AC_REQUIRE([gl_SYSTEMD_CHOICE])\n\n  dnl Set READUTMP_LIB to '-lsystemd' or '', depending on whether use of\n  dnl systemd APIs is possible and desired (only the systemd login API, here).\n  dnl AC_LIB_LINKFLAGS_BODY would be overkill here, since few people install\n  dnl libsystemd in non-system directories.\n  READUTMP_LIB=\n  if test \"$SYSTEMD_CHOICE\" = yes; then\n    AC_CHECK_HEADER([systemd/sd-login.h])\n    if test $ac_cv_header_systemd_sd_login_h = yes; then\n      AC_CACHE_CHECK([for libsystemd version >= 254],\n        [gl_cv_lib_readutmp_systemd],\n        [gl_save_LIBS=\"$LIBS\"\n         LIBS=\"$LIBS -lsystemd\"\n         AC_LINK_IFELSE(\n           [AC_LANG_PROGRAM([[\n              #include <stdint.h>\n              #include <systemd/sd-login.h>\n              ]], [[\n              uint64_t st;\n              sd_session_get_start_time (\"1\", &st);\n              ]])\n           ],\n           [gl_cv_lib_readutmp_systemd=yes],\n           [gl_cv_lib_readutmp_systemd=no])\n         LIBS=\"$gl_save_LIBS\"\n        ])\n      if test $gl_cv_lib_readutmp_systemd = yes; then\n        AC_DEFINE([READUTMP_USE_SYSTEMD], [1],\n          [Define if the readutmp module should use the systemd login API.])\n        READUTMP_LIB='-lsystemd'\n      fi\n    fi\n  fi\n  AC_SUBST([READUTMP_LIB])\n\n  gl_PREREQ_READUTMP_H\n])\n\n# Prerequisites of readutmp.h and boot-time-aux.h.\nAC_DEFUN_ONCE([gl_PREREQ_READUTMP_H],\n[\n  dnl Persuade utmpx.h to declare utmpxname\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  AC_CHECK_HEADERS_ONCE([utmp.h utmpx.h])\n  if test $ac_cv_header_utmp_h = yes || test $ac_cv_header_utmpx_h = yes; then\n    dnl Prerequisites of lib/readutmp.h and lib/readutmp.c.\n    AC_CHECK_FUNCS_ONCE([utmpname utmpxname])\n    AC_CHECK_DECLS([endutent],,,[[\n/* <sys/types.h> is a prerequisite of <utmp.h> on FreeBSD 8.0, OpenBSD 4.6.  */\n#include <sys/types.h>\n#ifdef HAVE_UTMP_H\n# include <utmp.h>\n#endif\n]])\n    utmp_includes=\"\\\nAC_INCLUDES_DEFAULT\n#ifdef HAVE_UTMPX_H\n# include <utmpx.h>\n#endif\n#ifdef HAVE_UTMP_H\n# if defined _THREAD_SAFE && defined UTMP_DATA_INIT\n   /* When including both utmp.h and utmpx.h on AIX 4.3, with _THREAD_SAFE\n      defined, work around the duplicate struct utmp_data declaration.  */\n#  define utmp_data gl_aix_4_3_workaround_utmp_data\n# endif\n# include <utmp.h>\n#endif\n\"\n    AC_CHECK_MEMBERS([struct utmpx.ut_user],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_user],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_name],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_name],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_type],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_type],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_pid],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_pid],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_tv],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_host],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_host],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_id],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_id],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_session],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_session],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_exit],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_exit],,,[$utmp_includes])\n\n    AC_CHECK_MEMBERS([struct utmpx.ut_exit.ut_exit],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_exit.e_exit],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_exit.e_exit],,,[$utmp_includes])\n\n    AC_CHECK_MEMBERS([struct utmpx.ut_exit.ut_termination],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmpx.ut_exit.e_termination],,,[$utmp_includes])\n    AC_CHECK_MEMBERS([struct utmp.ut_exit.e_termination],,,[$utmp_includes])\n  fi\n\n  AC_CHECK_DECLS([sysinfo],,,[[\n    #include <sys/sysinfo.h>\n    ]])\n\n  AC_CHECK_HEADERS_ONCE([sys/param.h])\n  dnl <sys/sysctl.h> requires <sys/param.h> on OpenBSD 4.0.\n  AC_CHECK_HEADERS([sys/sysctl.h],,,\n    [AC_INCLUDES_DEFAULT\n     #if HAVE_SYS_PARAM_H\n     # include <sys/param.h>\n     #endif\n    ])\n  AC_CHECK_FUNCS([sysctl])\n\n  AC_CHECK_HEADERS_ONCE([OS.h])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}