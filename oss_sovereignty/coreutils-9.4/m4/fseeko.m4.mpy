{
  "module_name": "fseeko.m4",
  "hash_id": "eac445222cdbf6336930141f875da814952bab6126f53d8370a98b83e9520854",
  "original_prompt": "Ingested from coreutils-9.4/m4/fseeko.m4",
  "human_readable_source": "# fseeko.m4 serial 20\ndnl Copyright (C) 2007-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN([gl_FUNC_FSEEKO],\n[\n  AC_REQUIRE([gl_STDIO_H_DEFAULTS])\n  AC_REQUIRE([gl_STDIN_LARGE_OFFSET])\n  AC_REQUIRE([gl_SYS_TYPES_H])\n  AC_REQUIRE([AC_PROG_CC])\n\n  dnl Persuade glibc <stdio.h> to declare fseeko().\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  AC_CACHE_CHECK([for fseeko], [gl_cv_func_fseeko],\n    [\n      AC_LINK_IFELSE([AC_LANG_PROGRAM([[#include <stdio.h>\n]], [[fseeko (stdin, 0, 0);]])],\n        [gl_cv_func_fseeko=yes], [gl_cv_func_fseeko=no])\n    ])\n\n  AC_CHECK_DECLS_ONCE([fseeko])\n  if test $ac_cv_have_decl_fseeko = no; then\n    HAVE_DECL_FSEEKO=0\n  fi\n\n  if test $gl_cv_func_fseeko = no; then\n    HAVE_FSEEKO=0\n  else\n    if test $WINDOWS_64_BIT_OFF_T = 1; then\n      REPLACE_FSEEKO=1\n    fi\n    if test $gl_cv_var_stdin_large_offset = no; then\n      REPLACE_FSEEKO=1\n    fi\n    m4_ifdef([gl_FUNC_FFLUSH_STDIN], [\n      gl_FUNC_FFLUSH_STDIN\n      case \"$gl_cv_func_fflush_stdin\" in\n        *yes) ;;\n        *) REPLACE_FSEEKO=1 ;;\n      esac\n    ])\n  fi\n])\n\ndnl Code shared by fseeko and ftello.  Determine if large files are supported,\ndnl but stdin does not start as a large file by default.\nAC_DEFUN([gl_STDIN_LARGE_OFFSET],\n  [\n    AC_CACHE_CHECK([whether stdin defaults to large file offsets],\n      [gl_cv_var_stdin_large_offset],\n      [AC_LINK_IFELSE([AC_LANG_PROGRAM([[#include <stdio.h>]],\n[[#if defined __SL64 && defined __SCLE /* cygwin */\n  /* Cygwin 1.5.24 and earlier fail to put stdin in 64-bit mode, making\n     fseeko/ftello needlessly fail.  This bug was fixed in 1.5.25, and\n     it is easier to do a version check than building a runtime test.  */\n# include <cygwin/version.h>\n# if CYGWIN_VERSION_DLL_COMBINED < CYGWIN_VERSION_DLL_MAKE_COMBINED (1005, 25)\n  choke me\n# endif\n#endif]])],\n        [gl_cv_var_stdin_large_offset=yes],\n        [gl_cv_var_stdin_large_offset=no])])\n])\n\n# Prerequisites of lib/fseeko.c.\nAC_DEFUN([gl_PREREQ_FSEEKO],\n[\n  dnl Native Windows has the function _fseeki64. mingw hides it in some\n  dnl circumstances, but mingw64 makes it usable again.\n  AC_CHECK_FUNCS([_fseeki64])\n  if test $ac_cv_func__fseeki64 = yes; then\n    AC_CHECK_DECLS([_fseeki64])\n  fi\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}