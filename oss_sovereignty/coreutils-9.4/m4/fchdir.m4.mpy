{
  "module_name": "fchdir.m4",
  "hash_id": "eabbb24bb77e2ebbbe8adf79306c05ba17d943dd049db54d641144de4cac13a7",
  "original_prompt": "Ingested from coreutils-9.4/m4/fchdir.m4",
  "human_readable_source": "# fchdir.m4 serial 29\ndnl Copyright (C) 2006-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN([gl_FUNC_FCHDIR],\n[\n  AC_REQUIRE([gl_UNISTD_H_DEFAULTS])\n  AC_REQUIRE([gl_DIRENT_H_DEFAULTS])\n  AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n\n  AC_CHECK_DECLS_ONCE([fchdir])\n  if test $ac_cv_have_decl_fchdir = no; then\n    HAVE_DECL_FCHDIR=0\n  fi\n\n  AC_REQUIRE([gl_TEST_FCHDIR])\n  if test $HAVE_FCHDIR = 0; then\n    AC_DEFINE([REPLACE_FCHDIR], [1],\n      [Define to 1 if gnulib's fchdir() replacement is used.])\n    dnl We must also replace anything that can manipulate a directory fd,\n    dnl to keep our bookkeeping up-to-date.  We don't have to replace\n    dnl fstatat, since no platform has fstatat but lacks fchdir.\n    AC_CACHE_CHECK([whether open can visit directories],\n      [gl_cv_func_open_directory_works],\n      [AC_RUN_IFELSE(\n         [AC_LANG_PROGRAM(\n            [[#include <fcntl.h>\n            ]GL_MDA_DEFINES],\n            [[return open(\".\", O_RDONLY) < 0;]])],\n         [gl_cv_func_open_directory_works=yes],\n         [gl_cv_func_open_directory_works=no],\n         [case \"$host_os\" in\n                               # Guess yes on Linux systems.\n            linux-* | linux)   gl_cv_func_open_directory_works=\"guessing yes\" ;;\n                               # Guess yes on systems that emulate the Linux system calls.\n            midipix*)          gl_cv_func_open_directory_works=\"guessing yes\" ;;\n                               # Guess yes on glibc systems.\n            *-gnu* | gnu*)     gl_cv_func_open_directory_works=\"guessing yes\" ;;\n                               # Guess no on native Windows.\n            mingw* | windows*) gl_cv_func_open_directory_works=\"guessing no\" ;;\n                               # If we don't know, obey --enable-cross-guesses.\n            *)                 gl_cv_func_open_directory_works=\"$gl_cross_guess_normal\" ;;\n          esac\n         ])])\n    case \"$gl_cv_func_open_directory_works\" in\n      *yes) ;;\n      *)\n        AC_DEFINE([REPLACE_OPEN_DIRECTORY], [1], [Define to 1 if open() should\nwork around the inability to open a directory.])\n        ;;\n    esac\n  fi\n])\n\n# Determine whether to use the overrides in lib/fchdir.c.\nAC_DEFUN([gl_TEST_FCHDIR],\n[\n  AC_REQUIRE([gl_UNISTD_H_DEFAULTS])\n  AC_CHECK_FUNCS_ONCE([fchdir])\n  if test $ac_cv_func_fchdir = no; then\n    HAVE_FCHDIR=0\n  fi\n])\n\n# Prerequisites of lib/fchdir.c.\nAC_DEFUN([gl_PREREQ_FCHDIR], [:])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}