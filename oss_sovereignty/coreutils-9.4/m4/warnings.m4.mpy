{
  "module_name": "warnings.m4",
  "hash_id": "1e923af898a1cfc733ecf2ed1186a0e670ffb1b569535d3b80ab325f2840156b",
  "original_prompt": "Ingested from coreutils-9.4/m4/warnings.m4",
  "human_readable_source": "# warnings.m4 serial 19\ndnl Copyright (C) 2008-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\ndnl From Simon Josefsson\n\nAC_PREREQ([2.64])\n\n# gl_COMPILER_OPTION_IF(OPTION, [IF-SUPPORTED], [IF-NOT-SUPPORTED],\n#                       [PROGRAM = AC_LANG_PROGRAM()])\n# -----------------------------------------------------------------\n# Check if the compiler supports OPTION when compiling PROGRAM.\n#\n# The effects of this macro depend on the current language (_AC_LANG).\nAC_DEFUN([gl_COMPILER_OPTION_IF],\n[\nAS_VAR_PUSHDEF([gl_Warn], [gl_cv_warn_[]_AC_LANG_ABBREV[]_$1])dnl\nAS_VAR_PUSHDEF([gl_Flags], [_AC_LANG_PREFIX[]FLAGS])dnl\nAS_LITERAL_IF([$1],\n  [m4_pushdef([gl_Positive], m4_bpatsubst([$1], [^-Wno-], [-W]))],\n  [gl_positive=\"$1\"\ncase $gl_positive in\n  -Wno-*) gl_positive=-W`expr \"X$gl_positive\" : 'X-Wno-\\(.*\\)'` ;;\nesac\nm4_pushdef([gl_Positive], [$gl_positive])])dnl\nAC_CACHE_CHECK([whether _AC_LANG compiler handles $1], [gl_Warn], [\n  gl_save_compiler_FLAGS=\"$gl_Flags\"\n  AS_VAR_APPEND(m4_defn([gl_Flags]),\n    [\" $gl_unknown_warnings_are_errors ]m4_defn([gl_Positive])[\"])\n  AC_LINK_IFELSE([m4_default([$4], [AC_LANG_PROGRAM([[]])])],\n                 [AS_VAR_SET([gl_Warn], [yes])],\n                 [AS_VAR_SET([gl_Warn], [no])])\n  gl_Flags=\"$gl_save_compiler_FLAGS\"\n])\nAS_VAR_IF(gl_Warn, [yes], [$2], [$3])\nm4_popdef([gl_Positive])dnl\nAS_VAR_POPDEF([gl_Flags])dnl\nAS_VAR_POPDEF([gl_Warn])dnl\n])\n\n# gl_UNKNOWN_WARNINGS_ARE_ERRORS\n# ------------------------------\n# Clang doesn't complain about unknown warning options unless one also\n# specifies -Wunknown-warning-option -Werror.  Detect this.\n#\n# The effects of this macro depend on the current language (_AC_LANG).\nAC_DEFUN([gl_UNKNOWN_WARNINGS_ARE_ERRORS],\n[_AC_LANG_DISPATCH([$0], _AC_LANG, $@)])\n\n# Specialization for _AC_LANG = C. This macro can be AC_REQUIREd.\nAC_DEFUN([gl_UNKNOWN_WARNINGS_ARE_ERRORS(C)],\n[\n  AC_LANG_PUSH([C])\n  gl_UNKNOWN_WARNINGS_ARE_ERRORS_IMPL\n  AC_LANG_POP([C])\n])\n\n# Specialization for _AC_LANG = C++. This macro can be AC_REQUIREd.\nAC_DEFUN([gl_UNKNOWN_WARNINGS_ARE_ERRORS(C++)],\n[\n  AC_LANG_PUSH([C++])\n  gl_UNKNOWN_WARNINGS_ARE_ERRORS_IMPL\n  AC_LANG_POP([C++])\n])\n\n# Specialization for _AC_LANG = Objective C. This macro can be AC_REQUIREd.\nAC_DEFUN([gl_UNKNOWN_WARNINGS_ARE_ERRORS(Objective C)],\n[\n  AC_LANG_PUSH([Objective C])\n  gl_UNKNOWN_WARNINGS_ARE_ERRORS_IMPL\n  AC_LANG_POP([Objective C])\n])\n\nAC_DEFUN([gl_UNKNOWN_WARNINGS_ARE_ERRORS_IMPL],\n[gl_COMPILER_OPTION_IF([-Werror -Wunknown-warning-option],\n   [gl_unknown_warnings_are_errors='-Wunknown-warning-option -Werror'],\n   [gl_unknown_warnings_are_errors=])])\n\n# gl_WARN_ADD(OPTION, [VARIABLE = WARN_CFLAGS/WARN_CXXFLAGS],\n#             [PROGRAM = AC_LANG_PROGRAM()])\n# -----------------------------------------------------------\n# Adds OPTION to VARIABLE (which defaults to WARN_CFLAGS or WARN_CXXFLAGS)\n# if the compiler supports it when compiling PROGRAM.\n#\n# If VARIABLE is a variable name, AC_SUBST it.\n#\n# The effects of this macro depend on the current language (_AC_LANG).\n#\n# Example: gl_WARN_ADD([-Wparentheses]).\nAC_DEFUN([gl_WARN_ADD],\n[AC_REQUIRE([gl_UNKNOWN_WARNINGS_ARE_ERRORS(]_AC_LANG[)])\ngl_COMPILER_OPTION_IF([$1],\n  [AS_VAR_APPEND(m4_if([$2], [], [[WARN_]_AC_LANG_PREFIX[FLAGS]], [[$2]]), [\" $1\"])],\n  [],\n  [$3])\nm4_ifval([$2],\n         [AS_LITERAL_IF([$2], [AC_SUBST([$2])])],\n         [AC_SUBST([WARN_]_AC_LANG_PREFIX[FLAGS])])dnl\n])\n\n\n# gl_CC_INHIBIT_WARNINGS\n# sets and substitutes a variable GL_CFLAG_INHIBIT_WARNINGS, to a $(CC) option\n# that reverts all preceding -W* options, if available.\n# This is expected to be '-w' at least on gcc, clang, AIX xlc, xlclang, Sun cc,\n# \"compile cl\" (MSVC), \"compile clang-cl\" (MSVC-compatible clang). Or it can be\n# empty.\nAC_DEFUN([gl_CC_INHIBIT_WARNINGS],\n[\n  AC_REQUIRE([AC_PROG_CC])\n  AC_CACHE_CHECK([for C compiler option to inhibit all warnings],\n    [gl_cv_cc_winhibit],\n    [rm -f conftest*\n     echo 'int dummy;' > conftest.c\n     AC_TRY_COMMAND([${CC-cc} $CFLAGS $CPPFLAGS -c conftest.c 2>conftest1.err]) >/dev/null\n     AC_TRY_COMMAND([${CC-cc} $CFLAGS $CPPFLAGS -w -c conftest.c 2>conftest2.err]) >/dev/null\n     if test $? = 0 && test `wc -l < conftest1.err` = `wc -l < conftest2.err`; then\n       gl_cv_cc_winhibit='-w'\n     else\n       gl_cv_cc_winhibit=none\n     fi\n     rm -f conftest*\n    ])\n  case \"$gl_cv_cc_winhibit\" in\n    none) GL_CFLAG_INHIBIT_WARNINGS='' ;;\n    *)\n      GL_CFLAG_INHIBIT_WARNINGS=\"$gl_cv_cc_winhibit\"\n      dnl If all warnings are inhibited, there's no point in having the GCC\n      dnl analyzer enabled. This saves RAM requirements and CPU consumption.\n      gl_WARN_ADD([-fno-analyzer], [GL_CFLAG_INHIBIT_WARNINGS])\n      ;;\n  esac\n  AC_SUBST([GL_CFLAG_INHIBIT_WARNINGS])\n])\n\n# gl_CXX_INHIBIT_WARNINGS\n# sets and substitutes a variable GL_CXXFLAG_INHIBIT_WARNINGS, to a $(CC) option\n# that reverts all preceding -W* options, if available.\nAC_DEFUN([gl_CXX_INHIBIT_WARNINGS],\n[\n  dnl Requires AC_PROG_CXX or gl_PROG_ANSI_CXX.\n  if test -n \"$CXX\" && test \"$CXX\" != no; then\n    AC_CACHE_CHECK([for C++ compiler option to inhibit all warnings],\n      [gl_cv_cxx_winhibit],\n      [rm -f conftest*\n       echo 'int dummy;' > conftest.cc\n       AC_TRY_COMMAND([${CXX-c++} $CXXFLAGS $CPPFLAGS -c conftest.cc 2>conftest1.err]) >/dev/null\n       AC_TRY_COMMAND([${CXX-c++} $CXXFLAGS $CPPFLAGS -w -c conftest.cc 2>conftest2.err]) >/dev/null\n       if test $? = 0 && test `wc -l < conftest1.err` = `wc -l < conftest2.err`; then\n         gl_cv_cxx_winhibit='-w'\n       else\n         gl_cv_cxx_winhibit=none\n       fi\n       rm -f conftest*\n      ])\n    case \"$gl_cv_cxx_winhibit\" in\n      none) GL_CXXFLAG_INHIBIT_WARNINGS='' ;;\n      *)\n        GL_CXXFLAG_INHIBIT_WARNINGS=\"$gl_cv_cxx_winhibit\"\n        dnl If all warnings are inhibited, there's no point in having the GCC\n        dnl analyzer enabled. This saves RAM requirements and CPU consumption.\n        gl_WARN_ADD([-fno-analyzer], [GL_CXXFLAG_INHIBIT_WARNINGS])\n        ;;\n    esac\n  else\n    GL_CXXFLAG_INHIBIT_WARNINGS=''\n  fi\n  AC_SUBST([GL_CXXFLAG_INHIBIT_WARNINGS])\n])\n\n\n# Local Variables:\n# mode: autoconf\n# End:\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}