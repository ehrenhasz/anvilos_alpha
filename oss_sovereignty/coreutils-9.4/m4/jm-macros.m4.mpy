{
  "module_name": "jm-macros.m4",
  "hash_id": "37193af8495e17a0f2f57ea86c9a6459fc1794a099afcc1cd16182b5906a613a",
  "original_prompt": "Ingested from coreutils-9.4/m4/jm-macros.m4",
  "human_readable_source": "#serial 114   -*- autoconf -*-\n\ndnl Misc type-related macros for coreutils.\n\n# Copyright (C) 1998-2023 Free Software Foundation, Inc.\n\n# This program is free software: you can redistribute it and/or modify\n# it under the terms of the GNU General Public License as published by\n# the Free Software Foundation, either version 3 of the License, or\n# (at your option) any later version.\n\n# This program is distributed in the hope that it will be useful,\n# but WITHOUT ANY WARRANTY; without even the implied warranty of\n# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n# GNU General Public License for more details.\n\n# You should have received a copy of the GNU General Public License\n# along with this program.  If not, see <https://www.gnu.org/licenses/>.\n\n# Written by Jim Meyering.\n\nAC_DEFUN([coreutils_MACROS],\n[\n  AM_MISSING_PROG(HELP2MAN, help2man)\n  AC_SUBST([MAN])\n\n  gl_CHECK_ALL_TYPES\n\n  AC_REQUIRE([gl_CHECK_DECLS])\n\n  AC_REQUIRE([gl_PREREQ])\n\n  AC_REQUIRE([AC_FUNC_FSEEKO])\n\n  # By default, argmatch should fail calling usage (EXIT_FAILURE).\n  AC_DEFINE([ARGMATCH_DIE], [usage (EXIT_FAILURE)],\n            [Define to the function xargmatch calls on failures.])\n  AC_DEFINE([ARGMATCH_DIE_DECL], [void usage (int _e)],\n            [Define to the declaration of the xargmatch failure function.])\n\n  # Ensure VLAs are not used.\n  # Note -Wvla is implicitly added by gl_MANYWARN_ALL_GCC\n  AC_DEFINE([GNULIB_NO_VLA], [1], [Define to 1 to disable use of VLAs])\n\n  # used by shred\n  AC_CHECK_FUNCS_ONCE([directio])\n\n  coreutils_saved_libs=$LIBS\n    LIBS=\"$LIBS $LIB_SELINUX\"\n    # Used by selinux.c.\n    AC_CHECK_FUNCS([mode_to_security_class], [], [])\n  LIBS=$coreutils_saved_libs\n\n  # Used by sort.c.\n  AC_CHECK_FUNCS_ONCE([nl_langinfo])\n  # Used by timeout.c\n  AC_CHECK_FUNCS_ONCE([setitimer setrlimit prctl])\n\n  # Used by tail.c.\n  AC_CHECK_FUNCS([inotify_init],\n    [AC_DEFINE([HAVE_INOTIFY], [1],\n     [Define to 1 if you have usable inotify support.])])\n\n  AC_CHECK_FUNCS_ONCE([\n    endgrent\n    endpwent\n    fallocate\n    fchown\n    fchmod\n    ftruncate\n    iswspace\n    mkfifo\n    mbrlen\n    setgroups\n    sethostname\n    siginterrupt\n    sync\n    syncfs\n    sysctl\n    sysinfo\n    tcgetpgrp\n  ])\n\n  # These checks are for Interix, to avoid its getgr* functions, in favor\n  # of these replacements.  The replacement functions are much more efficient\n  # because they do not query the domain controller for user information\n  # when it is not needed.\n  AC_CHECK_FUNCS_ONCE([\n    getgrgid_nomembers\n    getgrnam_nomembers\n    getgrent_nomembers\n  ])\n\n  dnl This can't use AC_REQUIRE; I'm not quite sure why.\n  cu_PREREQ_STAT_PROG\n\n  # for dd.c and shred.c\n  #\n  # Use fdatasync only if declared.  On MacOS X 10.7, fdatasync exists but\n  # is not declared, and is ineffective.\n  LIB_FDATASYNC=\n  AC_SUBST([LIB_FDATASYNC])\n  AC_CHECK_DECLS_ONCE([fdatasync])\n  if test $ac_cv_have_decl_fdatasync = yes; then\n    coreutils_saved_libs=$LIBS\n    AC_SEARCH_LIBS([fdatasync], [rt posix4],\n                   [test \"$ac_cv_search_fdatasync\" = \"none required\" ||\n                    LIB_FDATASYNC=$ac_cv_search_fdatasync])\n    AC_CHECK_FUNCS([fdatasync])\n    LIBS=$coreutils_saved_libs\n  fi\n\n  # Check whether libcap is usable -- for ls --color support\n  LIB_CAP=\n  AC_ARG_ENABLE([libcap],\n    AS_HELP_STRING([--disable-libcap], [disable libcap support]))\n  if test \"X$enable_libcap\" != \"Xno\"; then\n    AC_CHECK_LIB([cap], [cap_get_file],\n      [AC_CHECK_HEADER([sys/capability.h],\n        [LIB_CAP=-lcap\n         AC_DEFINE([HAVE_CAP], [1], [libcap usability])]\n      )])\n    if test \"X$LIB_CAP\" = \"X\"; then\n      if test \"X$enable_libcap\" = \"Xyes\"; then\n        AC_MSG_ERROR([libcap library was not found or not usable])\n      else\n        AC_MSG_WARN([libcap library was not found or not usable.])\n        AC_MSG_WARN([AC_PACKAGE_NAME will be built without capability support.])\n      fi\n    fi\n  else\n    AC_MSG_WARN([libcap support disabled by user])\n  fi\n  AC_SUBST([LIB_CAP])\n\n  # See if linking 'seq' requires -lm.\n  # It does on nearly every system.  The single exception (so far) is\n  # BeOS which has all the math functions in the normal runtime library\n  # and doesn't have a separate math library.\n\n  AC_SUBST([SEQ_LIBM])\n  jm_break=:\n  for jm_seqlibs in '' '-lm'; do\n    jm_seq_save_LIBS=$LIBS\n    AC_LINK_IFELSE(\n      [AC_LANG_PROGRAM(\n         [[#include <math.h>\n         ]],\n         [[static double x, y;\n           x = floor (x);\n           x = rint (x);\n           x = modf (x, &y);]])],\n      [SEQ_LIBM=$jm_seqlibs\n       jm_break=break])\n    LIBS=$jm_seq_save_LIBS\n    $jm_break\n  done\n\n  # See is fpsetprec() required to use extended double precision\n  # This is needed on 32 bit FreeBSD to give accurate conversion of:\n  # `numfmt 9223372036854775808`\n  AC_LINK_IFELSE(\n    [AC_LANG_PROGRAM(\n       [[#include <ieeefp.h>\n       ]],\n       [[#ifdef __i386__\n          fpsetprec (FP_PE);\n         #else\n         # error not required on 64 bit\n         #endif\n       ]])],\n    [ac_have_fpsetprec=yes],\n    [ac_have_fpsetprec=no])\n  if test \"$ac_have_fpsetprec\" = \"yes\" ; then\n    AC_DEFINE([HAVE_FPSETPREC], 1, [whether fpsetprec is present and required])\n  fi\n\n  AC_REQUIRE([AM_LANGINFO_CODESET])\n\n  # Accept configure options: --with-tty-group[=GROUP], --without-tty-group\n  # You can determine the group of a TTY via 'stat --format %G /dev/tty'\n  # Omitting this option is equivalent to using --without-tty-group.\n  AC_ARG_WITH([tty-group],\n    AS_HELP_STRING([--with-tty-group[[[=NAME]]]],\n      [group used by system for TTYs, \"tty\" when not specified]\n      [ (default: do not rely on any group used for TTYs)]),\n    [tty_group_name=$withval],\n    [tty_group_name=no])\n\n  if test \"x$tty_group_name\" != xno; then\n    if test \"x$tty_group_name\" = xyes; then\n      tty_group_name=tty\n    fi\n    AC_MSG_NOTICE([TTY group used by system set to \"$tty_group_name\"])\n    AC_DEFINE_UNQUOTED([TTY_GROUP_NAME], [\"$tty_group_name\"],\n      [group used by system for TTYs])\n  fi\n])\n\nAC_DEFUN([gl_CHECK_ALL_HEADERS],\n[\n  AC_CHECK_HEADERS_ONCE([\n    hurd.h\n    linux/falloc.h\n    linux/fs.h\n    paths.h\n    priv.h\n    stropts.h\n    sys/mtio.h\n    sys/param.h\n    sys/systeminfo.h\n    syslog.h\n  ])\n  AC_CHECK_HEADERS([sys/sysctl.h], [], [],\n    [AC_INCLUDES_DEFAULT\n     [#if HAVE_SYS_PARAM_H\n       #include <sys/param.h>\n      #endif]])\n])\n\n# This macro must be invoked before any tests that run the compiler.\nAC_DEFUN([gl_CHECK_ALL_TYPES],\n[\n  dnl Checks for typedefs, structures, and compiler characteristics.\n  AC_REQUIRE([gl_BIGENDIAN])\n  AC_REQUIRE([AC_C_VOLATILE])\n  AC_REQUIRE([AC_C_INLINE])\n  AC_REQUIRE([AC_TYPE_UNSIGNED_LONG_LONG_INT])\n\n  AC_REQUIRE([gl_CHECK_ALL_HEADERS])\n  AC_CHECK_MEMBERS(\n    [struct stat.st_author],,,\n    [$ac_includes_default\n#include <sys/stat.h>\n  ])\n  AC_REQUIRE([AC_STRUCT_ST_BLOCKS])\n\n  AC_REQUIRE([AC_TYPE_GETGROUPS])\n  AC_REQUIRE([AC_TYPE_MBSTATE_T])\n  AC_REQUIRE([AC_TYPE_MODE_T])\n  AC_REQUIRE([AC_TYPE_OFF_T])\n  AC_REQUIRE([AC_TYPE_PID_T])\n  AC_REQUIRE([AC_TYPE_SIZE_T])\n  AC_REQUIRE([AC_TYPE_UID_T])\n  AC_CHECK_TYPE([ino_t], [unsigned long int])\n\n  dnl This relies on the fact that Autoconf's implementation of\n  dnl AC_CHECK_TYPE checks includes unistd.h.\n  AC_CHECK_TYPE([major_t], [unsigned int])\n  AC_CHECK_TYPE([minor_t], [unsigned int])\n\n  AC_REQUIRE([AC_HEADER_MAJOR])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}