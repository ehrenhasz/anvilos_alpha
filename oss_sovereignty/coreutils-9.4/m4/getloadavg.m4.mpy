{
  "module_name": "getloadavg.m4",
  "hash_id": "4899ad7a6177b5a6906524e1e6b979b81c9f678149133d21b8d4b6c678aaf06a",
  "original_prompt": "Ingested from coreutils-9.4/m4/getloadavg.m4",
  "human_readable_source": "# Check for getloadavg.\n\n# Copyright (C) 1992-1996, 1999-2000, 2002-2003, 2006, 2008-2023 Free Software\n# Foundation, Inc.\n\n# This file is free software; the Free Software Foundation\n# gives unlimited permission to copy and/or distribute it,\n# with or without modifications, as long as this notice is preserved.\n\n#serial 12\n\n# Autoconf defines AC_FUNC_GETLOADAVG, but that is obsolescent.\n# New applications should use gl_GETLOADAVG instead.\n\n# gl_GETLOADAVG\n# -------------\nAC_DEFUN([gl_GETLOADAVG],\n[AC_REQUIRE([gl_STDLIB_H_DEFAULTS])\n\n# Persuade glibc <stdlib.h> to declare getloadavg().\nAC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\ngl_save_LIBS=$LIBS\n\n# getloadavg is present in libc on glibc >= 2.2, Mac OS X, FreeBSD >= 2.0,\n# NetBSD >= 0.9, OpenBSD >= 2.0, Solaris >= 7.\nHAVE_GETLOADAVG=1\ngl_CHECK_FUNCS_ANDROID([getloadavg], [[#include <stdlib.h>]])\nif test $ac_cv_func_getloadavg != yes; then\n   case \"$gl_cv_onwards_func_getloadavg\" in\n     future*) REPLACE_GETLOADAVG=1 ;;\n   esac\n\n   gl_func_getloadavg_done=no\n\n   # Some systems with -lutil have (and need) -lkvm as well, some do not.\n   # On Solaris, -lkvm requires nlist from -lelf, so check that first\n   # to get the right answer into the cache.\n   # For kstat on solaris, we need to test for libelf and libkvm to force the\n   # definition of SVR4 below.\n   if test $gl_func_getloadavg_done = no; then\n     AC_CHECK_LIB([elf], [elf_begin], [LIBS=\"-lelf $LIBS\"])\n     AC_CHECK_LIB([kvm], [kvm_open], [LIBS=\"-lkvm $LIBS\"])\n     # Check for the 4.4BSD definition of getloadavg.\n     AC_CHECK_LIB([util], [getloadavg],\n       [LIBS=\"-lutil $LIBS\" gl_func_getloadavg_done=yes])\n   fi\n\n   if test $gl_func_getloadavg_done = no; then\n     # There is a commonly available library for RS/6000 AIX.\n     # Since it is not a standard part of AIX, it might be installed locally.\n     gl_getloadavg_LIBS=$LIBS\n     if test $cross_compiling != yes; then\n       LIBS=\"-L/usr/local/lib $LIBS\"\n     fi\n     AC_CHECK_LIB([getloadavg], [getloadavg],\n                  [LIBS=\"-lgetloadavg $LIBS\" gl_func_getloadavg_done=yes],\n                  [LIBS=$gl_getloadavg_LIBS])\n   fi\n\n   # Set up the replacement function if necessary.\n   if test $gl_func_getloadavg_done = no; then\n     HAVE_GETLOADAVG=0\n\n     # Solaris has libkstat which does not require root.\n     AC_CHECK_LIB([kstat], [kstat_open])\n     test $ac_cv_lib_kstat_kstat_open = yes && gl_func_getloadavg_done=yes\n\n     # AIX has libperfstat which does not require root\n     if test $gl_func_getloadavg_done = no; then\n       AC_CHECK_LIB([perfstat], [perfstat_cpu_total])\n       test $ac_cv_lib_perfstat_perfstat_cpu_total = yes && gl_func_getloadavg_done=yes\n     fi\n\n     if test $gl_func_getloadavg_done = no; then\n       AC_CHECK_HEADER([sys/dg_sys_info.h],\n         [gl_func_getloadavg_done=yes\n          AC_DEFINE([DGUX], [1], [Define to 1 for DGUX with <sys/dg_sys_info.h>.])\n          AC_CHECK_LIB([dgc], [dg_sys_info])])\n     fi\n   fi\nfi\n\nif test \"x$gl_save_LIBS\" = x; then\n  GETLOADAVG_LIBS=$LIBS\nelse\n  GETLOADAVG_LIBS=`echo \"$LIBS\" | sed \"s!$gl_save_LIBS!!\"`\nfi\nLIBS=$gl_save_LIBS\n\nAC_SUBST([GETLOADAVG_LIBS])dnl\n\n# Test whether the system declares getloadavg. Solaris has the function\n# but declares it in <sys/loadavg.h>, not <stdlib.h>.\nAC_CHECK_HEADERS([sys/loadavg.h])\nif test $ac_cv_header_sys_loadavg_h = yes; then\n  HAVE_SYS_LOADAVG_H=1\nelse\n  HAVE_SYS_LOADAVG_H=0\nfi\nAC_CHECK_DECL([getloadavg], [], [HAVE_DECL_GETLOADAVG=0],\n  [[#if HAVE_SYS_LOADAVG_H\n    /* OpenIndiana has a bug: <sys/time.h> must be included before\n       <sys/loadavg.h>.  */\n    # include <sys/time.h>\n    # include <sys/loadavg.h>\n    #endif\n    #include <stdlib.h>]])\n])# gl_GETLOADAVG\n\n\n# gl_PREREQ_GETLOADAVG\n# --------------------\n# Set up the AC_LIBOBJ replacement of 'getloadavg'.\nAC_DEFUN([gl_PREREQ_GETLOADAVG],\n[\n# Figure out what our getloadavg.c needs.\n\nAC_CHECK_HEADERS_ONCE([sys/param.h unistd.h])\n\n# On HPUX9, an unprivileged user can get load averages this way.\nif test $gl_func_getloadavg_done = no; then\n  AC_CHECK_FUNCS([pstat_getdynamic], [gl_func_getloadavg_done=yes])\nfi\n\n# We cannot check for <dwarf.h>, because Solaris 2 does not use dwarf (it\n# uses stabs), but it is still SVR4.  We cannot check for <elf.h> because\n# Irix 4.0.5F has the header but not the library.\nif test $gl_func_getloadavg_done = no && test \"$ac_cv_lib_elf_elf_begin\" = yes \\\n    && test \"$ac_cv_lib_kvm_kvm_open\" = yes; then\n  gl_func_getloadavg_done=yes\n  AC_DEFINE([SVR4], [1], [Define to 1 on System V Release 4.])\nfi\n\nif test $gl_func_getloadavg_done = no; then\n  AC_CHECK_HEADER([inq_stats/cpustats.h],\n  [gl_func_getloadavg_done=yes\n   AC_DEFINE([UMAX], [1], [Define to 1 for Encore UMAX.])\n   AC_DEFINE([UMAX4_3], [1],\n             [Define to 1 for Encore UMAX 4.3 that has <inq_status/cpustats.h>\n              instead of <sys/cpustats.h>.])])\nfi\n\nif test $gl_func_getloadavg_done = no; then\n  AC_CHECK_HEADER([sys/cpustats.h],\n  [gl_func_getloadavg_done=yes; AC_DEFINE([UMAX])])\nfi\n\nif test $gl_func_getloadavg_done = no; then\n  AC_CHECK_HEADERS([mach/mach.h])\nfi\n\nAC_CHECK_HEADERS([nlist.h],\n[AC_CHECK_MEMBERS([struct nlist.n_un.n_name],\n                  [], [],\n                  [#include <nlist.h>])\n AC_LINK_IFELSE([AC_LANG_PROGRAM([[#include <nlist.h>]],\n                   [[struct nlist x;\n                    #ifdef HAVE_STRUCT_NLIST_N_UN_N_NAME\n                    x.n_un.n_name = \"\";\n                    #else\n                    x.n_name = \"\";\n                    #endif]])],\n                [AC_DEFINE([N_NAME_POINTER], [1],\n                           [Define to 1 if the nlist n_name member is a pointer])])\n])dnl\n])# gl_PREREQ_GETLOADAVG\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}