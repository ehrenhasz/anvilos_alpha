{
  "module_name": "00gnulib.m4",
  "hash_id": "60b9bfe2cdb463018a3eca30eb15344adb87a6a6302824ddbbf6e9b05c77acd9",
  "original_prompt": "Ingested from coreutils-9.4/m4/00gnulib.m4",
  "human_readable_source": "# 00gnulib.m4 serial 8\ndnl Copyright (C) 2009-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\ndnl This file must be named something that sorts before all other\ndnl gnulib-provided .m4 files.  It is needed until the clang fix has\ndnl been included in Autoconf.\n\n# The following definitions arrange to use a compiler option\n# -Werror=implicit-function-declaration in AC_CHECK_DECL, when the\n# compiler is clang.  Without it, clang implicitly declares \"known\"\n# library functions in C mode, but not in C++ mode, which would cause\n# Gnulib to omit a declaration and thus later produce an error in C++\n# mode.  As of clang 9.0, these \"known\" functions are identified through\n# LIBBUILTIN invocations in the LLVM source file\n# llvm/tools/clang/include/clang/Basic/Builtins.def.\n# It's not possible to AC_REQUIRE the extra tests from AC_CHECK_DECL,\n# because AC_CHECK_DECL, like other Autoconf built-ins, is not supposed\n# to AC_REQUIRE anything: some configure.ac files have their first\n# AC_CHECK_DECL executed conditionally.  Therefore append the extra tests\n# to AC_PROG_CC.\nAC_DEFUN([gl_COMPILER_CLANG],\n[\ndnl AC_REQUIRE([AC_PROG_CC])\n  AC_CACHE_CHECK([whether the compiler is clang],\n    [gl_cv_compiler_clang],\n    [dnl Use _AC_COMPILE_IFELSE instead of AC_EGREP_CPP, to avoid error\n     dnl \"circular dependency of AC_LANG_COMPILER(C)\" if AC_PROG_CC has\n     dnl not yet been invoked.\n     _AC_COMPILE_IFELSE(\n        [AC_LANG_PROGRAM([[\n           #ifdef __clang__\n           barfbarf\n           #endif\n           ]],[[]])\n        ],\n        [gl_cv_compiler_clang=no],\n        [gl_cv_compiler_clang=yes])\n    ])\n])\nAC_DEFUN([gl_COMPILER_PREPARE_CHECK_DECL],\n[\ndnl AC_REQUIRE([AC_PROG_CC])\ndnl AC_REQUIRE([gl_COMPILER_CLANG])\n  AC_CACHE_CHECK([for compiler option needed when checking for declarations],\n    [gl_cv_compiler_check_decl_option],\n    [if test $gl_cv_compiler_clang = yes; then\n       dnl Test whether the compiler supports the option\n       dnl '-Werror=implicit-function-declaration'.\n       save_ac_compile=\"$ac_compile\"\n       ac_compile=\"$ac_compile -Werror=implicit-function-declaration\"\n       dnl Use _AC_COMPILE_IFELSE instead of AC_COMPILE_IFELSE, to avoid a\n       dnl warning \"AC_COMPILE_IFELSE was called before AC_USE_SYSTEM_EXTENSIONS\".\n       _AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]],[[]])],\n         [gl_cv_compiler_check_decl_option='-Werror=implicit-function-declaration'],\n         [gl_cv_compiler_check_decl_option=none])\n       ac_compile=\"$save_ac_compile\"\n     else\n       gl_cv_compiler_check_decl_option=none\n     fi\n    ])\n  if test \"x$gl_cv_compiler_check_decl_option\" != xnone; then\n    ac_compile_for_check_decl=\"$ac_compile $gl_cv_compiler_check_decl_option\"\n  else\n    ac_compile_for_check_decl=\"$ac_compile\"\n  fi\n])\ndnl Redefine _AC_CHECK_DECL_BODY so that it references ac_compile_for_check_decl\ndnl instead of ac_compile.  If, for whatever reason, the override of AC_PROG_CC\ndnl in zzgnulib.m4 is inactive, use the original ac_compile.\nm4_define([_AC_CHECK_DECL_BODY],\n[  ac_save_ac_compile=\"$ac_compile\"\n  if test -n \"$ac_compile_for_check_decl\"; then\n    ac_compile=\"$ac_compile_for_check_decl\"\n  fi]\nm4_defn([_AC_CHECK_DECL_BODY])[  ac_compile=\"$ac_save_ac_compile\"\n])\n\n# gl_00GNULIB\n# -----------\n# Witness macro that this file has been included.  Needed to force\n# Automake to include this file prior to all other gnulib .m4 files.\nAC_DEFUN([gl_00GNULIB])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}