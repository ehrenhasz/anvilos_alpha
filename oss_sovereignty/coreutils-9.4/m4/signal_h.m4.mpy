{
  "module_name": "signal_h.m4",
  "hash_id": "cb01e6a8442939de28ca3c3b02c6fd66104193ba2290658061dc8ae2e86b8069",
  "original_prompt": "Ingested from coreutils-9.4/m4/signal_h.m4",
  "human_readable_source": "# signal_h.m4 serial 22\ndnl Copyright (C) 2007-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN_ONCE([gl_SIGNAL_H],\n[\n  AC_REQUIRE([gl_SIGNAL_H_DEFAULTS])\n  AC_REQUIRE([gl_CHECK_TYPE_SIGSET_T])\n  gl_NEXT_HEADERS([signal.h])\n\n# AIX declares sig_atomic_t to already include volatile, and C89 compilers\n# then choke on 'volatile sig_atomic_t'.  C99 requires that it compile.\n  AC_CHECK_TYPE([volatile sig_atomic_t], [],\n    [HAVE_TYPE_VOLATILE_SIG_ATOMIC_T=0], [[\n#include <signal.h>\n    ]])\n\n  dnl Ensure the type pid_t gets defined.\n  AC_REQUIRE([AC_TYPE_PID_T])\n\n  AC_REQUIRE([AC_TYPE_UID_T])\n\n  dnl Persuade glibc <signal.h> to define sighandler_t.\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n  AC_CHECK_TYPE([sighandler_t], [], [HAVE_SIGHANDLER_T=0], [[\n#include <signal.h>\n    ]])\n\n  dnl Check for declarations of anything we want to poison if the\n  dnl corresponding gnulib module is not in use.\n  gl_WARN_ON_USE_PREPARE([[#include <signal.h>\n    ]], [pthread_sigmask sigaction\n    sigaddset sigdelset sigemptyset sigfillset sigismember\n    sigpending sigprocmask])\n\n  AC_REQUIRE([AC_C_RESTRICT])\n])\n\nAC_DEFUN([gl_CHECK_TYPE_SIGSET_T],\n[\n  AC_CHECK_TYPES([sigset_t],\n    [gl_cv_type_sigset_t=yes], [gl_cv_type_sigset_t=no],\n    [[\n      #include <signal.h>\n      /* Mingw defines sigset_t not in <signal.h>, but in <sys/types.h>.  */\n      #include <sys/types.h>\n    ]])\n  if test $gl_cv_type_sigset_t != yes; then\n    HAVE_SIGSET_T=0\n  fi\n])\n\n# gl_SIGNAL_MODULE_INDICATOR([modulename])\n# sets the shell variable that indicates the presence of the given module\n# to a C preprocessor expression that will evaluate to 1.\n# This macro invocation must not occur in macros that are AC_REQUIREd.\nAC_DEFUN([gl_SIGNAL_MODULE_INDICATOR],\n[\n  dnl Ensure to expand the default settings once only.\n  gl_SIGNAL_H_REQUIRE_DEFAULTS\n  gl_MODULE_INDICATOR_SET_VARIABLE([$1])\n  dnl Define it also as a C macro, for the benefit of the unit tests.\n  gl_MODULE_INDICATOR_FOR_TESTS([$1])\n])\n\n# Initializes the default values for AC_SUBSTed shell variables.\n# This macro must not be AC_REQUIREd.  It must only be invoked, and only\n# outside of macros or in macros that are not AC_REQUIREd.\nAC_DEFUN([gl_SIGNAL_H_REQUIRE_DEFAULTS],\n[\n  m4_defun(GL_MODULE_INDICATOR_PREFIX[_SIGNAL_H_MODULE_INDICATOR_DEFAULTS], [\n    gl_MODULE_INDICATOR_INIT_VARIABLE([GNULIB_PTHREAD_SIGMASK])\n    gl_MODULE_INDICATOR_INIT_VARIABLE([GNULIB_RAISE])\n    gl_MODULE_INDICATOR_INIT_VARIABLE([GNULIB_SIGNAL_H_SIGPIPE])\n    gl_MODULE_INDICATOR_INIT_VARIABLE([GNULIB_SIGPROCMASK])\n    gl_MODULE_INDICATOR_INIT_VARIABLE([GNULIB_SIGACTION])\n  ])\n  m4_require(GL_MODULE_INDICATOR_PREFIX[_SIGNAL_H_MODULE_INDICATOR_DEFAULTS])\n  AC_REQUIRE([gl_SIGNAL_H_DEFAULTS])\n])\n\nAC_DEFUN([gl_SIGNAL_H_DEFAULTS],\n[\n  dnl Assume proper GNU behavior unless another module says otherwise.\n  HAVE_POSIX_SIGNALBLOCKING=1; AC_SUBST([HAVE_POSIX_SIGNALBLOCKING])\n  HAVE_PTHREAD_SIGMASK=1;      AC_SUBST([HAVE_PTHREAD_SIGMASK])\n  HAVE_RAISE=1;                AC_SUBST([HAVE_RAISE])\n  HAVE_SIGSET_T=1;             AC_SUBST([HAVE_SIGSET_T])\n  HAVE_SIGINFO_T=1;            AC_SUBST([HAVE_SIGINFO_T])\n  HAVE_SIGACTION=1;            AC_SUBST([HAVE_SIGACTION])\n  HAVE_STRUCT_SIGACTION_SA_SIGACTION=1;\n                               AC_SUBST([HAVE_STRUCT_SIGACTION_SA_SIGACTION])\n  HAVE_TYPE_VOLATILE_SIG_ATOMIC_T=1;\n                               AC_SUBST([HAVE_TYPE_VOLATILE_SIG_ATOMIC_T])\n  HAVE_SIGHANDLER_T=1;         AC_SUBST([HAVE_SIGHANDLER_T])\n  REPLACE_PTHREAD_SIGMASK=0;   AC_SUBST([REPLACE_PTHREAD_SIGMASK])\n  REPLACE_RAISE=0;             AC_SUBST([REPLACE_RAISE])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}