{
  "module_name": "bison.m4",
  "hash_id": "5ee12c42d6294d924724fbb138a4990e5a624bf90c4a691f522232c99eff5ede",
  "original_prompt": "Ingested from coreutils-9.4/m4/bison.m4",
  "human_readable_source": "# serial 10\n\n# Copyright (C) 2002-2006, 2008-2023 Free Software Foundation, Inc.\n# This file is free software; the Free Software Foundation\n# gives unlimited permission to copy and/or distribute it,\n# with or without modifications, as long as this notice is preserved.\n\n# There are two types of parser skeletons:\n#\n# * Those that can be used with any Yacc implementation, including bison.\n#   For these, in the configure.ac, up to Autoconf 2.69, you could use\n#     AC_PROG_YACC\n#   In newer Autoconf versions, however, this macro is broken. See\n#     https://lists.gnu.org/archive/html/autoconf-patches/2013-03/msg00000.html\n#     https://lists.gnu.org/archive/html/bug-autoconf/2018-12/msg00001.html\n#   In the Makefile.am you could use\n#     $(SHELL) $(YLWRAP) $(srcdir)/foo.y \\\n#                        y.tab.c foo.c \\\n#                        y.tab.h foo.h \\\n#                        y.output foo.output \\\n#                        -- $(YACC) $(YFLAGS) $(AM_YFLAGS)\n#   or similar.\n#\n# * Those that make use of Bison extensions. For example,\n#     - %define api.pure   requires bison 2.7 or newer,\n#     - %precedence        requires bison 3.0 or newer.\n#   For these, in the configure.ac you will need an invocation of\n#     gl_PROG_BISON([VARIABLE], [MIN_BISON_VERSION])\n#   Example:\n#     gl_PROG_BISON([PARSE_DATETIME_BISON], [2.4])\n#   With this preparation, in the Makefile.am there are two ways to formulate\n#   the invocation. Both are direct, without use of 'ylwrap'.\n#   (a) You can invoke\n#         $(VARIABLE) -d $(SOME_BISON_OPTIONS) --output foo.c $(srcdir)/foo.y\n#       or similar.\n#   (b) If you want the invocation to honor an YFLAGS=... parameter passed to\n#       'configure' or an YFLAGS environment variable present at 'configure'\n#       time, add an invocation of gl_BISON to the configure.ac, and write\n#         $(VARIABLE) -d $(YFLAGS) $(AM_YFLAGS) $(srcdir)/foo.y\n#       or similar.\n\n# This macro defines the autoconf variable VARIABLE to 'bison' if the specified\n# minimum version of bison is found in $PATH, or to ':' otherwise.\nAC_DEFUN([gl_PROG_BISON],\n[\n  AC_CHECK_PROGS([$1], [bison])\n  if test -z \"$[$1]\"; then\n    ac_verc_fail=yes\n  else\n    cat >conftest.y <<_ACEOF\n%require \"$2\"\n%%\nexp:\n_ACEOF\n    AC_MSG_CHECKING([for bison $2 or newer])\n    ac_prog_version=`$$1 --version 2>&1 | sed -n 's/^.*GNU Bison.* \\([[0-9]]*\\.[[0-9.]]*\\).*$/\\1/p'`\n    : ${ac_prog_version:='v. ?.??'}\n    if $$1 conftest.y -o conftest.c 2>/dev/null; then\n      ac_prog_version=\"$ac_prog_version, ok\"\n      ac_verc_fail=no\n    else\n      ac_prog_version=\"$ac_prog_version, bad\"\n      ac_verc_fail=yes\n    fi\n    rm -f conftest.y conftest.c\n    AC_MSG_RESULT([$ac_prog_version])\n  fi\n  if test $ac_verc_fail = yes; then\n    [$1]=:\n  fi\n  AC_SUBST([$1])\n])\n\n# This macro sets the autoconf variables YACC (for old-style yacc Makefile\n# rules) and YFLAGS (to allow options to be passed as 'configure' time).\nAC_DEFUN([gl_BISON],\n[\n  : ${YACC='bison -o y.tab.c'}\ndnl\ndnl Declaring YACC & YFLAGS precious will not be necessary after GNULIB\ndnl requires an Autoconf greater than 2.59c, but it will probably still be\ndnl useful to override the description of YACC in the --help output, re\ndnl parse-datetime.y assuming 'bison -o y.tab.c'.\n  AC_ARG_VAR([YACC],\n[The \"Yet Another C Compiler\" implementation to use.  Defaults to\n'bison -o y.tab.c'.  Values other than 'bison -o y.tab.c' will most likely\nbreak on most systems.])dnl\n  AC_ARG_VAR([YFLAGS],\n[YFLAGS contains the list arguments that will be passed by default to Bison.\nThis script will default YFLAGS to the empty string to avoid a default value of\n'-d' given by some make applications.])dnl\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}