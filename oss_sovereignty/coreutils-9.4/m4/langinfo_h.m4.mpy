{
  "module_name": "langinfo_h.m4",
  "hash_id": "09b937436ae3269d2520c4620309fca833cb9aa7c0a0160b6c150dad0b8a2a62",
  "original_prompt": "Ingested from coreutils-9.4/m4/langinfo_h.m4",
  "human_readable_source": "# langinfo_h.m4 serial 12\ndnl Copyright (C) 2009-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN_ONCE([gl_LANGINFO_H],\n[\n  AC_REQUIRE([gl_LANGINFO_H_DEFAULTS])\n\n  dnl Persuade glibc-2.0.6 <langinfo.h> to define CODESET.\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  dnl <langinfo.h> is always overridden, because of GNULIB_POSIXCHECK.\n  gl_CHECK_NEXT_HEADERS([langinfo.h])\n\n  dnl Determine whether <langinfo.h> exists. It is missing on mingw and BeOS.\n  HAVE_LANGINFO_CODESET=0\n  HAVE_LANGINFO_T_FMT_AMPM=0\n  HAVE_LANGINFO_ALTMON=0\n  HAVE_LANGINFO_ERA=0\n  HAVE_LANGINFO_YESEXPR=0\n  AC_CHECK_HEADERS_ONCE([langinfo.h])\n  if test $ac_cv_header_langinfo_h = yes; then\n    HAVE_LANGINFO_H=1\n    dnl Determine what <langinfo.h> defines.\n    dnl CODESET is missing on OpenBSD 3.8.\n    dnl ERA etc. are missing on OpenBSD 6.7.\n    dnl T_FMT_AMPM and YESEXPR, NOEXPR are missing on IRIX 5.3.\n    dnl ALTMON_* are missing on glibc 2.26 and many other systems.\n    AC_CACHE_CHECK([whether langinfo.h defines CODESET],\n      [gl_cv_header_langinfo_codeset],\n      [AC_COMPILE_IFELSE(\n         [AC_LANG_PROGRAM([[#include <langinfo.h>\nint a = CODESET;\n]])],\n         [gl_cv_header_langinfo_codeset=yes],\n         [gl_cv_header_langinfo_codeset=no])\n      ])\n    if test $gl_cv_header_langinfo_codeset = yes; then\n      HAVE_LANGINFO_CODESET=1\n    fi\n    AC_CACHE_CHECK([whether langinfo.h defines T_FMT_AMPM],\n      [gl_cv_header_langinfo_t_fmt_ampm],\n      [AC_COMPILE_IFELSE(\n         [AC_LANG_PROGRAM([[#include <langinfo.h>\nint a = T_FMT_AMPM;\n]])],\n         [gl_cv_header_langinfo_t_fmt_ampm=yes],\n         [gl_cv_header_langinfo_t_fmt_ampm=no])\n      ])\n    if test $gl_cv_header_langinfo_t_fmt_ampm = yes; then\n      HAVE_LANGINFO_T_FMT_AMPM=1\n    fi\n    AC_CACHE_CHECK([whether langinfo.h defines ALTMON_1],\n      [gl_cv_header_langinfo_altmon],\n      [AC_COMPILE_IFELSE(\n         [AC_LANG_PROGRAM([[#include <langinfo.h>\nint a = ALTMON_1;\n]])],\n         [gl_cv_header_langinfo_altmon=yes],\n         [gl_cv_header_langinfo_altmon=no])\n      ])\n    if test $gl_cv_header_langinfo_altmon = yes; then\n      HAVE_LANGINFO_ALTMON=1\n    fi\n    AC_CACHE_CHECK([whether langinfo.h defines ERA],\n      [gl_cv_header_langinfo_era],\n      [AC_COMPILE_IFELSE(\n         [AC_LANG_PROGRAM([[#include <langinfo.h>\nint a = ERA;\n]])],\n         [gl_cv_header_langinfo_era=yes],\n         [gl_cv_header_langinfo_era=no])\n      ])\n    if test $gl_cv_header_langinfo_era = yes; then\n      HAVE_LANGINFO_ERA=1\n    fi\n    AC_CACHE_CHECK([whether langinfo.h defines YESEXPR],\n      [gl_cv_header_langinfo_yesexpr],\n      [AC_COMPILE_IFELSE(\n         [AC_LANG_PROGRAM([[#include <langinfo.h>\nint a = YESEXPR;\n]])],\n         [gl_cv_header_langinfo_yesexpr=yes],\n         [gl_cv_header_langinfo_yesexpr=no])\n      ])\n    if test $gl_cv_header_langinfo_yesexpr = yes; then\n      HAVE_LANGINFO_YESEXPR=1\n    fi\n  else\n    HAVE_LANGINFO_H=0\n  fi\n  AC_SUBST([HAVE_LANGINFO_H])\n  AC_SUBST([HAVE_LANGINFO_CODESET])\n  AC_SUBST([HAVE_LANGINFO_T_FMT_AMPM])\n  AC_SUBST([HAVE_LANGINFO_ALTMON])\n  AC_SUBST([HAVE_LANGINFO_ERA])\n  AC_SUBST([HAVE_LANGINFO_YESEXPR])\n\n  dnl Check for declarations of anything we want to poison if the\n  dnl corresponding gnulib module is not in use.\n  gl_WARN_ON_USE_PREPARE([[#include <langinfo.h>\n    ]], [nl_langinfo])\n])\n\n# gl_LANGINFO_MODULE_INDICATOR([modulename])\n# sets the shell variable that indicates the presence of the given module\n# to a C preprocessor expression that will evaluate to 1.\n# This macro invocation must not occur in macros that are AC_REQUIREd.\nAC_DEFUN([gl_LANGINFO_MODULE_INDICATOR],\n[\n  dnl Ensure to expand the default settings once only.\n  gl_LANGINFO_H_REQUIRE_DEFAULTS\n  gl_MODULE_INDICATOR_SET_VARIABLE([$1])\n  dnl Define it also as a C macro, for the benefit of the unit tests.\n  gl_MODULE_INDICATOR_FOR_TESTS([$1])\n])\n\n# Initializes the default values for AC_SUBSTed shell variables.\n# This macro must not be AC_REQUIREd.  It must only be invoked, and only\n# outside of macros or in macros that are not AC_REQUIREd.\nAC_DEFUN([gl_LANGINFO_H_REQUIRE_DEFAULTS],\n[\n  m4_defun(GL_MODULE_INDICATOR_PREFIX[_LANGINFO_H_MODULE_INDICATOR_DEFAULTS], [\n    gl_MODULE_INDICATOR_INIT_VARIABLE([GNULIB_NL_LANGINFO])\n  ])\n  m4_require(GL_MODULE_INDICATOR_PREFIX[_LANGINFO_H_MODULE_INDICATOR_DEFAULTS])\n  AC_REQUIRE([gl_LANGINFO_H_DEFAULTS])\n])\n\nAC_DEFUN([gl_LANGINFO_H_DEFAULTS],\n[\n  dnl Assume proper GNU behavior unless another module says otherwise.\n  HAVE_NL_LANGINFO=1;    AC_SUBST([HAVE_NL_LANGINFO])\n  REPLACE_NL_LANGINFO=0; AC_SUBST([REPLACE_NL_LANGINFO])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}