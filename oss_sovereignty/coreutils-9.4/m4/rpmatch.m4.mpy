{
  "module_name": "rpmatch.m4",
  "hash_id": "3ac5b887cfe3fb78787f73b9f66c587af097c8ba2b98b47178fb1987d5c2ee3b",
  "original_prompt": "Ingested from coreutils-9.4/m4/rpmatch.m4",
  "human_readable_source": "# rpmatch.m4 serial 10\ndnl Copyright (C) 2002-2003, 2007-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN([gl_FUNC_RPMATCH],\n[\n  dnl Persuade glibc <stdlib.h> to declare rpmatch().\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  AC_REQUIRE([gl_STDLIB_H_DEFAULTS])\n  AC_CHECK_FUNCS([rpmatch])\n  if test $ac_cv_func_rpmatch = no; then\n    HAVE_RPMATCH=0\n  fi\n])\n\n# Prerequisites of lib/rpmatch.c.\nAC_DEFUN([gl_PREREQ_RPMATCH], [\n  AC_CACHE_CHECK([for nl_langinfo and YESEXPR], [gl_cv_langinfo_yesexpr],\n    [AC_LINK_IFELSE([AC_LANG_PROGRAM([[#include <langinfo.h>]],\n       [[char* cs = nl_langinfo(YESEXPR); return !cs;]])],\n       [gl_cv_langinfo_yesexpr=yes],\n       [gl_cv_langinfo_yesexpr=no])\n    ])\n  if test $gl_cv_langinfo_yesexpr = yes; then\n    AC_DEFINE([HAVE_LANGINFO_YESEXPR], [1],\n      [Define if you have <langinfo.h> and nl_langinfo(YESEXPR).])\n  fi\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}