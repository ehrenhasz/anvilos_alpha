{
  "module_name": "strsignal.m4",
  "hash_id": "6ce07b531fdd083434352c96c6e7e72ca73ab3e6546b8f11d781033f82783a41",
  "original_prompt": "Ingested from coreutils-9.4/m4/strsignal.m4",
  "human_readable_source": "# strsignal.m4 serial 10\ndnl Copyright (C) 2008-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\nAC_DEFUN([gl_FUNC_STRSIGNAL],\n[\n  dnl Persuade glibc <string.h> to declare strsignal().\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  AC_REQUIRE([gl_STRING_H_DEFAULTS])\n  AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n\n  AC_CHECK_DECLS_ONCE([strsignal])\n  if test $ac_cv_have_decl_strsignal = no; then\n    HAVE_DECL_STRSIGNAL=0\n  fi\n\n  AC_CHECK_FUNCS([strsignal])\n  if test $ac_cv_func_strsignal = yes; then\n    HAVE_STRSIGNAL=1\n    dnl Check if strsignal behaves reasonably for out-of-range signal numbers.\n    dnl On Solaris it returns NULL; on AIX 5.1 it returns (char *) -1.\n    AC_CACHE_CHECK([whether strsignal always returns a string],\n      [gl_cv_func_working_strsignal],\n      [AC_RUN_IFELSE(\n         [AC_LANG_PROGRAM(\n            [[#include <string.h>\n#include <unistd.h> /* NetBSD 5.0 declares it in wrong header. */\n            ]],\n            [[int result = 0;\n              char *s = strsignal (-1);\n              if (s == (char *) 0)\n                result |= 1;\n              if (s == (char *) -1)\n                result |= 2;\n              return result;\n            ]])],\n         [gl_cv_func_working_strsignal=yes],\n         [gl_cv_func_working_strsignal=no],\n         [case \"$host_os\" in\n            solaris* | aix*) gl_cv_func_working_strsignal=no;;\n            *)               gl_cv_func_working_strsignal=\"guessing yes\";;\n          esac])])\n    if test \"$gl_cv_func_working_strsignal\" = no; then\n      REPLACE_STRSIGNAL=1\n    fi\n  else\n    HAVE_STRSIGNAL=0\n  fi\n])\n\n# Prerequisites of lib/strsignal.c.\nAC_DEFUN([gl_PREREQ_STRSIGNAL], [\n  AC_CHECK_HEADERS_ONCE([unistd.h])\n  AC_CHECK_DECLS([sys_siglist], [], [], [[#include <signal.h>]])\n  AC_CHECK_DECLS([_sys_siglist], [], [], [[#include <signal.h>]])\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}