{
  "module_name": "mmap-anon.m4",
  "hash_id": "43cec1fc8648da7c08b06a289f22aa24af6d86e369cb1d5281e8ea273dc59d3c",
  "original_prompt": "Ingested from coreutils-9.4/m4/mmap-anon.m4",
  "human_readable_source": "# mmap-anon.m4 serial 12\ndnl Copyright (C) 2005, 2007, 2009-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\n# Detect how mmap can be used to create anonymous (not file-backed) memory\n# mappings.\n# - On Linux, AIX, OSF/1, Solaris, Cygwin, Interix, Haiku, both MAP_ANONYMOUS\n#   and MAP_ANON exist and have the same value.\n# - On HP-UX, only MAP_ANONYMOUS exists.\n# - On Mac OS X, FreeBSD, NetBSD, OpenBSD, Minix, only MAP_ANON exists.\n# - On IRIX, neither exists, and a file descriptor opened to /dev/zero must be\n#   used.\n\nAC_DEFUN_ONCE([gl_FUNC_MMAP_ANON],\n[\n  dnl Persuade glibc <sys/mman.h> to define MAP_ANONYMOUS.\n  AC_REQUIRE([gl_USE_SYSTEM_EXTENSIONS])\n\n  # Check for mmap(). Don't use AC_FUNC_MMAP, because it checks too much: it\n  # fails on HP-UX 11, because MAP_FIXED mappings do not work. But this is\n  # irrelevant for anonymous mappings.\n  AC_CHECK_FUNC([mmap], [gl_have_mmap=yes], [gl_have_mmap=no])\n\n  # Try to allow MAP_ANONYMOUS.\n  gl_have_mmap_anonymous=no\n  if test $gl_have_mmap = yes; then\n    AC_MSG_CHECKING([for MAP_ANONYMOUS])\n    AC_EGREP_CPP([I cannot identify this map], [\n#include <sys/mman.h>\n#ifdef MAP_ANONYMOUS\n    I cannot identify this map\n#endif\n],\n      [gl_have_mmap_anonymous=yes])\n    if test $gl_have_mmap_anonymous != yes; then\n      AC_EGREP_CPP([I cannot identify this map], [\n#include <sys/mman.h>\n#ifdef MAP_ANON\n    I cannot identify this map\n#endif\n],\n        [AC_DEFINE([MAP_ANONYMOUS], [MAP_ANON],\n          [Define to a substitute value for mmap()'s MAP_ANONYMOUS flag.])\n         gl_have_mmap_anonymous=yes])\n    fi\n    AC_MSG_RESULT([$gl_have_mmap_anonymous])\n    if test $gl_have_mmap_anonymous = yes; then\n      AC_DEFINE([HAVE_MAP_ANONYMOUS], [1],\n        [Define to 1 if mmap()'s MAP_ANONYMOUS flag is available after including\n         config.h and <sys/mman.h>.])\n    fi\n  fi\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}