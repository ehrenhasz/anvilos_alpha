{
  "module_name": "intl-thread-locale.m4",
  "hash_id": "c78e2cf480fb0c15caee48580402facd41c32f8edf90bfde8ad09866de420341",
  "original_prompt": "Ingested from coreutils-9.4/m4/intl-thread-locale.m4",
  "human_readable_source": "# intl-thread-locale.m4 serial 10\ndnl Copyright (C) 2015-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\ndnl\ndnl This file can be used in projects which are not available under\ndnl the GNU General Public License or the GNU Lesser General Public\ndnl License but which still want to provide support for the GNU gettext\ndnl functionality.\ndnl Please note that the actual code of the GNU gettext library is covered\ndnl by the GNU Lesser General Public License, and the rest of the GNU\ndnl gettext package is covered by the GNU General Public License.\ndnl They are *not* in the public domain.\n\ndnl Check how to retrieve the name of a per-thread locale (POSIX locale_t).\ndnl Sets gt_nameless_locales.\nAC_DEFUN([gt_INTL_THREAD_LOCALE_NAME],\n[\n  AC_REQUIRE([AC_CANONICAL_HOST])\n\n  dnl Persuade Solaris <locale.h> to define 'locale_t'.\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  dnl Test whether uselocale() exists and works at all.\n  gt_FUNC_USELOCALE\n\n  dnl On OpenBSD >= 6.2, the locale_t type and the uselocale(), newlocale(),\n  dnl duplocale(), freelocale() functions exist but are effectively useless,\n  dnl because the locale_t value depends only on the LC_CTYPE category of the\n  dnl locale and furthermore contains only one bit of information (it\n  dnl distinguishes the \"C\" locale from the *.UTF-8 locales). See\n  dnl <https://cvsweb.openbsd.org/src/lib/libc/locale/newlocale.c?rev=1.1&content-type=text/x-cvsweb-markup>.\n  dnl In the setlocale() implementation they have thought about the programs\n  dnl that use the API (\"Even though only LC_CTYPE has any effect in the\n  dnl OpenBSD base system, store complete information about the global locale,\n  dnl such that third-party software can access it\"), but for uselocale()\n  dnl they did not think about the programs.\n  dnl In this situation, even the HAVE_NAMELESS_LOCALES support does not work.\n  dnl So, define HAVE_FAKE_LOCALES and disable all locale_t support.\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_CHECK_HEADERS_ONCE([xlocale.h])\n      AC_CACHE_CHECK([for fake locale system (OpenBSD)],\n        [gt_cv_locale_fake],\n        [AC_RUN_IFELSE(\n           [AC_LANG_SOURCE([[\n#include <locale.h>\n#if HAVE_XLOCALE_H\n# include <xlocale.h>\n#endif\nint main ()\n{\n  locale_t loc1, loc2;\n  if (setlocale (LC_ALL, \"de_DE.UTF-8\") == NULL) return 1;\n  if (setlocale (LC_ALL, \"fr_FR.UTF-8\") == NULL) return 1;\n  loc1 = newlocale (LC_ALL_MASK, \"de_DE.UTF-8\", (locale_t)0);\n  loc2 = newlocale (LC_ALL_MASK, \"fr_FR.UTF-8\", (locale_t)0);\n  return !(loc1 == loc2);\n}]])],\n           [gt_cv_locale_fake=yes],\n           [gt_cv_locale_fake=no],\n           [dnl Guess the locale system is fake only on OpenBSD.\n            case \"$host_os\" in\n              openbsd*) gt_cv_locale_fake=\"guessing yes\" ;;\n              *)        gt_cv_locale_fake=\"guessing no\" ;;\n            esac\n           ])\n        ])\n      ;;\n    *) gt_cv_locale_fake=no ;;\n  esac\n  case \"$gt_cv_locale_fake\" in\n    *yes)\n      gt_fake_locales=yes\n      AC_DEFINE([HAVE_FAKE_LOCALES], [1],\n        [Define if the locale_t type contains insufficient information, as on OpenBSD.])\n      ;;\n    *)\n      gt_fake_locales=no\n      ;;\n  esac\n\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_CACHE_CHECK([for Solaris 11.4 locale system],\n        [gt_cv_locale_solaris114],\n        [case \"$host_os\" in\n           solaris*)\n             dnl Test whether <locale.h> defines locale_t as a typedef of\n             dnl 'struct _LC_locale_t **' (whereas Illumos defines it as a\n             dnl typedef of 'struct _locale *').\n             dnl Another possible test would be to include <sys/localedef.h>\n             dnl and test whether it defines the _LC_core_data_locale_t type.\n             dnl This type was added in Solaris 11.4.\n             AC_COMPILE_IFELSE(\n               [AC_LANG_PROGRAM([[\n                  #include <locale.h>\n                  struct _LC_locale_t *x;\n                  locale_t y;\n                ]],\n                [[*y = x;]])],\n               [gt_cv_locale_solaris114=yes],\n               [gt_cv_locale_solaris114=no])\n             ;;\n           *) gt_cv_locale_solaris114=no ;;\n         esac\n        ])\n      ;;\n    *) gt_cv_locale_solaris114=no ;;\n  esac\n  if test $gt_cv_locale_solaris114 = yes; then\n    AC_DEFINE([HAVE_SOLARIS114_LOCALES], [1],\n      [Define if the locale_t type is as on Solaris 11.4.])\n  fi\n\n  dnl Solaris 12 will maybe provide getlocalename_l.  If it does, it will\n  dnl improve the implementation of gl_locale_name_thread(), by removing\n  dnl the use of undocumented structures.\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      AC_CHECK_FUNCS([getlocalename_l])\n      ;;\n  esac\n\n  dnl This code is for platforms where the locale_t type does not provide access\n  dnl to the name of each locale category.  This code has the drawback that it\n  dnl requires the gnulib overrides of 'newlocale', 'duplocale', 'freelocale',\n  dnl which is a problem for GNU libunistring.  Therefore try hard to avoid\n  dnl enabling this code!\n  gt_nameless_locales=no\n  case \"$host_os\" in\n    dnl It's needed on AIX 7.2.\n    aix*)\n      gt_nameless_locales=yes\n      AC_DEFINE([HAVE_NAMELESS_LOCALES], [1],\n        [Define if the locale_t type does not contain the name of each locale category.])\n      ;;\n  esac\n\n  dnl We cannot support uselocale() on platforms where the locale_t type is\n  dnl fake.  So, set\n  dnl   gt_good_uselocale = gt_working_uselocale && !gt_fake_locales.\n  if test $gt_working_uselocale = yes && test $gt_fake_locales = no; then\n    gt_good_uselocale=yes\n    AC_DEFINE([HAVE_GOOD_USELOCALE], [1],\n      [Define if the uselocale exists, may be safely called, and returns sufficient information.])\n  else\n    gt_good_uselocale=no\n  fi\n\n  dnl Set gt_localename_enhances_locale_funcs to indicate whether localename.c\n  dnl overrides newlocale(), duplocale(), freelocale() to keep track of locale\n  dnl names.\n  if test $gt_good_uselocale = yes && test $gt_nameless_locales = yes; then\n    gt_localename_enhances_locale_funcs=yes\n    LOCALENAME_ENHANCE_LOCALE_FUNCS=1\n    AC_DEFINE([LOCALENAME_ENHANCE_LOCALE_FUNCS], [1],\n      [Define if localename.c overrides newlocale(), duplocale(), freelocale().])\n  else\n    gt_localename_enhances_locale_funcs=no\n  fi\n])\n\ndnl Tests whether uselocale() exists and is usable.\ndnl Sets gt_working_uselocale and defines HAVE_WORKING_USELOCALE.\nAC_DEFUN([gt_FUNC_USELOCALE],\n[\n  AC_REQUIRE([AC_CANONICAL_HOST]) dnl for cross-compiles\n\n  dnl Persuade glibc and Solaris <locale.h> to define 'locale_t'.\n  AC_REQUIRE([AC_USE_SYSTEM_EXTENSIONS])\n\n  gl_CHECK_FUNCS_ANDROID([uselocale], [[#include <locale.h>]])\n\n  dnl On AIX 7.2, the uselocale() function is not documented and leads to\n  dnl crashes in subsequent setlocale() invocations.\n  dnl In 2019, some versions of z/OS lack the locale_t type and have a broken\n  dnl uselocale function.\n  if test $ac_cv_func_uselocale = yes; then\n    AC_CHECK_HEADERS_ONCE([xlocale.h])\n    AC_CACHE_CHECK([whether uselocale works],\n      [gt_cv_func_uselocale_works],\n      [AC_RUN_IFELSE(\n         [AC_LANG_SOURCE([[\n#include <locale.h>\n#if HAVE_XLOCALE_H\n# include <xlocale.h>\n#endif\nlocale_t loc1;\nint main ()\n{\n  uselocale (NULL);\n  setlocale (LC_ALL, \"en_US.UTF-8\");\n  return 0;\n}]])],\n         [gt_cv_func_uselocale_works=yes],\n         [gt_cv_func_uselocale_works=no],\n         [# Guess no on AIX and z/OS, yes otherwise.\n          case \"$host_os\" in\n            aix* | openedition*) gt_cv_func_uselocale_works=\"guessing no\" ;;\n            *)                   gt_cv_func_uselocale_works=\"guessing yes\" ;;\n          esac\n         ])\n      ])\n  else\n    gt_cv_func_uselocale_works=no\n  fi\n  case \"$gt_cv_func_uselocale_works\" in\n    *yes)\n      gt_working_uselocale=yes\n      AC_DEFINE([HAVE_WORKING_USELOCALE], [1],\n        [Define if the uselocale function exists and may safely be called.])\n      ;;\n    *)\n      gt_working_uselocale=no\n      ;;\n  esac\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}