{
  "module_name": "gethostname.m4",
  "hash_id": "1b3cd85e86a1f0e33bac41c1e8808610d9d01f9fe747a4ceac215448af480a97",
  "original_prompt": "Ingested from coreutils-9.4/m4/gethostname.m4",
  "human_readable_source": "# gethostname.m4 serial 15\ndnl Copyright (C) 2002, 2008-2023 Free Software Foundation, Inc.\ndnl This file is free software; the Free Software Foundation\ndnl gives unlimited permission to copy and/or distribute it,\ndnl with or without modifications, as long as this notice is preserved.\n\n# Ensure\n# - the gethostname() function,\n# - the HOST_NAME_MAX macro in <limits.h>.\nAC_DEFUN([gl_FUNC_GETHOSTNAME],\n[\n  AC_REQUIRE([gl_UNISTD_H_DEFAULTS])\n  gl_PREREQ_SYS_H_WINSOCK2\n\n  dnl Where is gethostname() defined?\n  dnl - On native Windows, it is in ws2_32.dll.\n  dnl - Otherwise it is in libc.\n  GETHOSTNAME_LIB=\n  AC_CHECK_FUNCS([gethostname], , [\n    AC_CACHE_CHECK([for gethostname in winsock2.h and -lws2_32],\n      [gl_cv_w32_gethostname],\n      [gl_cv_w32_gethostname=no\n       gl_save_LIBS=\"$LIBS\"\n       LIBS=\"$LIBS -lws2_32\"\n       AC_LINK_IFELSE([AC_LANG_PROGRAM([[\n#ifdef HAVE_WINSOCK2_H\n#include <winsock2.h>\n#endif\n#include <stddef.h>\n]], [[gethostname(NULL, 0);]])], [gl_cv_w32_gethostname=yes])\n       LIBS=\"$gl_save_LIBS\"\n      ])\n    if test \"$gl_cv_w32_gethostname\" = \"yes\"; then\n      GETHOSTNAME_LIB=\"-lws2_32\"\n    fi\n  ])\n  AC_SUBST([GETHOSTNAME_LIB])\n\n  if test \"$ac_cv_func_gethostname\" = no; then\n    HAVE_GETHOSTNAME=0\n  fi\n\n  gl_PREREQ_HOST_NAME_MAX\n])\n\n# Provide HOST_NAME_MAX when <limits.h> lacks it.\nAC_DEFUN([gl_PREREQ_HOST_NAME_MAX], [\n  dnl - On most Unix systems, use MAXHOSTNAMELEN from <sys/param.h> instead.\n  dnl - On Solaris, Cygwin, BeOS, use MAXHOSTNAMELEN from <netdb.h> instead.\n  dnl - On mingw, use 256, because\n  dnl   <https://docs.microsoft.com/en-us/windows/desktop/api/winsock/nf-winsock-gethostname> says:\n  dnl   \"if a buffer of 256 bytes is passed in the name parameter and\n  dnl    the namelen parameter is set to 256, the buffer size will always\n  dnl    be adequate.\"\n  dnl With this, there is no need to use sysconf (_SC_HOST_NAME_MAX), which\n  dnl is not a compile-time constant.\n  dnl We cannot override <limits.h> using the usual technique, because\n  dnl gl_CHECK_NEXT_HEADERS does not work for <limits.h>. Therefore retrieve\n  dnl the value of HOST_NAME_MAX at configure time.\n  AC_CHECK_HEADERS_ONCE([sys/param.h])\n  AC_CHECK_HEADERS_ONCE([sys/socket.h])\n  AC_CHECK_HEADERS_ONCE([netdb.h])\n  AC_CACHE_CHECK([for HOST_NAME_MAX], [gl_cv_decl_HOST_NAME_MAX], [\n    gl_cv_decl_HOST_NAME_MAX=\n    AC_EGREP_CPP([lucky], [\n#include <limits.h>\n#ifdef HOST_NAME_MAX\nlucky\n#endif\n      ], [gl_cv_decl_HOST_NAME_MAX=yes])\n    if test -z \"$gl_cv_decl_HOST_NAME_MAX\"; then\n      dnl It's not defined in <limits.h>. Substitute it.\n      if test \"$gl_cv_w32_gethostname\" = yes; then\n        dnl mingw.\n        gl_cv_decl_HOST_NAME_MAX=256\n      else\n        AC_COMPUTE_INT([gl_cv_decl_HOST_NAME_MAX], [MAXHOSTNAMELEN], [\n#include <sys/types.h>\n#if HAVE_SYS_PARAM_H\n# include <sys/param.h>\n#endif\n#if HAVE_SYS_SOCKET_H\n# include <sys/socket.h>\n#endif\n#if HAVE_NETDB_H\n# include <netdb.h>\n#endif\n],\n          [dnl The system does not define MAXHOSTNAMELEN in any of the common\n           dnl headers. Use a safe fallback.\n           gl_cv_decl_HOST_NAME_MAX=256\n          ])\n      fi\n    fi\n  ])\n  if test \"$gl_cv_decl_HOST_NAME_MAX\" != yes; then\n    AC_DEFINE_UNQUOTED([HOST_NAME_MAX], [$gl_cv_decl_HOST_NAME_MAX],\n      [Define HOST_NAME_MAX when <limits.h> does not define it.])\n  fi\n])\n\n# Prerequisites of lib/gethostname.c.\nAC_DEFUN([gl_PREREQ_GETHOSTNAME], [\n  if test \"$gl_cv_w32_gethostname\" != \"yes\"; then\n    AC_CHECK_FUNCS([uname])\n  fi\n])\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}