{
  "module_name": "configure.ac",
  "hash_id": "3a8b705cb2d0c69d52d084f71b8006cd589ac45ece13af3c5864910e1a37ab4f",
  "original_prompt": "Ingested from linux-6.6.14/tools/usb/usbip/configure.ac",
  "human_readable_source": "dnl Process this file with autoconf to produce a configure script.\n\nAC_PREREQ(2.59)\nAC_INIT([usbip-utils], [2.0], [linux-usb@vger.kernel.org])\nAC_DEFINE([USBIP_VERSION], [0x00000111], [binary-coded decimal version number])\n\nCURRENT=0\nREVISION=1\nAGE=0\nAC_SUBST([LIBUSBIP_VERSION], [$CURRENT:$REVISION:$AGE], [library version])\n\nAC_CONFIG_SRCDIR([src/usbipd.c])\nAC_CONFIG_HEADERS([config.h])\n\nAM_INIT_AUTOMAKE([foreign])\nLT_INIT\n\n# Silent build for automake >= 1.11\nm4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])\n\nAC_SUBST([EXTRA_CFLAGS], [\"-Wall -Werror -Wextra -std=gnu99\"])\n\n# Checks for programs.\nAC_PROG_CC\nAC_PROG_INSTALL\nAC_PROG_MAKE_SET\n\n# Checks for header files.\nAC_HEADER_DIRENT\nAC_HEADER_STDC\nAC_CHECK_HEADERS([arpa/inet.h fcntl.h netdb.h netinet/in.h stdint.h stdlib.h dnl\n\t\t  string.h sys/socket.h syslog.h unistd.h])\n\n# Checks for typedefs, structures, and compiler characteristics.\nAC_TYPE_INT32_T\nAC_TYPE_SIZE_T\nAC_TYPE_SSIZE_T\nAC_TYPE_UINT16_T\nAC_TYPE_UINT32_T\nAC_TYPE_UINT8_T\n\n# Checks for library functions.\nAC_FUNC_REALLOC\nAC_CHECK_FUNCS([memset mkdir regcomp socket strchr strerror strstr dnl\n\t\tstrtoul])\n\nAC_CHECK_HEADER([libudev.h],\n\t\t[AC_CHECK_LIB([udev], [udev_new],\n\t\t\t      [LIBS=\"$LIBS -ludev\"],\n\t\t\t      [AC_MSG_ERROR([Missing udev library!])])],\n\t\t[AC_MSG_ERROR([Missing /usr/include/libudev.h])])\n\n# Checks for libwrap library.\nAC_MSG_CHECKING([whether to use the libwrap (TCP wrappers) library])\nAC_ARG_WITH([tcp-wrappers],\n\t    [AS_HELP_STRING([--with-tcp-wrappers],\n\t\t\t    [use the libwrap (TCP wrappers) library])],\n\t    dnl [ACTION-IF-GIVEN]\n\t    [if test \"$withval\" = \"yes\"; then\n\t\t     AC_MSG_RESULT([yes])\n\t\t     AC_MSG_CHECKING([for hosts_access in -lwrap])\n\t\t     saved_LIBS=\"$LIBS\"\n\t\t     LIBS=\"-lwrap $saved_LIBS\"\n\t\t     AC_TRY_LINK(\n\t\t       [int hosts_access(); int allow_severity, deny_severity;],\n\t\t       [hosts_access()],\n\t\t       [AC_MSG_RESULT([yes]);\n\t\t\tAC_DEFINE([HAVE_LIBWRAP], [1],\n\t\t\t\t  [use tcp wrapper]) wrap_LIB=\"-lwrap\"],\n\t\t       [AC_MSG_RESULT([not found]); exit 1])\n\t     else\n\t\t     AC_MSG_RESULT([no]);\n\t     fi],\n\t    dnl [ACTION-IF-NOT-GIVEN]\n\t    [AC_MSG_RESULT([(default)])\n\t     AC_MSG_CHECKING([for hosts_access in -lwrap])\n\t     saved_LIBS=\"$LIBS\"\n\t     LIBS=\"-lwrap $saved_LIBS\"\n\t     AC_TRY_LINK(\n\t       [int hosts_access(); int allow_severity, deny_severity;],\n\t       [hosts_access()],\n\t       [AC_MSG_RESULT([yes]);\n\t\tAC_DEFINE([HAVE_LIBWRAP], [1], [use tcp wrapper])],\n\t       [AC_MSG_RESULT([no]); LIBS=\"$saved_LIBS\"])])\n\n# Sets directory containing usb.ids.\nAC_ARG_WITH([usbids-dir],\n\t    [AS_HELP_STRING([--with-usbids-dir=DIR],\n\t       [where usb.ids is found (default /usr/share/hwdata/)])],\n\t    [USBIDS_DIR=$withval], [USBIDS_DIR=\"/usr/share/hwdata/\"])\nAC_SUBST([USBIDS_DIR])\n\n# use _FORTIFY_SOURCE\nAC_MSG_CHECKING([whether to use fortify])\nAC_ARG_WITH([fortify],\n\t    [AS_HELP_STRING([--with-fortify],\n\t\t\t    [use _FORTIFY_SOURCE=2 option when compiling)])],\n\t\t\t    dnl [ACTION-IF-GIVEN]\n\t\t\t    [if test \"$withval\" = \"yes\"; then\n\t\t\t\tAC_MSG_RESULT([yes])\n\t\t\t\tCFLAGS=\"$CFLAGS -D_FORTIFY_SOURCE=2 -O\"\n\t\t\t     else\n\t\t\t     \tAC_MSG_RESULT([no])\n\t\t\t\tCFLAGS=\"$CFLAGS -U_FORTIFY_SOURCE\"\n\t\t\t     fi\n\t\t\t    ],\n\t\t\t    dnl [ACTION-IF-NOT-GIVEN]\n\t\t\t    [AC_MSG_RESULT([default])])\n\nAC_CONFIG_FILES([Makefile libsrc/Makefile src/Makefile])\nAC_OUTPUT\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}