{
  "module_name": "csum-copy_64.S",
  "hash_id": "4896eee32d6963e8eced40535eb8d7b7eaa1eaa01dc6d8cd58f1544ed2ff8c36",
  "original_prompt": "Ingested from linux-6.6.14/arch/x86/lib/csum-copy_64.S",
  "human_readable_source": " \n#include <linux/linkage.h>\n#include <asm/errno.h>\n#include <asm/asm.h>\n\n \n\n\t.macro source\n10:\n\t_ASM_EXTABLE_UA(10b, .Lfault)\n\t.endm\n\n\t.macro dest\n20:\n\t_ASM_EXTABLE_UA(20b, .Lfault)\n\t.endm\n\nSYM_FUNC_START(csum_partial_copy_generic)\n\tsubq  $5*8, %rsp\n\tmovq  %rbx, 0*8(%rsp)\n\tmovq  %r12, 1*8(%rsp)\n\tmovq  %r14, 2*8(%rsp)\n\tmovq  %r13, 3*8(%rsp)\n\tmovq  %r15, 4*8(%rsp)\n\n\tmovl  $-1, %eax\n\txorl  %r9d, %r9d\n\tmovl  %edx, %ecx\n\tcmpl  $8, %ecx\n\tjb    .Lshort\n\n\ttestb  $7, %sil\n\tjne   .Lunaligned\n.Laligned:\n\tmovl  %ecx, %r12d\n\n\tshrq  $6, %r12\n\tjz\t.Lhandle_tail        \n\n\tclc\n\n\t \n\t \n\t \n\t \n\t.p2align 4\n.Lloop:\n\tsource\n\tmovq  (%rdi), %rbx\n\tsource\n\tmovq  8(%rdi), %r8\n\tsource\n\tmovq  16(%rdi), %r11\n\tsource\n\tmovq  24(%rdi), %rdx\n\n\tsource\n\tmovq  32(%rdi), %r10\n\tsource\n\tmovq  40(%rdi), %r15\n\tsource\n\tmovq  48(%rdi), %r14\n\tsource\n\tmovq  56(%rdi), %r13\n\n30:\n\t \n\t_ASM_EXTABLE(30b, 2f)\n\tprefetcht0 5*64(%rdi)\n2:\n\tadcq  %rbx, %rax\n\tadcq  %r8, %rax\n\tadcq  %r11, %rax\n\tadcq  %rdx, %rax\n\tadcq  %r10, %rax\n\tadcq  %r15, %rax\n\tadcq  %r14, %rax\n\tadcq  %r13, %rax\n\n\tdecl %r12d\n\n\tdest\n\tmovq %rbx, (%rsi)\n\tdest\n\tmovq %r8, 8(%rsi)\n\tdest\n\tmovq %r11, 16(%rsi)\n\tdest\n\tmovq %rdx, 24(%rsi)\n\n\tdest\n\tmovq %r10, 32(%rsi)\n\tdest\n\tmovq %r15, 40(%rsi)\n\tdest\n\tmovq %r14, 48(%rsi)\n\tdest\n\tmovq %r13, 56(%rsi)\n\n\tleaq 64(%rdi), %rdi\n\tleaq 64(%rsi), %rsi\n\n\tjnz\t.Lloop\n\n\tadcq  %r9, %rax\n\n\t \n.Lhandle_tail:\n\t \n\tmovq %rcx, %r10\n\tandl $63, %ecx\n\tshrl $3, %ecx\n\tjz\t.Lfold\n\tclc\n\t.p2align 4\n.Lloop_8:\n\tsource\n\tmovq (%rdi), %rbx\n\tadcq %rbx, %rax\n\tdecl %ecx\n\tdest\n\tmovq %rbx, (%rsi)\n\tleaq 8(%rsi), %rsi  \n\tleaq 8(%rdi), %rdi\n\tjnz\t.Lloop_8\n\tadcq %r9, %rax\t \n\n.Lfold:\n\t \n\tmovl %eax, %ebx\n\tshrq $32, %rax\n\taddl %ebx, %eax\n\tadcl %r9d, %eax\n\n\t \n.Lhandle_7:\n\tmovl %r10d, %ecx\n\tandl $7, %ecx\n.L1:\t\t\t\t \n\tshrl $1, %ecx\n\tjz   .Lhandle_1\n\tmovl $2, %edx\n\txorl %ebx, %ebx\n\tclc\n\t.p2align 4\n.Lloop_1:\n\tsource\n\tmovw (%rdi), %bx\n\tadcl %ebx, %eax\n\tdecl %ecx\n\tdest\n\tmovw %bx, (%rsi)\n\tleaq 2(%rdi), %rdi\n\tleaq 2(%rsi), %rsi\n\tjnz .Lloop_1\n\tadcl %r9d, %eax\t \n\n\t \n.Lhandle_1:\n\ttestb $1, %r10b\n\tjz    .Lende\n\txorl  %ebx, %ebx\n\tsource\n\tmovb (%rdi), %bl\n\tdest\n\tmovb %bl, (%rsi)\n\taddl %ebx, %eax\n\tadcl %r9d, %eax\t\t \n\n.Lende:\n\ttestq %r10, %r10\n\tjs  .Lwas_odd\n.Lout:\n\tmovq 0*8(%rsp), %rbx\n\tmovq 1*8(%rsp), %r12\n\tmovq 2*8(%rsp), %r14\n\tmovq 3*8(%rsp), %r13\n\tmovq 4*8(%rsp), %r15\n\taddq $5*8, %rsp\n\tRET\n.Lshort:\n\tmovl %ecx, %r10d\n\tjmp  .L1\n.Lunaligned:\n\txorl %ebx, %ebx\n\ttestb $1, %sil\n\tjne  .Lodd\n1:\ttestb $2, %sil\n\tje   2f\n\tsource\n\tmovw (%rdi), %bx\n\tdest\n\tmovw %bx, (%rsi)\n\tleaq 2(%rdi), %rdi\n\tsubq $2, %rcx\n\tleaq 2(%rsi), %rsi\n\taddq %rbx, %rax\n2:\ttestb $4, %sil\n\tje .Laligned\n\tsource\n\tmovl (%rdi), %ebx\n\tdest\n\tmovl %ebx, (%rsi)\n\tleaq 4(%rdi), %rdi\n\tsubq $4, %rcx\n\tleaq 4(%rsi), %rsi\n\taddq %rbx, %rax\n\tjmp .Laligned\n\n.Lodd:\n\tsource\n\tmovb (%rdi), %bl\n\tdest\n\tmovb %bl, (%rsi)\n\tleaq 1(%rdi), %rdi\n\tleaq 1(%rsi), %rsi\n\t \n\tleaq -1(%rcx, %rcx), %rcx\n\trorq $1, %rcx\n\tshll $8, %ebx\n\taddq %rbx, %rax\n\tjmp 1b\n\n.Lwas_odd:\n\troll $8, %eax\n\tjmp .Lout\n\n\t \n.Lfault:\n\txorl %eax, %eax\n\tjmp  .Lout\nSYM_FUNC_END(csum_partial_copy_generic)\n",
  "logic_map": {},
  "failure_modes": [],
  "crash_correlation_map": {}
}